 ;;Solution_Test/script/PathNet -- BB Transfusion/R_BBRefAntigen/BB_REF_GET_ANTIGENS.PRG Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************
        Request #:              225806

        Product:                PathNet
        Product Team:           Blood Bank
        
        Program purpose:        Retrieve all antigens from codeset 1612

        Executing from:         include\BBRefAntigens.cls
        Special Notes:          NONE 
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     001 05/04/00 Lawrence Dsouza      Initial Release                     *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

DROP PROGRAM   bb_ref_get_antigens:dba GO
CREATE PROGRAM bb_ref_get_antigens:dba

/*
RECORD request
(
  1  active_flag        = i2
)
*/

RECORD reply
( 
  1  Antigens[*]
     2 antigen_cd     = f8
     2 display        = vc
     2 description    = vc
     2 opposite_cd    = f8
     2 active_ind     = i2
     
%i cclsource:status_block.inc
)

/***********************************************************************
*   Script                                                             *
***********************************************************************/
DECLARE nCnt = i2

SET nCnt = 0

SET sErrorMsg = FILLSTRING(255," ")
SET sError_Check = ERROR(sErrorMsg,1)

SET STAT = ALTERLIST(reply->Antigens,10)

SELECT 
   IF (request->active_flag = 2)
      PLAN cv 
         where cv.code_set = 1612
           and cv.cdf_meaning in ("+","-")
      JOIN cve 
         where cve.code_value = cv.code_value
           and cve.field_name = "Opposite"  
           and cve.code_set = cv.code_set
   ELSE 
      PLAN cv 
         where cv.code_set = 1612
           and cv.cdf_meaning in ("+","-")
           and cv.active_ind = request->active_flag
      JOIN cve 
         where cve.code_value = cv.code_value
           and cve.field_name = "Opposite"
           and cve.code_set = cv.code_set
   ENDIF

INTO "nl:" 

FROM 
   code_value cv
   ,code_value_extension cve 

HEAD cv.code_value
   nCnt = nCnt + 1
   IF (mod(nCnt, 10) = 1 and nCnt != 1) 
      stat = ALTERLIST(reply->Antigens,nCnt + 10)
   ENDIF  

DETAIL
   reply->Antigens[nCnt].antigen_cd     = cv.code_value
   reply->Antigens[nCnt].display        = cv.display
   reply->Antigens[nCnt].description    = cv.description
   reply->Antigens[nCnt].active_ind     = cv.active_ind
   IF (TRIM(cve.field_value) = "")
      reply->Antigens[nCnt].opposite_cd = 0
   ELSE
      reply->Antigens[nCnt].opposite_cd = cnvtreal(cve.field_value)
   ENDIF
WITH NOCOUNTER               

SET STAT = ALTERLIST(reply->Antigens,nCnt)               
SET sError_Check = ERROR(sErrorMsg,0)   ;get error message

IF (sError_Check = 0)
   IF (CURQUAL > 0)
      SET reply->status_data->status = "S"  
   ELSE
      SET reply->status_data->status = "Z"
   ENDIF
ELSE
   SET reply->status_data->status = "F"
   SET reply->status_data->subeventstatus[1]->TargetObjectValue = sErrorMsg
ENDIF

#EXIT_SCRIPT        
END GO

;Generated by GNU enscript 1.6.4.
