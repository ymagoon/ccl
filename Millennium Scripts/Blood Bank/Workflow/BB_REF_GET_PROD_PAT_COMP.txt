 ;;Solution_Test/script/PathNet -- BB Transfusion/R_BBRefPatProdCompat/BB_REF_GET_PROD_PAT_COMP.PRG Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************
        Request #:              225808

        Product:                Pathnet                        
        Product Team:           Blood Bank
        
        Program purpose:        Retrieve product patient compatibility info

        Executing from:         include\BBRefPPProductPatient.cls
        Special Notes:          
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     001 06/24/00 Lawrence Dsouza      Initial Release                     *
;     002 01/09/02 Sasi Gandham         Add field to Order By for Head      *
;     003 02/12/02 Chris Noe            CAPEP00011552: Added change to allow*
;                                       for the retrieval of compatibility  *
;                                       options without the need of patient *
;                                       abo/rh compatibilities.             *
;     004 06/08/05 Jim Jensen           Return the bbd_no_gt_dir_prsn_flag  *
;                                       in the reply structure.             *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************
DROP PROGRAM   bb_ref_get_prod_pat_comp: DBA GO
CREATE PROGRAM  bb_ref_get_prod_pat_comp: DBA

/*
Record request
(  
  1 product_type_cd   = f8
)
*/

RECORD  Reply
( 
   1  product_aborh[*]
      2  product_type_cd         = f8
      2  product_aborh_cd        = f8
      2  no_gt_on_prsn_flag      = i2 
      2  no_ad_on_prsn_flag      = i2
      2  aborh_option_flag       = i2
      2  patient_aborh[*] 
         3  prsn_aborh_cd        = f8
         3  warn_ind             = i2
%i cclsource:status_block.inc
)
/***********************************************************************
*   Script                                                             *
***********************************************************************/
SET nProductAborhCnt = 0
SET nPatientAborhCnt = 0

SET sErrorMsg = FILLSTRING(255," ")
SET sError_Check = ERROR(sErrorMsg,1)

SET stat = ALTERLIST(reply->product_aborh, 10)

SELECT 
   IF (request->product_type_cd = 0)
      PLAN pa   
        WHERE pa.active_ind = 1
      JOIN ppa 
        WHERE ppa.product_cd = OUTERJOIN(pa.product_cd)
          AND ppa.prod_aborh_cd = OUTERJOIN(pa.product_aborh_cd)
          AND ppa.active_ind = OUTERJOIN(1)
   ELSE
      PLAN pa 
        WHERE pa.product_cd  = request->product_type_cd   
          AND pa.active_ind = 1
      JOIN ppa 
        WHERE ppa.product_cd = OUTERJOIN(pa.product_cd)
          AND ppa.prod_aborh_cd = OUTERJOIN(pa.product_aborh_cd)
          AND ppa.active_ind = OUTERJOIN(1)
   ENDIF
INTO "nl:"
   parent_record_combination = BUILD(pa.product_cd, pa.product_aborh_cd)
FROM
  product_aborh  pa
  ,product_patient_aborh  ppa

order by parent_record_combination

HEAD parent_record_combination
   IF (nProductAborhCnt > 0)
      stat = ALTERLIST(reply->product_aborh[nProductAborhCnt].patient_aborh, nPatientAborhCnt)
   ENDIF

   nProductAborhCnt = nProductAborhCnt + 1 
   IF (MOD(nProductAborhCnt, 10) = 1 AND nProductAborhCnt != 1)
      stat = ALTERLIST(reply->product_aborh, nProductAborhCnt + 9)
   ENDIF

   reply->product_aborh[nProductAborhCnt].product_type_cd         = pa.product_cd
   reply->product_aborh[nProductAborhCnt].product_aborh_cd        = pa.product_aborh_cd
   reply->product_aborh[nProductAborhCnt].no_gt_on_prsn_flag      = pa.no_gt_on_prsn_flag
   reply->product_aborh[nProductAborhCnt].no_ad_on_prsn_flag      = pa.no_gt_autodir_prsn_flag
   reply->product_aborh[nProductAborhCnt].aborh_option_flag       = pa.aborh_option_flag


   stat = ALTERLIST(reply->product_aborh[nProductAborhCnt].patient_aborh, 5)
   nPatientAborhCnt = 0

DETAIL
   nPatientAborhCnt = nPatientAborhCnt + 1
   IF (MOD(nPatientAborhCnt, 5) = 1 AND nPatientAborhCnt != 1)
      stat = ALTERLIST(reply->product_aborh[nProductAborhCnt].patient_aborh, nPatientAborhCnt + 4)
   ENDIF
   
   reply->product_aborh[nProductAborhCnt].patient_aborh[nPatientAborhCnt]->prsn_aborh_cd = ppa.prsn_aborh_cd  
   reply->product_aborh[nProductAborhCnt].patient_aborh[nPatientAborhCnt]->warn_ind = ppa.warn_ind

WITH NOCOUNTER  

SET stat = ALTERLIST(reply->product_aborh[nProductAborhCnt].patient_aborh, nPatientAborhCnt)
SET stat = ALTERLIST(reply->product_aborh, nProductAborhCnt)

SET sError_Check = ERROR(sErrorMsg,0)   ;get error message

IF (sError_Check = 0)
   IF (nProductAborhCnt > 0)
      SET reply->status_data->status = "S"
   ELSE
      SET reply->status_data->status = "Z"
   ENDIF
ELSE
   SET reply->status_data->status = "F"
   SET reply->status_data->subeventstatus[1]->TargetObjectValue = sErrorMsg
ENDIF
#EXIT_SCRIPT
     
END GO

;Generated by GNU enscript 1.6.4.
