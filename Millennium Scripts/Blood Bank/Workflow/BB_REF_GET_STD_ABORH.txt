 ;;Solution_Test/script/PathNet -- BB Transfusion/R_BBRefStdAboRh/BB_REF_GET_STD_ABORH.PRG Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************
        Request #:              225804

        Product:                PathNet
        Product Team:           Blood Bank
        
        Program purpose:        Retrieve all ABO and RH information

        Executing from:         include\BBRefStdAboRh.cls
        Special Notes:          NONE 
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     001 05/04/00 Lawrence Dsouza      Initial Release                     *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

DROP PROGRAM   bb_ref_get_std_aborh:dba GO
CREATE PROGRAM bb_ref_get_std_aborh:dba

/*
RECORD request
(
 
  1  active_flag        = i2
)
*/

RECORD reply
( 
  1  AboRh[*]
     2 aborh_cd     = f8
     2 display      = vc
     2 meaning      = c40
     2 description  = vc
     2 abo_cd       = f8
     2 rh_cd        = f8
     
%i cclsource:status_block.inc
)

/***********************************************************************
*   Script                                                             *
***********************************************************************/
DECLARE nCnt = i2

SET nCnt = 0

SET sErrorMsg = FILLSTRING(255," ")
SET sError_Check = ERROR(sErrorMsg,1)

SET STAT = ALTERLIST(reply->AboRh,10)

SELECT 
   IF (request->active_flag = 2)
      PLAN cv 
        where cv.code_set = 1640
      JOIN cve 
        where cve.code_value = cv.code_value
          and cve.field_name in ("ABOOnly_cd", "RhOnly_cd")
          and cve.code_set = cv.code_set 
   ELSE 
      PLAN cv 
        where cv.code_set = 1640
          and cv.active_ind = request->active_flag
      JOIN cve 
        where cve.code_value = cv.code_value
          and cve.field_name in ("ABOOnly_cd", "RhOnly_cd")
          and cve.code_set = cv.code_set 
   ENDIF

INTO "nl:" 

FROM 
   code_value cv
   ,code_value_extension cve 

ORDER by cv.code_value

HEAD cv.code_value
   nCnt = nCnt + 1
   IF (mod(nCnt, 10) = 1 and nCnt != 1) 
      stat = ALTERLIST(reply->AboRh,nCnt + 10)
   ENDIF  

DETAIL
   reply->AboRh[nCnt].aborh_cd    = cv.code_value
   reply->AboRh[nCnt].display     = cv.display
   reply->AboRh[nCnt].meaning     = cv.cdf_meaning
   reply->AboRh[nCnt].description = cv.description
   IF (cve.field_name = "ABOOnly_cd") 
      IF (TRIM(cve.field_value) = "")
         reply->AboRh[nCnt].abo_cd      = 0
      ELSE
         reply->AboRh[nCnt].abo_cd      = cnvtreal(cve.field_value)
      ENDIF
   ELSE   
      IF (TRIM(cve.field_value) = "")
         reply->AboRh[nCnt].rh_cd       = 0
      ELSE
         reply->AboRh[nCnt].rh_cd       = cnvtreal(cve.field_value)
      ENDIF
   ENDIF

WITH NOCOUNTER               

SET STAT = ALTERLIST(reply->AboRh,nCnt)               

SET sError_Check = ERROR(sErrorMsg,0)   ;get error message

IF (sError_Check = 0)
   IF (CURQUAL > 0)
      SET reply->status_data->status = "S"
   ELSE
      SET reply->status_data->status = "Z"
   ENDIF
ELSE
   SET reply->status_data->status = "F"
   SET reply->status_data->subeventstatus[1]->TargetObjectValue = sErrorMsg
ENDIF

#EXIT_SCRIPT        
END GO

;Generated by GNU enscript 1.6.4.
