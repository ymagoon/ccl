 ;;Solution_Test/script/PathNet -- BB Transfusion/Antibodyantigenreltool/bbt_add_transfusion_reqs.prg Turn on black mode


/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       bbt_add_transfusion_reqs.prg
        Object name:            bbt_add_transfusion_reqs
        Request #:              225040

        Product:                Blood Bank Transfusion
        Product Team:           Blood Bank Transfusion
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Used to add transfusion requirements including
                                antibodies to the transfusion_requirements
                                table.

        Tables read:            none
        Tables updated:         transfusion_requirements, code_value
        Executing from:         Antibody Tool

        Special Notes:          none

******************************************************************************/

 
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     000 04/01/96 Teresa Davis         Initial Release                     *
;     001 04/22/97 Martin Olson         commented out reference to the pri- *
;                                       mary indicator.  Left in request    *
;     002 06/18/98 Amy Therrien         Added significance indicator        *
;~DE~************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

drop program bbt_add_transfusion_reqs:dba go
create program bbt_add_transfusion_reqs:dba


/**************************************************************
*        Request Record
**************************************************************/
/*
record request
( 1 code_set            = i4
  1 qual(1)
    2 cdf_meaning = c12
    2 display = c40
    2 primary_ind = i2
    2 display_key = c40
    2 description       = vc
    2 definition = vc
    2 anti_d_ind = i2
    2 collation_seq     = i4
    2 active_type_cd    = f8
    2 active_ind        = i2
    2 trans_req_desc    = vc
    2 rh_cd             = f8
    2 warn_ind          = i2
    2 override_ind      = i2
    2 significance_ind  = i2 ;002 AT4131
)
*/
/*
record reqinfo
( 1 commit_ind  = i2
  1 updt_id     = f8
  1 position_cd = f8
  1 updt_app    = i4
  1 updt_task   = i4
  1 updt_req    = i4
  1 updt_applctx= i4
) 
*/
/***************************************************************
*        Reply Record
***************************************************************/

record reply
( 1 qual[1]
    2 code_value        = f8
    2 display           = c40
    2 antid_updt_cnt    = i4
    2 relationship_id   = f8
  1 status_data
    2 status = c1
    2 subeventstatus[1]
      3 OperationName = c15
      3 OperationStatus = c1
      3 TargetObjectName = c15
      3 TargetObjectValue = vc
)

/******************************************************
*        Initialize Variables
******************************************************/
set reply->status_data->status  = "F"
set failures                    = 0
set nbr_to_add = size(request->qual,5)
set y = 1
set count1 = 0
set next_code = 0.0


; ** Get "Authorized" data_status_cd code_value
%i cclsource:bbt_get_auth_data_status_cd.inc

/******************************************************
*        Instructions
******************************************************/

#start_loop
for (y = y to nbr_to_add)

  /************************/
  /* Get sequence number  */
  /************************/
  execute cpm_next_code

  /************************/
  /*    Insert record     */
  /************************/
  insert into code_value c set
    c.code_value = next_code,
    c.code_set = request->code_set,
    c.cdf_meaning = if (request->qual[y]->cdf_meaning > " ")
                       request->qual[y]->cdf_meaning
                    else
                       NULL
                    endif,
;    c.cdf_meaning = request->qual[y]->cdf_meaning,
;    c.primary_ind = request->qual[y]->primary_ind,
    c.display = request->qual[y]->display,
    c.display_key = trim(cnvtupper(cnvtalphanum(request->qual[y]->display))),
    c.description = request->qual[y]->description,
    c.definition = request->qual[y]->definition,
    c.collation_seq = request->qual[y]->collation_seq,
    c.active_type_cd = 0.0,
    c.active_ind = request->qual[y]->active_ind,
    c.active_dt_tm = if (request->qual[y]->active_ind = 1)
                       cnvtdatetime(curdate,curtime3)
                     else
                       NULL
                     endif,
    c.inactive_dt_tm = if (request->qual[y]->active_ind = 0)
                         cnvtdatetime(curdate,curtime3)
                       else
                         NULL
                       endif,
    c.updt_dt_tm = cnvtdatetime(curdate,curtime3),
    c.updt_id = reqinfo->updt_id,
    c.updt_cnt = 0,
    c.updt_task = reqinfo->updt_task,
    c.updt_applctx = reqinfo->updt_applctx,
    c.begin_effective_dt_tm = cnvtdatetime(curdate,curtime3),
    c.end_effective_dt_tm = cnvtdatetime("31-DEC-2100"),
    c.data_status_cd           = auth_data_status_cd,
    c.data_status_dt_tm        = cnvtdatetime(curdate,curtime3),
    c.data_status_prsnl_id     = reqinfo->updt_id,
    c.active_status_prsnl_id   = reqinfo->updt_id
             
  with nocounter 


  /************************/
  /*   Check status       */
  /************************/
  if (curqual = 0)
     set failures = failures + 1
     if (failures > 1)
        set stat = alter(reply->status_data->subeventstatus,failures)
     endif
     set reply->status_data->subeventstatus[failures]->operationstatus = "F"
     set reply->status_data->subeventstatus[failures]->targetobjectvalue = 
                                        request->qual[y]->display  
     /* set code_value to 0 if the code was not added */
     set reply->qual[count1]->code_value = 0.0
     set reply->qual[count1]->display = request->qual[y]->display

     rollback
     set y = y + 1
     go to start_loop
  else
    set count1 = count1 + 1
    set stat = alter(reply->qual,count1)
    set reply->qual[count1]->code_value = next_code
    set reply->qual[count1]->display = request->qual[y]->display
  endif

  /****************************
  *    Insert Antibody Row
  ****************************/

  insert into  transfusion_requirements t
   set
     t.codeset                   = request->code_set,
     t.requirement_cd             = next_code,
     t.description                = trim(request->qual[y]->trans_req_desc),
     t.chart_name                 = trim(request->qual[y]->trans_req_desc),
     t.anti_d_ind                 = request->qual[y]->anti_d_ind,
     t.active_ind                 = request->qual[y]->active_ind,
     t.active_status_cd           = reqdata->active_status_cd,
     t.active_status_prsnl_id     = reqinfo->updt_id,
     t.active_status_dt_tm        = cnvtdatetime(curdate,curtime3),
     t.updt_dt_tm                 = cnvtdatetime(curdate,curtime3),
     t.updt_id                    = reqinfo->updt_id,
     t.updt_task                  = reqinfo->updt_task,
     t.updt_applctx               = reqinfo->updt_applctx,
     t.significance_ind		  = request->qual[y]->significance_ind, ;200 AT4131
     t.updt_cnt                   = 0
  with nocounter

  if (curqual = 0)
     ;log error

     set failures = failures + 1
     if (failures > 1)
        set stat = alter(reply->status_data->subeventstatus,failures)
     endif
     set reply->status_data->subeventstatus[failures]->operationstatus = "F"
     set reply->status_data->subeventstatus[failures]->targetobjectvalue = 
                                        request->qual[y]->display  
     /* set code_value to 0 if the code was not added */
     set reply->qual[count1]->code_value = 0.0
     set reply->qual[count1]->display = request->qual[y]->display

     rollback
     set y = y + 1
     go to start_loop
  endif
  if (request->qual[y]->anti_d_ind = 1)
%i cclsource:bbt_get_pathnet_seq.inc
     insert into trans_req_r t
     set
        t.relationship_id            = new_pathnet_seq,
        t.requirement_cd             = next_code,
        t.special_testing_cd         = request->qual[y].rh_cd,
        t.warn_ind                   = request->qual[y].warn_ind,
        t.allow_override_ind         = request->qual[y].override_ind,
        t.active_ind                 = request->qual[y].active_ind,
        t.active_status_cd           = reqdata->active_status_cd,
        t.active_status_prsnl_id     = reqinfo->updt_id,
        t.active_status_dt_tm        = cnvtdatetime(curdate,curtime3),
        t.updt_dt_tm                 = cnvtdatetime(curdate,curtime3),
        t.updt_id                    = reqinfo->updt_id,
        t.updt_task                  = reqinfo->updt_task,
        t.updt_applctx               = reqinfo->updt_applctx,
        t.updt_cnt                   = 0
     with nocounter
     if (curqual = 0)
        ;log error

        set failures = failures + 1
        if (failures > 1)
           set stat = alter(reply->status_data->subeventstatus,failures)
        endif
        set reply->status_data->subeventstatus[failures]->operationstatus = "F"
        set reply->status_data->subeventstatus[failures]->targetobjectvalue = 
                                        request->qual[y]->display  
        /* set code_value to 0 if the code was not added */
        set reply->qual[count1]->code_value = 0.0
        set reply->qual[count1]->display = request->qual[y]->display

        rollback
        set y = y + 1
        go to start_loop
     else
        set reply->qual[count1]->antid_updt_cnt = 0
        set reply->qual[count1]->relationship_id = new_pathnet_seq
        commit
     endif
  else
     commit
  endif
endfor

#exit_script
if (failures > 0)
   set reply->status_data->status = "T"
else
   set reply->status_data->status = "S"
endif

end go

;Generated by GNU enscript 1.6.4.
