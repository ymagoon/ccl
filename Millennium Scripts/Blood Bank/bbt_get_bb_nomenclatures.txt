 ;;Solution_Test/script/PathNet -- BB Transfusion/Bbt_Specialtestingtool/BBT_GET_BB_NOMENCLATURES.PRG Turn on black mode

/*~BB~************************************************************************
  *                                                                      *
  *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
  *                              Technology, Inc.                        *
  *       Revision      (c) 1984-1997 Cerner Corporation                 *
  *                                                                      *
  *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
  *  This material contains the valuable properties and trade secrets of *
  *  Cerner Corporation of Kansas City, Missouri, United States of       *
  *  America (Cerner), embodying substantial creative efforts and        *
  *  confidential information, ideas and expressions, no part of which   *
  *  may be reproduced or transmitted in any form or by any means, or    *
  *  retained in any storage or retrieval system without the express     *
  *  written permission of Cerner.                                       *
  *                                                                      *
  *  Cerner is a registered mark of Cerner Corporation.                  *
  *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************

        Source file name:       bbt_get_bb_nomenclatures.prg       
        Object name:            bbt_get_bb_nomenclatures
        Request #:              225462    

        Product:                PathNet                
        Product Team:           BB Transfusion
        HNA Version:            500
        CCL Version:            4.0

        Program purpose:        Get all blood bank nomenclatures (short string
                                field on the nomenclature table) & nomenclature ids
                                corresponding to them. 

        Tables read:            code_value, nomenclature

        Tables updated:         <None>

        Executing from:         VB

        Special Notes:          
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;    *000 10/23/97 Badari Kopparapu     Initial Release                     *
;     001 05/10/99 Doug Saus            Replaced code_value select with uar *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

drop program bbt_get_bb_nomenclatures:dba go
create program bbt_get_bb_nomenclatures:dba

/*
record request
(

)
*/

record reply
(
 1  qual[*]
     2  nomenclature_id = f8
     2  display         = c40
%i cclsource:status_block.inc
)

/*
 * Initialize global variables
 */
    set count1 = 0
    set failed = "T"
    set reply->status_data->status = "F"
    set cv_cnt = 0
    set source_cd = 0.0 ;001
;001 begin
/* Get code_value for BLOOD BANK from code_value table */
    set cdf_meaning = fillstring(12," ")
    set cdf_meaning = "BLOOD BANK"
    set stat = uar_get_meaning_by_codeset(400,cdf_meaning,1,source_cd)
    call echo(source_cd)
;001 end

/* Get nomenclature id and description (short string) 
                                     from blood bank */
 
select into "nl:"
    n.nomenclature_id,
    n.short_string
from 
    nomenclature n
where
    n.source_vocabulary_cd = source_cd
order by
    n.short_string
        
head report
    cv_cnt = 0 
    stat = alterlist(reply->qual, 10)
 
head n.short_string
    cv_cnt = cv_cnt + 1
    if(mod(cv_cnt, 10) = 1 and cv_cnt != 1)
        stat = alterlist(reply->qual, cv_cnt + 9)
    endif
    reply->qual[cv_cnt]->nomenclature_id = n.nomenclature_id
    reply->qual[cv_cnt]->display         =  n.short_string

foot report
    stat = alterlist(reply->qual, cv_cnt)
    failed = "F"

with nocounter


set count1 = count1 + 1
if (count1 > 1)
    set stat = alterlist(reply->status_data->subeventstatus, count1)
endif
set reply->status_data->subeventstatus[count1]->OperationName       = "Get Blood Bank Nomenclatures"
set reply->status_data->subeventstatus[count1]->TargetObjectName    = "bbt_get_bb_nomenclatures"

if (failed = "F")
    if (size(reply->qual, 5) > 0)
        set reply->status_data->status = "S"
        set reply->status_data->subeventstatus[count1]->OperationStatus     = "S"
        set reply->status_data->subeventstatus[count1]->TargetObjectValue   = 
            "SUCCESS"
    else
        set reply->status_data->status = "Z"
        set reply->status_data->subeventstatus[count1]->OperationStatus     = "S"
        set reply->status_data->subeventstatus[count1]->TargetObjectValue   = 
            "ZERO"
    endif
else
    set reply->status_data->status = "F"
    set reply->status_data->subeventstatus[count1]->OperationStatus     = "F"
    set reply->status_data->subeventstatus[count1]->TargetObjectValue   = 
        "Select on code_value/code_value_extension failed"
endif


end go

;Generated by GNU enscript 1.6.4.
