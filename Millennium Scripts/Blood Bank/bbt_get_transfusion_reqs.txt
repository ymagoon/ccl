 ;;Solution_Test/script/PathNet -- BB Transfusion/Antibodyantigenreltool/bbt_get_transfusion_reqs.prg Turn on black mode


/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       bbt_get_transfusion_reqs
        Object name:            bbt_get_transfusion_reqs
        Request #:              225042

        Product:                Blood Bank Transfusion
        Product Team:           Blood Bank Transfusion
        HNA Version:            500
        CCL Version:            4.0

        Program purpose:        Used to get all transfusion requirements
                                and corresponding relationship information

        Tables read:            transfusion_requirements
                                trans_req_r
                                excld_trans_req_prod_cat_r
        Tables updated:         none
        Executing from:         Antigen Negative Tool

        Special Notes:          none

******************************************************************************/


;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     000 04/03/96 Teresa Davis         Initial Release                     *
;     001 01/07/98 Victoria Savariyar   Commented the join with             *
;                                       Transfusion_Requirements table      *
;                                       from getting just the antibodies    *
;                                       with anti_d_ind = 1. This will allow*
;                                       it to return the antibodies with    *
;                                       other antigens.                     *
;     002 06/18/98 Amy Therrien         Added significance indicator        *
;     002 01/08/02 Sasi Gandham         Add order by clause for Head field  *
;     003 09/03/13 Fitsum Abebe         Support for making transfusion      *
;                                       requirements inapplicable for a     *
;                                       specific set of product categories. *
;~DE~************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

drop program bbt_get_transfusion_reqs:dba go
create program bbt_get_transfusion_reqs:dba

/**************************************************************
*        Request Record
**************************************************************/
/*
record request
( 1 request_type_ind  valid values 1 get antibodies and antigen negatives
                                   0 get antibodies only
  1 requirement_cd = f8
  1 code_set       = f8
)
*/

/*
record reqinfo
( 1 commit_ind  = i2
  1 updt_id     = f8
  1 position_cd = f8
  1 updt_app    = i4
  1 updt_task   = i4
  1 updt_req    = i4
  1 updt_applctx= i4
)
*/
/***************************************************************
*        Reply Record
***************************************************************/

record reply
( 1 qual(1)
    2 code_set                  = i4
    2 code_value                = f8
    2 display                   = c40
    2 description               = c40
    2 definition                = vc
    2 anti_d_ind                = i2
    2 active_type_cd            = f8
    2 active_ind                = i2
    2 significance_ind          = i2 ;002 AT4131
    2 updt_cnt                  = i4
    2 transreq_desc             = vc
    2 transreq_updtcnt          = i4
    2 reltn_cnt                 = i4
    2 qual2(1)
      3 relationship_id         = f8
      3 special_testing_cd      = f8
      3 special_testing_disp    = c40
      3 warn_ind                = i2
      3 override_ind            = i2
      3 spectst_active_ind      = i2
      3 spectst_updt_cnt        = i4
    2 excluded_product_cat_cnt  = i4
    2 excluded_product_cat_qual[*]
      3 excld_trans_req_prod_cat_r_id = f8
      3 product_cat_cd          = f8
      3 product_cat_disp        = vc
      3 active_ind              = i2
      3 updt_cnt                = i4
  1 status_data
    2 status = c1
    2 subeventstatus[1]
      3 OperationName = c15
      3 OperationStatus = c1
      3 TargetObjectName = c15
      3 TargetObjectValue = vc
)

/******************************************************
*        Initialize Variables
******************************************************/
set reply->status_data[1]->status = "F"
set anti_cnt = 0
set neg_cnt = 0
set stat = alter(reply->qual,20)
set max_anti = 20
set max_neg = 1
set cnt = 0
set TRANSFUSION_REQ_CS = 1611
/******************************************************
*        Instructions
******************************************************/
select into "nl:"
     c.*,
     t.*,
     n.*
from transfusion_requirements t,
     code_value c,
     (dummyt d with seq = 1),
     Trans_req_r n

plan t where (request->requirement_cd > 0
         and t.requirement_cd = request->requirement_cd)
          or (request->requirement_cd = 0
         and t.requirement_cd > 0
             and (request->code_set = 0
              or request->code_set = t.codeset))
join c
   where (t.requirement_cd = c.code_value)
join d
   where (d.seq = 1)
join n
   where    (request->request_type_ind = 1
                and  t.requirement_cd = n.requirement_cd)
            or (request->request_type_ind = 0
                and  t.requirement_cd = n.requirement_cd
                and  n.active_ind = 1)
                
order by t.requirement_cd                
                
head t.requirement_cd
   neg_cnt = 0
   anti_cnt = anti_cnt + 1
   if (anti_cnt > max_anti)
      max_anti = anti_cnt
      stat = alter(reply->qual,max_anti)
   endif
   reply->qual[anti_cnt].code_set                  = t.codeset
   reply->qual[anti_cnt].code_value                = c.code_value
   reply->qual[anti_cnt].display                   = c.display
   reply->qual[anti_cnt].description               = c.description
   reply->qual[anti_cnt].definition                = c.definition
   reply->qual[anti_cnt].active_type_cd            = c.active_type_cd
   reply->qual[anti_cnt].active_ind                = c.active_ind
   reply->qual[anti_cnt].updt_cnt                  = c.updt_cnt
   reply->qual[anti_cnt].transreq_desc             = t.description
   reply->qual[anti_cnt].anti_d_ind                = t.anti_d_ind
   reply->qual[anti_cnt].transreq_updtcnt          = t.updt_cnt
   reply->qual[anti_cnt].significance_ind	   = t.significance_ind

detail
   if (n.special_testing_cd > 0)
      neg_cnt = neg_cnt + 1
      if (neg_cnt > max_neg)
         max_neg = neg_cnt
         stat = alter(reply->qual->qual2,max_neg)
      endif
      reply->qual[anti_cnt].qual2[neg_cnt].relationship_id = n.relationship_id
      reply->qual[anti_cnt].qual2[neg_cnt].special_testing_cd
        = n.special_testing_cd
      reply->qual[anti_cnt].qual2[neg_cnt].warn_ind = n.warn_ind
      reply->qual[anti_cnt].qual2[neg_cnt].override_ind
        = n.allow_override_ind
      reply->qual[anti_cnt].qual2[neg_cnt].spectst_active_ind = n.active_ind
      reply->qual[anti_cnt].qual2[neg_cnt].spectst_updt_cnt = n.updt_cnt
   endif

foot t.requirement_cd
   reply->qual[anti_cnt].reltn_cnt            = neg_cnt


with nocounter, outerjoin=d

if (curqual = 0)
   ;log error
   set reply->status_data->status = "Z"
else
   set reply->status_data->status = "S"
endif

/*************************************************************
* If a specific transfusion requirement is requested, retrieve
* product categories that are excluded/inapplicable.
**************************************************************/
if(request->requirement_cd > 0 and size(reply->qual, 5) > 0 and  reply->qual[1].code_set = TRANSFUSION_REQ_CS)

    select into "nl:"
    from excld_trans_req_prod_cat_r etp  
    plan etp where etp.requirement_cd = request->requirement_cd and etp.active_ind = 1
    head report
      cnt = 0
    detail
      cnt = cnt + 1
      if (cnt > size(reply->qual[1].excluded_product_cat_qual, 5))         
         stat = alterlist(reply->qual[1].excluded_product_cat_qual, cnt)
      endif     
      reply->qual[1].excluded_product_cat_qual[cnt].excld_trans_req_prod_cat_r_id = etp.excld_trans_req_prod_cat_r_id  
      reply->qual[1].excluded_product_cat_qual[cnt].product_cat_cd = etp.product_cat_cd
      reply->qual[1].excluded_product_cat_qual[cnt].active_ind = etp.active_ind
      reply->qual[1].excluded_product_cat_qual[cnt].updt_cnt = etp.updt_cnt
    foot report
      reply->qual[1].excluded_product_cat_cnt = cnt
    with nocounter
    
endif

end go

;Generated by GNU enscript 1.6.4.
