 ;;Solution_Test/script/PathNet -- BB Transfusion/BBT_UPDATETRANSREQ/BBT_GET_TRANS_REQUIRE.PRG Turn on black mode

/*~BB~************************************************************************
  *                                                                      *
  *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
  *                              Technology, Inc.                        *
  *       Revision      (c) 1984-1995 Cerner Corporation                 *
  *                                                                      *
  *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
  *  This material contains the valuable properties and trade secrets of *
  *  Cerner Corporation of Kansas City, Missouri, United States of       *
  *  America (Cerner), embodying substantial creative efforts and        *
  *  confidential information, ideas and expressions, no part of which   *
  *  may be reproduced or transmitted in any form or by any means, or    *
  *  retained in any storage or retrieval system without the express     *
  *  written permission of Cerner.                                       *
  *                                                                      *
  *  Cerner is a registered mark of Cerner Corporation.                  *
  *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************

        Source file name:       BBT_GET_TRANS_REQUIRE.PRG
        Object name:            bbt_get_trans_require
        Request #:              225219

        Product:                Blood Bank Transfusion
        Product Team:           PathNet
        HNA Version:            500
        CCL Version:            4.0

        Program purpose:        Return all active transfusion requirements for
                                the person and encounter specified

        Tables read:            PERSON_TRANS_REQ

        Tables updated:         N/A

        Executing from:         Update Transfusion Requirements

        Special Notes:          N/A
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     001 10/29/96 Linda DeJong         Initial Release                     *
;     002 05/26/98 Jon Weekes/Matt Benson  Removed qualification on encntr_id
;     003 03/19/12 Gayatri Samala     	added code to get added and removed *
;										details for transfusion reqs to fix *
; 										for CR 1-324245021                  *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

        drop program bbt_get_trans_require:dba go
        create program bbt_get_trans_require:dba

/****************************************************************************
*       Request record                                                      *
*****************************************************************************/
        /*
        record request
        (
          1  person_id = f8
          1  encntr_id = f8
        )
        */

/****************************************************************************
*       Reply record                                                        *
*****************************************************************************/
        record reply
        (
          1  qual[*]
             2  requirement_cd = f8
             2  requirement_disp = c40
             2  updt_cnt = i4
             2  person_trans_req_id = f8
             2  added_user_name = c20
             2  added_dt_tm = dq8
             2  removed_user_name = c20
             2  removed_dt_tm = dq8
             2  active_ind = i2
%I CCLSOURCE:STATUS_BLOCK.INC
        )

/****************************************************************************
*       Initialize Variables                                                *
*****************************************************************************/
        set err_cnt = 0 
        set require_cnt = 0
        set reply->status_data.status = "F"

/****************************************************************************
* query the person_trans_req and prsnl tables to get the added and removed  *
  requirement details for a given person_id                                 *
*****************************************************************************/
  		select into "nl:"
        	p.requirement_cd,
            p.person_id,
            p.updt_cnt,
            pr.username
 
        from
            person_trans_req p,
            prsnl pr
 
        plan p where p.person_id = request->person_id
 
		join pr where pr.person_id in (p.updt_id, p.added_prsnl_id) and pr.person_id > 0
 
		order by p.person_trans_req_id
 
		head report
			require_cnt = 0
			err_cnt = 0
 
		head p.person_trans_req_id
			require_cnt = require_cnt + 1
			stat = alterlist(reply->qual, require_cnt)
			reply->qual[require_cnt].requirement_cd = p.requirement_cd
        	reply->qual[require_cnt].updt_cnt = p.updt_cnt
        	reply->qual[require_cnt].person_trans_req_id = p.person_trans_req_id
        	reply->qual[require_cnt].active_ind = p.active_ind
 
		detail
			; updt_id and updt_dt_tm will be used to get removed requirement details
			if (pr.person_id = p.updt_id)
				if (p.active_ind = 0)
					reply->qual[require_cnt].removed_user_name = pr.username
					reply->qual[require_cnt].removed_dt_tm = cnvtdatetime(p.updt_dt_tm)
				else
					; this is for old data
					if (p.added_prsnl_id = 0)
						reply->qual[require_cnt].added_user_name = pr.username
						reply->qual[require_cnt].added_dt_tm = cnvtdatetime(p.updt_dt_tm)
					endif
				endif
			endif
 
 			; added_prsnl_id and added_dt_tm will be used to get added requirement details
			if (pr.person_id = p.added_prsnl_id and p.added_prsnl_id > 0)
				reply->qual[require_cnt].added_user_name = pr.username
				reply->qual[require_cnt].added_dt_tm = cnvtdatetime(p.added_dt_tm)
			endif
		with nocounter
 
 
        if (curqual = 0)
        	set err_cnt = err_cnt + 1
            set reply->status_data->subeventstatus[err_cnt]->OperationName
                 = "select"
              set reply->status_data->subeventstatus[err_cnt]->OperationStatus
                 = "Z"
              set reply->status_data->subeventstatus[err_cnt]->TargetObjectName
                 = "PERSON_TRANS_REQ"
              set reply->status_data->subeventstatus[err_cnt]->TargetObjectValue 
                 = "unable to return requirements specified"
              set reply->status_data.status = "Z"
           else
              set reply->status_data.status = "S"
           endif
; 002   endif
/****************************************************************************
*       exit_script                                                         *
*****************************************************************************/
           end go

;Generated by GNU enscript 1.6.4.
