 ;;Solution_Test/script/PathNet -- BB Transfusion/RESULTENTRYFORBLOODBANK/BBT_ADD_PRS_ANTIGEN.INC Turn on black mode

/*
 *  PURPOSE:  add row to person_antigen table
 *
 *  PRELIMINARIES:
 *   1)  Include the following code in calling program:
 *           %i cclsource:bbt_add_prs_antigen.inc
 *           END        ; end subroutine ADD_PERSON_ANTIGEN
 *   2)  Declare the followoing variables in the calling program, prior to calling the subroutine:
 *             set gsub_person_antigen_status = "  " (gsub = global subroutine variable)
 *   3)  Call subroutine using CALL FORMAT below
 *   4)  After control returns from subroutine, evaluate gsub_person_antigen_status as follows:
 *         "FA" = add person_antigen failed                                
 *         "FS" = next sequence number get failed
 *         "OK" = add successful                                           
 *
 *
 *  CALL FORMAT (this may be copied into calling program): */
 ;       call ADD_PERSON_ANTIGEN
 ;          (                             ** These <variables> set in calling program **
 ;           /* person_id              */ <person_id>,
 ;           /* encntr_id              */ <encntr_id>,
 ;           /* antigen_cd             */ <antigen_cd>,
 ;           /* result_id              */ <result_id>,
 ;           /* rh_phenotype_id        */ <person_rh_phenotype_id>,        ; 0 if not from rh_phenotype test
 ;           /* bb_result_id           */ <bb_result_id>,
 ;           /* active_ind             */ <active_ind>,
 ;           /* active_status_cd       */ <reqdata->active_status_cd>,
 ;           /* active_status_dt_tm    */ cnvtdatetime(<active_status_dt_tm>),
 ;           /* active_status_prsnl_id */ <active_status_prsnl_id>
 ;          )

    subroutine ADD_PERSON_ANTIGEN
        (
         sub_person_id,
         sub_encntr_id,
         sub_antigen_cd,
         sub_result_id,
         sub_bb_result_id,
         sub_rh_phenotype_id,
         sub_active_ind,
         sub_active_status_cd,
         sub_active_status_dt_tm,
         sub_active_status_prsnl_id
        )

    /* Initialize gsub_person_antigen_status */
    set gsub_person_antigen_status = "  "
    set unique_id = 0.0

    set unique_id = NEXT_PATHNET_SEQ(0)


    if (curqual = 0) 
        set gsub_person_antigen_status = "FS"
    else
        /************************/
        /*    Insert record     */    
        /************************/
        insert into person_antigen p set
            p.person_antigen_id         = unique_id,
            p.person_id                 = sub_person_id,
            p.antigen_cd                = sub_antigen_cd,
            p.encntr_id                 = sub_encntr_id,
            p.result_id                 = sub_result_id,
            p.bb_result_id              = sub_bb_result_id,
            p.person_rh_phenotype_id    = sub_rh_phenotype_id,
            p.active_ind                = sub_active_ind,
            p.active_status_cd          = sub_active_status_cd,
            p.active_status_dt_tm       = cnvtdatetime(sub_active_status_dt_tm),
            p.active_status_prsnl_id    = sub_active_status_prsnl_id,            
            p.updt_dt_tm                = cnvtdatetime(curdate,curtime3),
            p.updt_id                   = reqinfo->updt_id,       
            p.updt_cnt                  = 0,
            p.updt_task                 = reqinfo->updt_task,   
            p.updt_applctx              = reqinfo->updt_applctx      
        with nocounter 

        if (curqual = 0)
            set gsub_person_antigen_status = "FA"       
        else
            set gsub_person_antigen_status = "OK"
        endif           ; endif curqual = 0 person_antigen   
    endif
/* END for subroutine is to be included in calling program code */

;Generated by GNU enscript 1.6.4.
