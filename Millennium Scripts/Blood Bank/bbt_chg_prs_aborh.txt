 ;;Solution_Test/script/PathNet -- BB Transfusion/RESULTENTRYFORBLOODBANK/BBT_CHG_PRS_ABORH.INC Turn on black mode

/*
 *  PURPOSE:  change row in person_aborh table
 *  
 *  PRELIMINARIES:
 *   1)  Include the following code in calling program:
 *           %i cclsource:bbt_chg_prs_aborh.inc
 *           END        ; end subroutine CHG_PERSON_ABORH
 *   2)  Declare 'gsub_person_aborh_inact_status = "  "' prior to calling subroutine (gsub = global subroutine variable)
 *   3)  Call subroutine using CALL FORMAT below
 *   4)  After control returns from subroutine, evaluate gsub_person_aborh_inact_status as follows:
 *         "FL" = lock forupdate failed
 *         "FU" = update failed (lock forupdate, if requested, successful)
 *         "OK" = update successful (lock forupdate, if requested, successful)
 *
 *  CALL FORMAT (this may be copied into calling program): */
 ;       call CHG_PERSON_ABORH        /* These <variables> set in calling program */
 ;          (
 ;           /* person_id              */ <person_id>,
 ;           /* abo_cd                 */ <abo_cd>,
 ;           /* rh_cd                  */ <rh_cd>,
 ;           /* active_ind             */ <active_ind>,
 ;           /* active_status_cd       */ <reqdata->active_status_cd>,
 ;           /* updt_cnt               */ <updt_cnt>,
 ;           /* lock_forupdate_ind     */ <1>, = "Yes" or <0>, = "No"
 ;          )

    subroutine CHG_PERSON_ABORH
        (
         sub_person_id,
         sub_abo_cd,
         sub_rh_cd,
         sub_active_ind,
         sub_active_status_cd,
         sub_updt_cnt,
         sub_lock_forupdate_ind
        )

    /* Initialize gsub_person_aborh_status */
    set gsub_person_aborh_inact_status = "  "
    set person_aborh_id = 0.0

    if (sub_lock_forupdate_ind = 1)
        select into "nl:"
            pa.person_aborh_id
        from
            person_aborh pa
        where   pa.person_id     = sub_person_id        
        and     pa.abo_cd = sub_abo_cd
        and     pa.rh_cd  = sub_rh_cd
        and     pa.active_ind = 1
        and     pa.updt_cnt       = sub_updt_cnt
        detail 
           person_aborh_id = pa.person_aborh_id
        with    nocounter, forupdate(pa)
        if (curqual = 0)
            set gsub_person_aborh_inact_status = "FL"
        endif
    endif

    if ((sub_lock_forupdate_ind = 0)
    or (sub_lock_forupdate_ind = 1 and curqual > 0))
            update into person_aborh pa
                set pa.active_ind               = sub_active_ind,
                    pa.active_status_cd         = sub_active_status_cd,
                    pa.end_effective_dt_tm      = cnvtdatetime(curdate, curtime3),
                    pa.updt_cnt                 = pa.updt_cnt + 1,
                    pa.updt_dt_tm               = cnvtdatetime(curdate, curtime3),
                    pa.updt_id                  = ReqInfo->updt_id,
                    pa.updt_task                = ReqInfo->updt_task,
                    pa.updt_applctx             = ReqInfo->updt_applctx
            where   pa.person_aborh_id          = person_aborh_id
                    and pa.updt_cnt             = sub_updt_cnt
            with    nocounter

        if (curqual = 0)
            set gsub_person_aborh_inact_status = "FU"
        else
            set gsub_person_aborh_inact_status = "OK"
        endif
    endif
/* END for subroutine is to be included in calling program code */

;Generated by GNU enscript 1.6.4.
