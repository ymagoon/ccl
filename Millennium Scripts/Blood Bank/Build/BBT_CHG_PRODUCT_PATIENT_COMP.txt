 ;;Solution_Test/script/PathNet -- BB Transfusion/Bbt_Productpatientcompatibilitytool/bbt_chg_product_patient_comp.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       BBT_CHG_PRODUCT_PATIENT_COMP
        Object name:            BBT_CHG_PRODUCT_PATIENT_COMP
        Request #:              225100

        Product:                Pathnet
        Product Team:           Blood Bank
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        To modify the product-person aborh compatibility

        Tables read:            PRODUCT_ABORH, PRODUCT_PATIENT_ABORH
        Tables updated:         PRODUCT_ABORH, PRODUCT_PATIENT_ABORH
        Executing from:         Produc_1

        Special Notes:          ?

******************************************************************************/

 
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     000 06/20/96 Mike Goings          Initial Release                     *
;~DE~************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************


drop program bbt_chg_product_patient_comp:dba go
create program bbt_chg_product_patient_comp:dba

/************************/
/*    Request record    */
/************************/
/*
record request
( 1 product_cd = f8
  1 product_aborh_cd = f8
  1 no_gt_on_prsn_flag = i4
  1 no_ad_on_prsn_flag = i4
  1 bbd_no_gt_dir_prsn_flag = i4
  1 prod_sequence_nbr = i4
  1 product_flag = c1
  1 person_flag = c1
  1 prod_active_ind = i2
  1 person_aborh_cnt = i4
  1 person_aborh_data[x]
    2 person_aborh_cd = f8
    2 prsn_sequence_nbr = i4
    2 warn_indicator = i2
  1 aborh_indicator = i2 ; 1 - aborh,  0 - rh only
  1 disp_no_curraborh_prsn_flag = i4
 )
*/


/************************/
/*     Reply record     */
/************************/
  record reply
  ( 
%i cclsource:status_block.inc
  )

/************************/
/* Initialize variables */
/************************/
set reply->status_data->status = "F"
set person_aborh_cnt  = request->person_aborh_cnt
set cur_updt_cnt = 0
set y = 1
  
/*****************************/
/*   Lock row(s) to update   */
/*****************************/

if ((request->person_flag = "F") and (request->product_flag = "F"))
  set reply->status_data->status = "P",
  go to exit_script
endif

/*********************************/
/*   product aborh information   */
/*********************************/
if (request->product_flag = "T")
  select into "nl:"
    p.*
  from product_aborh p
  where
     p.product_cd = request->product_cd and
     p.product_aborh_cd = request->product_aborh_cd and
     p.sequence_nbr = request->prod_sequence_nbr 
;and  p.active_ind = 1
  detail
      cur_updt_cnt = p.updt_cnt    
  with nocounter, forupdate(p)
  if (curqual = 0)
    set reply->status_data->status = "F",
    go to exit_script
  endif
endif

/*********************************/
/*   person aborh information    */
/*********************************/
if (request->person_flag = "T")
  for (x = 1 to person_aborh_cnt)
      select into "nl:"
        per.*
      from product_patient_aborh per
      where
         per.product_cd = request->product_cd and
         per.prod_aborh_cd = request->product_aborh_cd and
         per.prsn_aborh_cd = request->person_aborh_data[x]->person_aborh_cd and
         per.sequence_nbr = request->person_aborh_data[x]->prsn_sequence_nbr and
         per.active_ind = 1
      
      with nocounter, forupdate(per)

      if (curqual = 0)
        set reply->status_data->status = "F",
        go to exit_script
      endif
  endfor    
endif

/************************/
/*    Update record     */
/************************/
if (request->product_flag = "T")
  update into product_aborh p set

    p.product_cd = request->product_cd,
    p.product_aborh_cd = request->product_aborh_cd,
    p.sequence_nbr = request->prod_sequence_nbr,
    p.no_gt_on_prsn_flag = request->no_gt_on_prsn_flag,
    p.no_gt_autodir_prsn_flag = request->no_ad_on_prsn_flag,
    p.disp_no_curraborh_prsn_flag = request->disp_no_curraborh_prsn_flag,
    p.active_ind = request->prod_active_ind,
    p.active_status_cd = reqdata->active_status_cd,
    p.updt_cnt = p.updt_cnt +1,
    p.updt_dt_tm = cnvtdatetime(curdate, curtime3),
    p.updt_id = reqinfo->updt_id,
    p.updt_task = reqinfo->updt_task,
    p.updt_applctx = reqinfo->updt_applctx,
    p.aborh_option_flag = request->aborh_indicator

  where
    p.product_cd = request->product_cd and
    p.product_aborh_cd = request->product_aborh_cd and
    p.sequence_nbr = request->prod_sequence_nbr 
;and p.active_ind = 1
  with nocounter 

endif

if (curqual = 0)
   set reply->status_data->status = "F",
   go to exit_script
else
   set reply->status_data->status = "S"
endif

if (request->person_flag = "T")
  for (y = 1 to person_aborh_cnt)
      update into product_patient_aborh per set
        per.product_cd = request->product_cd,
        per.prod_aborh_cd = request->product_aborh_cd,
        per.prsn_aborh_cd = request->person_aborh_data[y]->person_aborh_cd,
        per.sequence_nbr = request->person_aborh_data[y]->prsn_sequence_nbr,
        per.active_ind = 0,
        per.active_status_cd = reqdata->active_status_cd,
        per.warn_ind = 
          request->person_aborh_data[y]->warn_indicator,
        per.updt_cnt = per.updt_cnt + 1,
        per.updt_dt_tm = cnvtdatetime(curdate, curtime3),
        per.updt_id = reqinfo->updt_id,
        per.updt_task = reqinfo->updt_task,
        per.updt_applctx = reqinfo->updt_applctx

      where
         per.product_cd = request->product_cd and
         per.prod_aborh_cd = request->product_aborh_cd and
         per.prsn_aborh_cd = request->person_aborh_data[y]->person_aborh_cd and
         per.sequence_nbr = request->person_aborh_data[y]->prsn_sequence_nbr and
         per.active_ind = 1

      if (curqual = 0)
        set reply->status_data->status = "F",
        go to exit_script
      endif
  endfor    
endif

/************************/
/*    exit_script       */
/************************/
#exit_script
if ((reply->status_data->status = "P") or (reply->status_data->status = "F"))
   rollback
else
   commit
endif

end go

;Generated by GNU enscript 1.6.4.
