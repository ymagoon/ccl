 ;;Solution_Test/script/Order Management/orm_chg_fmtflds/orm_chg_fmtflds.prg Turn on black mode

 
/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/
 
/*****************************************************************************
 
        Source file name:       ORM_CHG_FMTFLDS.PRG
        Object name:            ORM_CHG_FMTFLDS
        Request #:              500029
 
        Product:                DCP
        Product Team:           ORDER MANAGEMENT
        HNA Version:            500
        CCL Version:            4.0
 
        Program purpose:        Updates an order entry field on the
                                OE_FORMAT_FIELDS table
 
        Tables read:            OE_FORMAT_FIELDS
        Tables updated:         OE_FORMAT_FIELDS
        Executing from:         ORDER ENTRY FORMAT TOOL (app 500003)
 
        Special Notes:          -
 
******************************************************************************/
 
 
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer           Comment                               *
;    *--- -------- ------------------ ------------------------------------- *
;     ### 09/14/95 Matt Huffman       Initial Release                       *
;     002 05/31/97 Joe Geris          Added whole bunch of new fields       *
;     003 05/04/98 Shawn Purcell      Added filter_params                   *
;     004 10/04/98 Shawn Purcell      Add required_review_ind, and          *
;                                     require_verify_ind                    *
;     005 07/07/00 John Gedeon        Set core_ind=clin_line_ind            *
; ;   006 11/06/02 Fanny Ruiz         Set core_ind=core_ind
;     007 03/06/03 Randy Rogers		  Orders data merge			     		*
;     008 05/29/07 Phil Chen          Add lock_on_modify_flag               *
; 	  009 16/02/10	Keerthi 		 	Add carry_fwd_plan_ind				*
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************
 
drop program orm_chg_fmtflds:dba go
create program orm_chg_fmtflds:dba
 
/*
record request
(
1 oe_format_id = F8
1 oe_field_id = F8
1 action_type_cd = F8
1 accept_flag = I2
1 default_value = vc               ;max length of 100
1 input_mask = vc                  ;max length of 50
1 require_cosign_ind = I2
1 prolog_method = F8
1 epilog_method = F8
1 status_line = vc                 ;max length of 200
1 label_text = vc                  ;max length of 200
1 group_seq = I4
1 field_seq = I4
1 max_nbr_occur = i4
1 value_required_ind = i2
1 clin_line_ind = i2
1 core_ind =i2                      ;mod 006
1 clin_line_label = c(25)
1 clin_suffix_ind = i2
1 disp_yes_no_flag = i2
1 dept_line_ind = i2
1 dept_line_label = c(25)
1 dept_suffix_ind = i2
1 disp_dept_yes_no_flag = i2
1 def_prev_order_ind = i2
1 filter_params = vc
1 require_review_ind = i2
1 require_cosign_ind = i2
1 require_verify_ind = i2
1 lock_on_modify_flag = i2		; mod 008
1 carry_fwd_plan_ind = i2		;mod 009
)
*/
 
record reply
( 1 qual[1]
    2 oe_format_id = f8
  1 status_data
    2 status = c1
    2 subeventstatus[1]
      3 OperationName = c8
      3 OperationStatus = c1
      3 TargetObjectName = c15
      3 TargetObjectValue = vc
)
 
set failed = "F"
set reply->status_data->status = "F"
 
declare default_value =  vc with public, noconstant(fillstring(100," "))		;007 Start
declare default_parent_entity_name = vc with public, noconstant(fillstring(100," "))
declare default_parent_entity_id = f8 with public, noconstant(0.0)
declare field_type_flag = i2 with public, noconstant(0)
declare oe_field_meaning_id = f8 with public, noconstant(0.0)
declare script_version = vc with public, noconstant(" ")
 
select into "nl:"
from ORDER_ENTRY_FIELDS oef
where oef.oe_field_id = request->oe_field_id
detail
	field_type_flag 	= oef.field_type_flag
	oe_field_meaning_id = oef.oe_field_meaning_id
with nocounter
 
/*** Set oe_format_fields information based on field_type_flag ***/
     if (field_type_flag in (0,1,2,3,5,7,11,14,15) or field_type_flag = NULL)
		set default_value = request->default_value
		set default_parent_entity_name = " "
		set default_parent_entity_id = 0
	 elseif (field_type_flag in (6,9))
		if (ISNUMERIC(request->default_value) = 0 ) ;default_value is a string
	 		set default_value = request->default_value
	 	else
			set default_value = " "
		endif
		set default_parent_entity_name = "CODE_VALUE"
		set default_parent_entity_id = cnvtreal(request->default_value)
	 elseif (field_type_flag = 12)
	 	if (ISNUMERIC(request->default_value) = 0 ) ;default_value is a string
	 		set default_value = request->default_value
	 	else
			set default_value = " "
		endif
		set default_parent_entity_id = cnvtreal(request->default_value)
	 	if (oe_field_meaning_id = 48)
	 		set default_parent_entity_name = "RESEARCH_ACCOUNT"
	 	elseif (oe_field_meaning_id = 123)
	 		set default_parent_entity_name = "SCH_BOOK_INSTR"
	 	else
	 		set default_parent_entity_name = "CODE_VALUE"
	 	endif
	 elseif (field_type_flag in (8,13))
		if (ISNUMERIC(request->default_value) = 0 ) ;default_value is a string
	 		set default_value = request->default_value
	 	else
			set default_value = " "
		endif
		set default_parent_entity_name = "PERSON"
		set default_parent_entity_id = cnvtreal(request->default_value)
	 elseif (field_type_flag = 10)
		if (ISNUMERIC(request->default_value) = 0 ) ;default_value is a string
	 		set default_value = request->default_value
	 	else
			set default_value = " "
		endif
		set default_parent_entity_name = "NOMENCLATURE"
		set default_parent_entity_id = cnvtreal(request->default_value)
	 endif																		;007 End
 
update into oe_format_fields off
set
  off.oe_format_id        			= request->oe_format_id,
  off.oe_field_id         			= request->oe_field_id,
  off.action_type_cd      			= request->action_type_cd,
  off.accept_flag         			= request->accept_flag,
  off.default_value       			= default_value,							;007
  off.default_parent_entity_name 	= default_parent_entity_name,				;007
  off.default_parent_entity_id 		= default_parent_entity_id,					;007
  off.input_mask          			= request->input_mask,
  off.require_cosign_ind  			= request->require_cosign_ind,
  off.prolog_method       			= request->prolog_method,
  off.epilog_method       			= request->epilog_method,
  off.status_line         			= request->status_line,
  off.label_text          			= request->label_text,
  off.group_seq           			= request->group_seq,
  off.field_seq           			= request->field_seq,
  off.max_nbr_occur       			= request->max_nbr_occur,
  off.core_ind            			= request->core_ind,
  off.value_required_ind  			= request->value_required_ind,
  off.clin_line_ind       			= request->clin_line_ind,
  off.core_ind            			= request->core_ind,                 ;mod 006
  off.clin_line_label     			= request->clin_line_label,
  off.clin_suffix_ind     			= request->clin_suffix_ind,
  off.disp_yes_no_flag    			= request->disp_yes_no_flag,
  off.dept_line_ind       			= request->dept_line_ind,
  off.dept_line_label     			= request->dept_line_label,
  off.dept_suffix_ind     			= request->dept_suffix_ind,
  off.disp_dept_yes_no_flag    		= request->disp_dept_yes_no_flag,
  off.def_prev_order_ind  			= request->def_prev_order_ind,
  off.filter_params       			= request->filter_params,
  off.require_review_ind  			= request->require_review_ind,
  off.require_verify_ind  			= request->require_verify_ind,
  off.updt_dt_tm          			= cnvtdatetime(curdate,curtime),
  off.updt_id             			= reqinfo->updt_id,
  off.updt_task           			= reqinfo->updt_task,
  off.updt_applctx        			= reqinfo->updt_applctx,
  off.updt_cnt            			= off.updt_cnt + 1,
  off.lock_on_modify_flag           = request->lock_on_modify_flag,		;mod 008
  off.carry_fwd_plan_ind           = request->carry_fwd_plan_ind		;mod 009
 
where off.oe_format_id      = request->oe_format_id
  and request->oe_field_id  > 0
  and off.oe_field_id       = request->oe_field_id
  and off.action_type_cd    = request->action_type_cd
with counter
 
if (curqual = 0)
  set reply->status_data->subeventstatus[1]->targetobjectname = "order entry format field table"
  set reply->status_data->subeventstatus[1]->operationstatus = "F"
  set reply->status_data->subeventstatus[1]->operationname = "update"
  set reply->status_data->subeventstatus[1]->targetObjectValue = "unable to update into table"
  set failed = "T"
endif
 
if (failed = "F")
  set reqinfo->commit_ind = 1
  set reply->status_data->status = "S"
else
  set reqinfo->commit_ind = 0
  set reply->status_data->status = "F"
endif
 
set script_version = "009 16/02/10 km019227"
end
 
go
 

;Generated by GNU enscript 1.6.4.
