 ;;Solution_Test/script/PathNet -- BB Transfusion/RESULTENTRYFORBLOODBANK/BBT_GET_RH_PHENOTYPE_ANTIGENS.INC Turn on black mode

/*
 *  PURPOSE:  Get antigens (bb_rh_pheno_testing) associated with rh_phenotype (bb_rh_phenotype)
 *
 *  PRELIMINARIES:
 *   1)  Include the following code in calling program:
 *           %i cclsource:bbt_get_rh_phenotype_antigens.inc
 *           END        ; end subroutine GET_RH_PHENOTYPE_ANTIGENS
 *
 *   2)  Declare the followoing variables in the calling program, prior to calling the subroutine:
 *
 *             record rh_a_rec
 *             (
 *              1  antigenlist[*]
 *                  2  antigen_cd       = f8
 *             )
 *
 *             set gsub_rh_phenotype_status = "  " (gsub = global subroutine variable)
 *             set rh_a_cnt = 0
 *
 *   3)  Call subroutine using CALL FORMAT below
 *
 *   4)  After control returns from subroutine, evaluate gsub_rh_phenotype_status as follows:
 *         "FF" = Select failed
 *         "OK" = Select successful                                           
 *
 *         evaluate rh_a_cnt for number of antigens returned
 *
 *  CALL FORMAT (this may be copied into calling program): */
 ;       call GET_RH_PHENOTYPE_ANTIGENS
 ;          (                             ** These <variables> set in calling program **
 ;           /* rh_phenotype_id */ , <rh_phenotype_id>  ; from bb_rh_phenotype row for resulted nomenclature_id
 ;          )
 ;  01/12/2009      Ronda Zheng         Moved BBD_GET_RH_PHENOTYPE_ANTIGENS function from bbd_get_rh_phenotype_antigens.inc.

;Moved BBD_GET_RH_PHENOTYPE_ANTIGENS function from bbd_get_rh_phenotype_antigens.inc.
;Need to add BBD_GET_RH_PHENOTYPE_ANTIGENS subroutine at the begining to keep passive because
;GET_RH_PHENOTYPE_ANTIGENS subroutine END statement is in calling scripts.
subroutine BBD_GET_RH_PHENOTYPE_ANTIGENS
    (
     sub_nomenclature_id
    )
 
 
/* Initialize gsub_rh_phenotype_status */
set gsub_bbd_rh_phenotype_status = "  "
 
set select_ok_ind = 0
set rh_a_cnt = 0
set stat = alterlist(rh_a_rec->antigenlist, 0)
 
select into "nl:"
    brp.fr_nomenclature_id,
    brpt.special_testing_cd
from
    bb_rh_phenotype brp,
    bb_rh_pheno_testing brpt
plan brp where
      (brp.fr_nomenclature_id         = sub_nomenclature_id
        or brp.w_nomenclature_id    = sub_nomenclature_id)
      and brp.active_ind            = 1
join brpt where
          brpt.rh_phenotype_id    = brp.rh_phenotype_id and
          brpt.active_ind   = 1
 
head report
    select_ok_ind = 0
    rh_a_cnt = 0
 
detail
    rh_a_cnt = rh_a_cnt + 1
    stat = alterlist(rh_a_rec->antigenlist, rh_a_cnt)
    rh_a_rec->antigenlist[rh_a_cnt]->antigen_cd     = brpt.special_testing_cd
 
foot report
    select_ok_ind = 1
 
with    nocounter
 
if (select_ok_ind != 1)
    set gsub_bbd_rh_phenotype_status = "FF"
else
    set gsub_bbd_rh_phenotype_status = "OK"
endif
END


 
subroutine GET_RH_PHENOTYPE_ANTIGENS
    (
     sub_rh_phenotype_id
    )


/* Initialize gsub_rh_phenotype_status */
set gsub_rh_phenotype_status = "  "

set select_ok_ind = 0
set rh_a_cnt = 0
set stat = alterlist(rh_a_rec->antigenlist, 0)
set stat = alterlist(rh_a_rec->antigenlist, 10)


select into "nl:"
    brpt.special_testing_cd
from
    bb_rh_pheno_testing brpt
where
    brpt.rh_phenotype_id    = sub_rh_phenotype_id
      and brpt.active_ind   = 1
    
head report
    select_ok_ind = 0
    rh_a_cnt = 0

detail
    rh_a_cnt = rh_a_cnt + 1
    if (mod(rh_a_cnt, 10) = 1 and rh_a_cnt != 1)
        stat = alterlist(rh_a_rec->antigenlist, rh_a_cnt + 9)
    endif
    rh_a_rec->antigenlist[rh_a_cnt]->antigen_cd     = brpt.special_testing_cd

foot report
    stat = alterlist(rh_a_rec->antigenlist, rh_a_cnt)
    select_ok_ind = 1

with    nocounter, nullreport

if (select_ok_ind != 1)        
    set gsub_rh_phenotype_status = "FF"
else
    set gsub_rh_phenotype_status = "OK"
endif

/* END for subroutine is to be included in calling program */  


;Generated by GNU enscript 1.6.4.
