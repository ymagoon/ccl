 ;;Solution_Test/script/PathNet -- BB Transfusion/RESULTENTRYFORBLOODBANK/BBT_ADD_PRODUCT_RH_PHENOTYPE.INC Turn on black mode

/*
 *  PURPOSE:  add row to product_rh_phenotype
 *
 *  PRELIMINARIES:
 *   1)  Include the following code in calling program:
 *           %i cclsource:bbt_add_product_rh_phenotype.inc
 *           END        ; end subroutine ADD_product_RH_PHENOTYPE
 *
 *           %i cclsource:bbt_get_pathnet_seq_sub.inc
 *
 *   2)  Declare the followoing variables in the calling program, prior to calling the subroutine:
 *             set gsub_rh_phenotype_status     = "  "      ; (gsub = global subroutine variable)
 *             set new_rh_phenotype_id          = 0.0       
 *             set bb_rh_phenotype_id           = 0.0       ; rh_phenotype_id from bb_rh_phenotype table for  nomenclature_id
 *
 *   3)  Call subroutine using CALL FORMAT below
 *
 *   4)  After control returns from subroutine, evaluate gsub_rh_phenotype_status as follows:
 *         "FZ" = Zero rh_phenotype records found for nomenclature_id -- could not get bb_rh_phenotype_id
 *         "FM" = Multiple rh_phenotype records found for nomenclature_id
 *         "FF" = rh_phenotype select (to get bb_rh_phenotype_id) failed
 *         "FS" = get new product_rh_phenotype_id (seq) failed
 *         "FA" = add product_rh_phenotype failed                                
 *         "OK" = add successful                                           
 *
 *  CALL FORMAT (this may be copied into calling program): */
 ;       call ADD_product_RH_PHENOTYPE
 ;          (                             ** These <variables> set in calling program **
 ;           /* product_id              */ <product_id>
 ;           /* nomenclature_id        */ , <nomenclature_id>
 ;           /* active_ind             */ , <active_ind>
 ;           /* active_status_cd       */ , <reqdata->active_status_cd>
 ;           /* active_status_dt_tm    */ , cnvtdatetime(<active_status_dt_tm>)
 ;           /* active_status_prsnl_id */ , <active_status_prsnl_id>
 ;          )

subroutine ADD_PRODUCT_RH_PHENOTYPE
    (
     sub_product_id
     , sub_nomenclature_id
     , sub_active_ind
     , sub_active_status_cd
     , sub_active_status_dt_tm
     , sub_active_status_prsnl_id
    )

/* Initialize gsub_rh_phenotype_status */
set gsub_rh_phenotype_status = "  "

/*
 * Get rh_phenotype_id from bb_rh_phenotype reference table for resulted nomenclature_id
 */
set bb_rh_phenotype_id = 0.0
set select_ok_ind = 0  
set brp_qual_cnt = 0

select into "nl:"
    brp.rh_phenotype_id
from
    bb_rh_phenotype brp
where    
    (brp.fr_nomenclature_id         = sub_nomenclature_id
        or brp.w_nomenclature_id    = sub_nomenclature_id)
      and brp.active_ind            = 1

head report
    select_ok_ind = 0
    brp_qual_cnt = 0

detail
    brp_qual_cnt = brp_qual_cnt + 1
    bb_rh_phenotype_id      = brp.rh_phenotype_id

foot report
    select_ok_ind = 1

with    nocounter, nullreport

if (select_ok_ind = 1)
    if (brp_qual_cnt = 0)
        set gsub_rh_phenotype_status = "FZ"
    elseif (brp_qual_cnt > 1)
        set gsub_rh_phenotype_status = "FM"
    endif
else
    set gsub_rh_phenotype_status = "FF"
endif


if (trim(gsub_rh_phenotype_status) = "")
    set new_rh_phenotype_id = 0.0

    set new_rh_phenotype_id = NEXT_PATHNET_SEQ(0)

    if (curqual = 0) 
       set gsub_rh_phenotype_status = "FS"
    else
        insert into product_rh_phenotype prp set
            prp.product_rh_phenotype_id         = new_rh_phenotype_id
            , prp.product_id                    = sub_product_id
            , prp.rh_phenotype_id               = bb_rh_phenotype_id
            , prp.nomenclature_id               = sub_nomenclature_id
            , prp.updt_cnt                      = 0
            , prp.updt_dt_tm                    = cnvtdatetime(curdate,curtime3)
            , prp.updt_id                       = reqinfo->updt_id
            , prp.updt_task                     = reqinfo->updt_task
            , prp.updt_applctx                  = reqinfo->updt_applctx      
            , prp.active_ind                    = sub_active_ind
            , prp.active_status_cd              = sub_active_status_cd
            , prp.active_status_dt_tm           = cnvtdatetime(sub_active_status_dt_tm)
            , prp.active_status_prsnl_id        = sub_active_status_prsnl_id  
        with nocounter

        if (curqual = 0)
            set gsub_rh_phenotype_status = "FA"
        else
            set gsub_rh_phenotype_status = "OK"
        endif
    endif
endif

/* END for subroutine is to be included in calling program */   

;Generated by GNU enscript 1.6.4.
