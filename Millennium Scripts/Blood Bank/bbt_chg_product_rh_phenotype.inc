 ;;Solution_Test/script/PathNet -- BB Transfusion/RESULTENTRYFORBLOODBANK/BBT_CHG_PRODUCT_RH_PHENOTYPE.INC Turn on black mode

/*
 *  PURPOSE:  add row to product_rh_phenotype 
 *
 *  PRELIMINARIES:
 *   1)  Include the following code in calling program:
 *           %i cclsource:bbt_chg_product_rh_phenotype.inc
 *           END        ; end subroutine CHG_PRODUCT_RH_PHENOTYPE
 *
 *   2)  Declare the followoing variables in the calling program, prior to calling the subroutine:
 *           set gsub_rh_phenotype_status     = "  "      ; (gsub = global subroutine variable)
 *
 *   3)  Call subroutine using CALL FORMAT below
 *
 *   4)  After control returns from subroutine, evaluate gsub_rh_phenotype_status as follows:
 *         "FL" = Lock forupdate failed for product_rh_phenotype failed
 *         "FU" = Update for product_rh_phenotype failed
 *         "OK" = Update successful                                           
 *
 *  CALL FORMAT (this may be copied into calling program): */
 ;       call CHG_product_RH_PHENOTYPE
 ;          (                             ** These <variables> set in calling program **
 ;           /* rh_phenotype_id  */ <rh_phenotype_id>
 ;           /* updt_cnt         */ , <rh_phenotype_updt_cnt>
 ;           /* active_ind       */ , <active_ind>
 ;           /* active_status_cd */ , <reqdata->in/active_status_cd>
 ;          )

subroutine CHG_PRODUCT_RH_PHENOTYPE
    (
     sub_rh_phenotype_id
     , sub_updt_cnt
     , sub_active_ind
     , sub_active_status_cd
    )

/* Initialize gsub_rh_phenotype_status */
set gsub_rh_phenotype_status = "  "

/*
 * Lock product_rh_phenotype row forupdate 
 */
select into "nl:"
    prp.product_rh_phenotype_id
from
    product_rh_phenotype prp
where
    prp.product_rh_phenotype_id  = sub_rh_phenotype_id
      and prp.updt_cnt           = sub_updt_cnt
with    nocounter

if (curqual = 0)
    set gsub_rh_phenotype_status = "FL"
else
    /*
     * Update product_rh_phenotype row
     */
    update into product_rh_phenotype prp set
        prp.updt_cnt        = prp.updt_cnt + 1
        , prp.updt_dt_tm                    = cnvtdatetime(curdate,curtime3)
        , prp.updt_id                       = reqinfo->updt_id
        , prp.updt_task                     = reqinfo->updt_task
        , prp.updt_applctx                  = reqinfo->updt_applctx      
        , prp.active_ind                    = sub_active_ind
        , prp.active_status_cd              = sub_active_status_cd

    where   prp.product_rh_phenotype_id      = sub_rh_phenotype_id

    with    nocounter

    if (curqual = 0)
        set gsub_rh_phenotype_status = "FU"
    else
        set gsub_rh_phenotype_status = "OK"
    endif
endif

/* END for subroutine is to be included in calling program */   

;Generated by GNU enscript 1.6.4.
