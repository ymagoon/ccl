 ;;Solution_Test/script/PathNet -- BB Transfusion/BBInterface/BBT_GET_ALL_ABORH_BARCODES.PRG Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       bbt_get_all_aborh_barcodes.prg
        Object name:            bbt_get_all_aborh_barcodes
        Request #:              ?

        Product:                Pathnet                              
        Product Team:           Blood Bank
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Retrieve all aborh barcodes (codabar)
                               

        Tables read:            code_value, code_value_extension
                               
        Tables updated:         none
        Executing from:         BBComboBox control (part of BBInterface.ocx)

        Special Notes:          none
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;    *001 03/29/00 Doug Saus            Initial Release                     *
;    *002 10/03/00 Doug Saus            Removed retrieval of ISBT barcodes  *
;    *003 01/08/02 Sasi Gandham         Add Order By for the Head Field     *
;~DE~************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

drop program bbt_get_all_aborh_barcodes:dba go
create program bbt_get_all_aborh_barcodes:dba

/*
NO REQUEST
*/

record reply
(
 1  CodabarBarcodes[*]
   2  barcode                           = c15
   2  code_value_cd                     = f8
   2  code_value_disp                   = vc
   2  code_value_mean                   = c12
   2  cve1_cd                           = f8
   2  cve1_disp                         = vc
   2  cve1_mean                         = c12
   2  cve2_cd                           = f8
   2  cve2_disp                         = vc
   2  cve2_mean                         = c12
%i cclsource:status_block.inc
)

set    codabar_barcode = fillstring(15," ")
set    abo_cd = 0.0
set    rh_cd = 0.0
set    codabar_cnt = 0

;initialize error handling
set sErrorMsg = fillstring(255," ")
set nErrorStatus = error(sErrorMsg,1)

select into "nl"

from 
    code_value cv
    , (dummyt d_cve with seq = 1)
    , code_value_extension cve

plan cv where cv.code_set = 1640
          and cv.active_ind = 1
          and cv.begin_effective_dt_tm <= cnvtdatetime(curdate,curtime3)
          and cv.end_effective_dt_tm >= cnvtdatetime(curdate,curtime3)

join d_cve

join cve where cve.code_value = cv.code_value

order by cv.code_value

head report
    ;initialize variables
    codabar_cnt = 0
    stat = alterlist(reply->CodabarBarcodes,10)

head cv.code_value
    codabar_barcode = fillstring(15," ")
    abo_cd = 0.0
    rh_cd = 0.0

detail
    if (cve.field_name = "Barcode")
        if (trim(cve.field_value) > ""
         and cve.field_value != NULL)
            codabar_barcode = cve.field_value
        endif

    elseif (cve.field_name = "ABOOnly_cd")
        abo_cd = cnvtreal(cve.field_value)

    elseif (cve.field_name = "RhOnly_cd")
        rh_cd = cnvtreal(cve.field_value)

    endif
    
foot cv.code_value
    codabar_cnt = codabar_cnt + 1
    if (mod(codabar_cnt, 10) = 1 and codabar_cnt != 1)
        stat = alterlist(reply->CodabarBarcodes,codabar_cnt + 9)
    endif
    reply->CodabarBarcodes[codabar_cnt].barcode         = codabar_barcode
    reply->CodabarBarcodes[codabar_cnt].code_value_cd   = cv.code_value
    reply->CodabarBarcodes[codabar_cnt].cve1_cd         = abo_cd
    reply->CodabarBarcodes[codabar_cnt].cve2_cd         = rh_cd

foot report
    ;resize list to correct size
    stat = alterlist(reply->CodabarBarcodes,codabar_cnt)

with nocounter

set nErrorStatus = error(sErrorMsg,0)
if (nErrorStatus = 0)
    ;success
    set reply->status_data = "S"
else
    ;failure
    set reply->status_data = "F"
    set reply->status_data->subeventstatus[1].OperationName = "bbt_get_all_aborh_barcodes"    
    set reply->status_data->subeventstatus[1].OperationStatus = "F"
    set reply->status_data->subeventstatus[1].TargetObjectName = "select aborh barcodes"    
    set reply->status_data->subeventstatus[1].TargetObjectValue = sErrorMsg
endif

end go

;Generated by GNU enscript 1.6.4.
