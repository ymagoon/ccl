 ;;Solution_Test/script/PathNet -- BB Transfusion/Scriptinventorysearch/BBT_INV_EVENTS_NOTES_SPECTEST.INC Turn on black mode

/******************************************************/
subroutine Get_Events_Notes_SpecTest (nodata)


    /*********************/
    /* load antigens     */
    /*********************/
    select into  "nl:"
        st.product_id
        , st.special_testing_cd

    from 
        (dummyt d with seq = value(product_count))
        , special_testing st

    plan        d
    join        st  where   st.product_id       = reply->qual[d.seq].product_id
                              and st.active_ind = 1

    order by    st.product_id

    head st.product_id
        antigen_count = 0    

    detail 
        antigen_count = antigen_count + 1,
        stat = alterlist (reply->qual[d.seq].antigens, antigen_count),
        reply ->qual[d.seq].antigens[antigen_count].antigen_cd = st.special_testing_cd

    with nocounter

    /*********************/
    /* load states       */
    /*********************/
    select into  "nl:"
        pe.product_id
        , pe.event_type_cd

    from  
        (dummyt d with seq = value(product_count))
        , code_value cv
        , product_event pe

    plan        d 
    join        cv  where   cv.code_set = 1610
                          and cv.cdf_meaning != "8" 
                          and cv.cdf_meaning != "6"
                          and cv.cdf_meaning != "13" 
                          and cv.cdf_meaning != "17" 
                          and cv.cdf_meaning != "18" 
                          and cv.active_ind = 1
    join        pe  where   pe.event_type_cd = cv.code_value 
                          and pe.product_id = reply->qual[d.seq].product_id 
                          and pe.active_ind = 1               

    order by    pe.product_id, cv.collation_seq, pe.event_type_cd, pe.product_event_id

    head pe.product_id
        states_count = 0
        stat = alterlist (reply->qual[d.seq].states, 3)

    head pe.event_type_cd
        states_count = states_count + 1
        if (mod(states_count, 3) = 1 and states_count != 1)
            stat = alterlist (reply->qual[d.seq].states, states_count + 2)
        endif
        reply->qual[d.seq].states[states_count].states_cd = pe.event_type_cd

    foot pe.product_id
        stat = alterlist(reply->qual[d.seq].states, states_count)

    with nocounter


    /*********************/
    /* load comment_ind  */
    /*********************/
    select into  "nl:"
      pn.product_id
      , pn.active_ind

    from 
        (dummyt d with seq = value(product_count))
        , product_note pn

    plan  d 
    join  pn  where   pn.product_id           = reply->qual[d.seq]->product_id
                      and pn.active_ind     = 1

    order pn.product_id

    detail
        reply->qual[d.seq]->comment_ind = 1

    with nocounter

end    ;end of sub Get_Events_Notes_SpecTest    

;Generated by GNU enscript 1.6.4.
