 ;;Solution_Test/script/PathNet -- BB Transfusion/BBCompXMElig/BBT_GET_RESULT_ABO_AND_RH.PRG Turn on black mode


/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       bbt_get_result_abo_and_rh.prg
        Object name:            bbt_get_result_abo_and_rh
        Request #:              225578

        Product:                Pathnet                              
        Product Team:           Blood Bank
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Retrieve abo and rh results
                               

        Tables read:            code_value_extension
                               
        Tables updated:         none
        Executing from:         Computer Crossmatch

        Special Notes:          none
******************************************************************************/

 
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;    *001 08/25/98 Doug Saus            Initial Release                     *
;~DE~************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

drop program bbt_get_result_abo_and_rh:dba go
create program bbt_get_result_abo_and_rh:dba

/*
record request
(
)
*/

record reply
(
 1  qual[*]
    2  result_aborh_cd                          = f8
    2  result_aborh_disp                        = c40
    2  result_aborh_mean                        = c12
    2  stand_aborh_cd                           = f8
    2  stand_aborh_disp                         = c40
    2  stand_aborh_mean                         = c12
    2  abo_cd                                   = f8
    2  abo_disp                                 = c40
    2  abo_mean                                 = c12
    2  rh_cd                                    = f8
    2  rh_disp                                  = c40
    2  rh_mean                                  = c12
%i cclsource:status_block.inc
)
      
set qual_cnt = 0
set select_ok_ind = 0
set reply->status_data->status = "S"


select into "nl:"
    cve1643.code_value
    , cve1643.field_name
    , cve1643.field_value
    , cve1640.field_name
    , cve1640.field_value

from
    code_value_extension cve1643
    , code_value_extension cve1640

plan    cve1643 where   cve1643.code_set                        = 1643
                        and cve1643.field_name                  = "ABORH_cd"
                        and cnvtreal(cve1643.field_value)       > 0.0

join    cve1640 where   cve1640.code_value                      = cnvtreal(cve1643.field_value)
                        and cnvtreal(cve1640.field_value)       > 0.0
                        and (cve1640.field_name                 = "ABOOnly_cd"
                         or cve1640.field_name                  = "RhOnly_cd")

order by        cve1643.code_value, cve1640.field_name

head report
    select_ok_ind = 0
    stat = alterlist(reply->qual, 10)


head cve1643.code_value
    qual_cnt = qual_cnt + 1

    if ((mod(qual_cnt, 10) = 1) and qual_cnt != 1)
        stat = alterlist(reply->qual, qual_cnt + 9)
    endif
    
    reply->qual[qual_cnt].result_aborh_cd = cve1643.code_value
    reply->qual[qual_cnt].stand_aborh_cd  = cnvtreal(cve1643.field_value)

detail
    if (cve1640.field_name = "ABOOnly_cd")
        reply->qual[qual_cnt]->abo_cd   = cnvtreal(cve1640.field_value)
    else
        reply->qual[qual_cnt]->rh_cd    = cnvtreal(cve1640.field_value)
    endif

foot report
    select_ok_ind = 1
    stat = alterlist(reply->qual,qual_cnt)

with nocounter, nullreport

if (select_ok_ind != 1)
    set reply->status_data->status = "F"
    set reply->status_data->subeventstatus[count1]->OperationName = "select"
    set reply->status_data->subeventstatus[count1]->OperationStatus = "F"
    set reply->status_data->subeventstatus[count1]->TargetObjectName = "bbt_get_result_abo_and_rh"
    set reply->status_data->subeventstatus[count1]->TargetObjectValue = "select for code_value_extensions failed"

endif


if (request->debug_ind = 1)
    set item_cnt = size(reply->qual, 5)
    for (item = 1 to item_cnt)
        call echo(build(item, ".",
            reply->qual[item]->result_aborh_cd           , "/",
            reply->qual[item]->stand_aborh_cd            , "/",
            reply->qual[item]->abo_cd                    , "/",
            reply->qual[item]->rh_cd                     , " ")) 
    endfor
endif

end go

;Generated by GNU enscript 1.6.4.
