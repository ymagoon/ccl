 ;;Solution_Test/script/PathNet -- BB Transfusion/Bbt_Productnotes/BBT_ADD_PRODUCT_NOTE.INC Turn on black mode

/* 
 * bbt_add_product_note.inc:  creates product_note and long_text rows
 *
 * USE:
 *  1.  Initialize the following GLOBAL variables in the calling program:
 *        set new_pathnet_seq = 0.0
 *        set new_product_note_id = 0.0
 *        set new_long_text_id = 0.0 
 *        set gsub_status = " "
 *        set gsub_process = fillstring(200, " ")
 *        set gsub_message = fillstring(200, " ")
 *  3.  Include in calling program as follows (NOTE:  the 'END' for the subroutine is to be coded in the calling program
 *      to allow for additional functionality for the subroutine by the calling program):
 *        %i cclsource:bbt_add_product_note.inc
 *        END           ; end subroutine ADD_PRODUCT_NOTE
 *  4.  Include 'bbt_add_long_text.inc' in calling program as well, as follows (NOTE: the 'END' for the subroutine is to
 *      be coded in the calling program here as well):
 *        %i cclsource:bbt_add_long_text.inc
 *        END           ; end subroutine ADD_LONG_TEXT
 *  5.  Call 'ADD_PRODUCT_NOTE' as follows: */
 ;          Call ADD_PRODUCT
 ;              (
 ;               /* product _id  */ <product_id>
 ;               /* product_note */ <product_note>
 ;              )
/*  6.  Evaluate gsub_process to determine success/failure of add.  gsub_process and gsub_message will have information
 *      regarding the success or failure of the add.
 */

subroutine ADD_PRODUCT_NOTE
    (
     sub_product_id,
     sub_product_note
    )     

    /*
     * Retrieve new sequence number for product_note_id
     */

    set new_product_note_id = 0.0

%i cclsource:bbt_get_pathnet_seq.inc

    if (curqual = 0)
        set gsub_status = "F"
        set gsub_process = "get new product_note_id (PATHNET_SEQ)"
        set gsub_message = "get new product_note_id (PATHNET_SEQ) failed"
    else
        set new_product_note_id = new_pathnet_seq

        /* Retrieve new sequence number for long_text_id */

        set new_long_text_id = 0.0
     
        select into "nl:"
            seqn = seq(LONG_DATA_SEQ, nextval)
        from        dual
        detail
            new_long_text_id   = seqn
        with        format, nocounter

        if (curqual = 0)
            set gsub_status = "F"
            set gsub_process = "get new long_text_id (LONG_DATA_SEQ)"
            set gsub_message = "get new long_text_id (LONG_DATA_SEQ) failed"
        else

            /* 
             * Add product_note row
             */
            insert into product_note pn
                set pn.product_note_id          = new_product_note_id,
                    pn.product_id               = sub_product_id,
                    pn.long_text_id             = new_long_text_id,
                    pn.active_ind               = 1,
                    pn.active_status_cd         = reqdata->active_status_cd,
                    pn.active_status_dt_tm      = cnvtdatetime(curdate, curtime3),
                    pn.active_status_prsnl_id   = reqinfo->updt_id,
                    pn.updt_cnt                 = 0,
                    pn.updt_dt_tm               = cnvtdatetime(curdate, curtime3),
                    pn.updt_id                  = reqinfo->updt_id,
                    pn.updt_task                = reqinfo->updt_task,
                    pn.updt_applctx             = reqinfo->updt_applctx
            with    nocounter

        if (curqual = 0)
            set gsub_status = "F"
            set gsub_process = "insert into product_note"
            set gsub_message = "insert into product_note failed"
        else
                    
            /* Insert long_text row for product_note */
            call ADD_LONG_TEXT
               (
                /* long_text_id       */ new_long_text_id,
                /* parent_entity_name */ "PRODUCT_NOTE",
                /* parent_entity_name */ new_product_note_id,
                /* long_text          */ sub_product_note
               )

             if (curqual = 0)
                 set gsub_status = "F"
                 set gsub_process = "insert into long_text"
                 set gsub_message = "insert into long_text failed"
            endif       ; endif curqual = 0 long_text
        endif       ; endif curqual = 0 product_note
    endif       ; endif curqual = 0 get new long_text_id
endif       ; endif curqual = 0 get new pathnet_seq

/* END for subroutine is to be coded in calling program to allow for additional processing in subroutine */

;Generated by GNU enscript 1.6.4.
