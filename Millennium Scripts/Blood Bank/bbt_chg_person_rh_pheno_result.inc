 ;;Solution_Test/script/PathNet -- BB Transfusion/RESULTENTRYFORBLOODBANK/BBT_CHG_PERSON_RH_PHENO_RESULT.INC Turn on black mode

/*
 *  PURPOSE:  update person_rh_pheno_result row
 *
 *  PRELIMINARIES:
 *   1)  Include the following code in calling program:
 *           %i cclsource:bbt_chg_person_rh_pheno_result.inc
 *           END        ; end subroutine CHG_PERSON_RH_PHENO_RESULT
 *
 *   2)  Declare the followoing variables in the calling program, prior to calling the subroutine:
 *           set gsub_rh_phenotype_status     = "  "      ; (gsub = global subroutine variable)
 *
 *   3)  Call subroutine using CALL FORMAT below
 *
 *   4)  After control returns from subroutine, evaluate gsub_rh_phenotype_status as follows:
 *         "FL" = Lock forupdate failed for person_rh_pheno_result failed
 *         "FU" = Update for person_rh_pheno_result failed
 *         "OK" = Update successful                                           
 *
 *  CALL FORMAT (this may be copied into calling program): */
 ;       call CHG_PERSON_RH_PHENO_RESULT
 ;          (                             ** These <variables> set in calling program **
 ;           /* rh_phenotype_id  */ <rh_phenotype_id>
 ;           /* updt_cnt         */ , <rh_phenotype_updt_cnt>
 ;           /* active_ind       */ , <active_ind>
 ;           /* active_status_cd */ , <reqdata->in/active_status_cd>
 ;          )

subroutine CHG_PERSON_RH_PHENO_RESULT
    (
     sub_rh_pheno_rs_id
     , sub_updt_cnt
     , sub_active_ind
     , sub_active_status_cd
    )

/* Initialize gsub_rh_phenotype_status */
set gsub_rh_phenotype_status = "  "

/*
 * Lock person_rh_pheno_result row forupdate 
 */
select into "nl:"
    prpr.person_rh_pheno_rs_id
from
    person_rh_pheno_result prpr
where
    prpr.person_rh_pheno_rs_id  = sub_rh_pheno_rs_id
      and prpr.updt_cnt          = sub_updt_cnt
with    nocounter

if (curqual = 0)
    set gsub_rh_phenotype_status = "FL"
else
    /*
     * Update person_rh_pheno_result row
     */
    update into person_rh_pheno_result prpr set
        prpr.updt_cnt                        = prpr.updt_cnt + 1
        , prpr.updt_dt_tm                    = cnvtdatetime(curdate,curtime3)
        , prpr.updt_id                       = reqinfo->updt_id
        , prpr.updt_task                     = reqinfo->updt_task
        , prpr.updt_applctx                  = reqinfo->updt_applctx      
        , prpr.active_ind                    = sub_active_ind
        , prpr.active_status_cd              = sub_active_status_cd

    where   prpr.person_rh_pheno_rs_id       = sub_rh_pheno_rs_id

    with    nocounter

    if (curqual = 0)
        set gsub_rh_phenotype_status = "FU"
    else
        set gsub_rh_phenotype_status = "OK"
    endif
endif

/* END for subroutine is to be included in calling program */   

;Generated by GNU enscript 1.6.4.
