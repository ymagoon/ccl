 ;;Solution_Test/script/PathNet -- BB Transfusion/Producthistoryreview/BBT_GET_ORIGINATING_SUPPLIER.PRG Turn on black mode

/*~BB~************************************************************************
  *                                                                      *
  *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
  *                              Technology, Inc.                        *
  *       Revision      (c) 1984-1997 Cerner Corporation                 *
  *                                                                      *
  *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
  *  This material contains the valuable properties and trade secrets of *
  *  Cerner Corporation of Kansas City, Missouri, United States of       *
  *  America (Cerner), embodying substantial creative efforts and        *
  *  confidential information, ideas and expressions, no part of which   *
  *  may be reproduced or transmitted in any form or by any means, or    *
  *  retained in any storage or retrieval system without the express     *
  *  written permission of Cerner.                                       *
  *                                                                      *
  *  Cerner is a registered mark of Cerner Corporation.                  *
  *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************

        Source file name:       bbt_get_originating_supplier.prg       
        Object name:            bbt_get_originating_supplier
        Request #:              225470

        Product:                PathNet                
        Product Team:           BB Transfusion
        HNA Version:            500
        CCL Version:            4.0

        Program purpose:        Get originating supplier id and name for
                                the product id in the request structure

        Tables read:            receipt
                                bb_supplier
                                organization

        Tables updated:         <None>

        Executing from:         VB

        Special Notes:          
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;    *000 10/31/97 Badari Kopparapu     Initial Release                     *
;    *001 12/12/97 Badari Kopparapu     Modified to select rows with id >0.0*
;    *002 10/22/02 Ronda Zheng          Used new_product_id instead of curqual*
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

drop program bbt_get_originating_supplier:dba go
create program bbt_get_originating_supplier:dba

/*
record request
(
1 product_id = f8
)
*/

record reply
(
   1  organization_id = f8
   1  org_name = c40
   1  bb_supplier_id = f8
   1  receipt_updt_cnt = i4
   1  product_event_id = f8
%i cclsource:status_block.inc
)

/*
 * Initialize global variables
 */
    set count1 = 0
    set reply->status_data->status = "F"
    set select_ok_ind = 0

set new_product_id = 0.0

select into "nl:"
  pr.modified_product_id,
  pr.pooled_product_id

from  product pr

plan pr  where pr.product_id = request->product_id
           and pr.active_ind = 1
detail
    if (pr.modified_product_id > 0.0)
        new_product_id = pr.modified_product_id
elseif (pr.pooled_product_id > 0.0)
        new_product_id = pr.pooled_product_id
 endif

with nocounter

if (new_product_id > 0)
   set request->product_id = new_product_id
endif                    

select into "nl:"
    o.organization_id,
    o.org_name,
    bbs.bb_supplier_id,
    r.product_event_id

from (dummyt d_r with seq = 1), 
      receipt r,
      bb_supplier bbs,
      organization o

plan d_r
join  r   where r.product_id = request->product_id
join bbs where bbs.bb_supplier_id = r.bb_supplier_id
           and r.bb_supplier_id > 0.0 ;BNK - select non-zero rows only.
join o   where o.organization_id = bbs.organization_id
           and o.organization_id > 0.0    ;BNK - select non-zero rows only.
order by r.product_event_id

detail 
    reply->organization_id    = o.organization_id
    reply->org_name  = o.org_name
    reply->bb_supplier_id = r.bb_supplier_id
    reply->product_event_id = r.product_event_id
    reply->receipt_updt_cnt = r.updt_cnt

foot report
    select_ok_ind = 1

with nocounter, nullreport, outerjoin(d_r)

set reply->status_data->subeventstatus[count1]->OperationName       = "Get Originating Supplier"
set reply->status_data->subeventstatus[count1]->TargetObjectName    = "bbt_get_originating_supplier"

if (select_ok_ind = 1)
    if  (reply->organization_id > 0)
        set reply->status_data->status = "S"
        set reply->status_data->subeventstatus[count1]->OperationStatus     = "S"
        set reply->status_data->subeventstatus[count1]->TargetObjectValue   = 
            "SUCCESS"
    else
        set reply->status_data->status = "Z"
        set reply->status_data->subeventstatus[count1]->OperationStatus     = "S"
        set reply->status_data->subeventstatus[count1]->TargetObjectValue   = 
            "ZERO"
    endif
else
    set reply->status_data->status = "F"
    set reply->status_data->subeventstatus[count1]->OperationStatus     = "F"
    set reply->status_data->subeventstatus[count1]->TargetObjectValue   = 
        "Select on bb_supplier/organization failed"
endif


end go

;Generated by GNU enscript 1.6.4.
