 ;;Solution_Test/script/PathNet -- BB Transfusion/Bbt_Bbdbtools/bbt_create_dta_evt_cds.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       bbt_create_dta_evt_cds.prg
        Object name:            bbt_create_dta_evt_cds
        Request #:              n/a

        Product:                PathNet Blood Bank Transfusion                          
        Product Team:           PathNet Blood Bank Transfusion
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        To create a comma separated file that contains
                                the information needed to do an initial load of 
                                event codes for blood bank's detail procedures.

        Tables read:            discrete_task_assay, event_code_alias_r
        Tables updated:         event_code
        Executing from:         

        Special Notes:          

******************************************************************************/

 
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     000 05/14/97 Cheryl Fay           Modification for blood bank         *
;     000 08/22/97 Lisa Grayson         Use new table for event code aliases*
;     003 02/06/00 Martin Myers         Moved standard evnt cd to dif script*
;~DE~************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

DROP PROGRAM BBT_CREATE_DTA_EVT_CDS:DBA GO
CREATE PROGRAM BBT_CREATE_DTA_EVT_CDS:DBA

record internal
( 1 int_rec[*]
    2 task_assay_cd = f8
    2 mnemonic = vc
)

%i cclsource:dm_post_event_code.inc

set cnt = 0
SET CODE_VALUE = 0.0
SET CODE_SET = 0
SET CDF_MEANING = FILLSTRING(12, " ")

SET CODE_SET = 106
SET CDF_MEANING = "BB"
EXECUTE CPM_GET_CD_FOR_CDF
SET BB_ACT_CD = CODE_VALUE

; REMOVE THE BILL ONLYS
SET CODE_VALUE = 0.0
SET GLAB_RESULT_TYPE_BILL_CD = 0.0
SET CODE_SET = 0
SET CDF_MEANING = FILLSTRING(12, " ")
SET CODE_SET = 289
SET CDF_MEANING = "17"
EXECUTE CPM_GET_CD_FOR_CDF
SET BB_RESULT_TYPE_BILL_CD = CODE_VALUE

set cnt = 0 
set x = 0

SELECT INTO "nl:"
      DTA.MNEMONIC,
      DTA.TASK_ASSAY_CD
FROM DISCRETE_TASK_ASSAY DTA
  WHERE DTA.ACTIVITY_TYPE_CD = BB_ACT_CD and
        DTA.DEFAULT_RESULT_TYPE_CD != BB_RESULT_TYPE_BILL_CD AND
        DTA.ACTIVE_IND = 1

HEAD REPORT
  cnt = 0	

detail
  cnt = cnt + 1
  stat = alterlist(internal->int_rec, cnt)
  internal->int_rec[cnt]->task_assay_cd = DTA.TASK_ASSAY_CD
  internal->int_rec[cnt]->mnemonic = DTA.MNEMONIC
with nocounter
Call echo(build("COUNT :", cnt))

/**********************************/
/** ADDING AN ENTRY FOR EACH DTA **/
/**********************************/

for (x = 1 to cnt)
  set dm_post_event_code->event_set_name = 
       substring(1,40,internal->int_rec[x]->mnemonic)
  set dm_post_event_code->event_cd_disp = 
       substring(1,40,internal->int_rec[x]->mnemonic)
  set dm_post_event_code->event_cd_descr = 
       substring(1,60,internal->int_rec[x]->mnemonic)
  set dm_post_event_code->event_cd_definition = internal->int_rec[x]->mnemonic
  set dm_post_event_code->status = "ACTIVE" 
  set dm_post_event_code->format = "UNKNOWN"
  set dm_post_event_code->storage = "UNKNOWN"
  set dm_post_event_code->event_class = "UNKNOWN"
  set dm_post_event_code->event_confid_level = "ROUTCLINICAL"
  set dm_post_event_code->event_subclass = "UNKNOWN"
  set dm_post_event_code->event_code_status = "AUTH"
  set dm_post_event_code->event_cd = 0.0
  set dm_post_event_code->parent_cd = internal->int_rec[x]->task_assay_cd
  set dm_post_event_code->flex1_cd = 0.0
  set dm_post_event_code->flex2_cd = 0.0
  set dm_post_event_code->flex3_cd = 0.0
  set dm_post_event_code->flex4_cd = 0.0
  set dm_post_event_code->flex5_cd = 0.0

  execute dm_post_event_code

  call echo(build("added :", dm_post_event_code->event_cd_disp,"->",
       dm_post_event_code->parent_cd,"->", dm_post_event_code->event_cd))

endfor

END GO

;Generated by GNU enscript 1.6.4.
