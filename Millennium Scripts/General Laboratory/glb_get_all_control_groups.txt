 ;;Solution_Test/script/PathNet -- Gen Lab/glb_get_all_control_groups/glb_get_all_control_groups.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       glb_get_all_control_groups.prg
        Object name:            glb_get_all_control_groups
        Request #:              257592

        Product:                PathNet                             
        Product Team:           General Lab      
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Retrieve all control groups that the user is
                                allowed to see.
                                
        Tables read:            code_value,
                                qc_group_ctrl_resource    
        
        Tables updated:         None
        Executing from:         QC Benchmark dll 

        Special Notes:          None                                                                                

******************************************************************************/

 
;~DB~****************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG                  *
;    ****************************************************************************
;    *                                                                          *
;    *Mod Date     Engineer             Comment                                 *
;    *--- -------- -------------------- --------------------------------------- *
;     001 06/09/05 Scott Sundahl        Initial release                         *
;~DE~****************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

drop program glb_get_all_control_groups:dba go
create program glb_get_all_control_groups:dba


/****************************************************************************
* Declarations                                                              *
*****************************************************************************/

/*
record request
(
)
*/

record reply
(
  1 qual[*]
    2 control_group_cd        = f8
    2 control_group_name      = vc
    2 control_group_updt_cnt  = i4 ; not used.  necessary for vb srv indexing
    2 active_ind              = i2
%i cclsource:status_block.inc
)

%i cclsource:pcs_resource_security.inc

call InitResourceSecurity(1)

declare nAvailableServRes     = i2 with protect, noconstant(0)
declare dTempServRes          = f8 with protect, noconstant(0.0)
declare lCount                = i4 with protect, noconstant(0)

set reply->status_data.status = "F"
/****************************************************************************
* Populate Reply                                                            *
*****************************************************************************/

select into "nl:"
  control_group_found = evaluate(nullind(qc.qc_group_ctrl_resource_id), 0, true, false)

from code_value cv,
     qc_group_ctrl_resource qc
     
plan cv where cv.code_set = 1909

join qc where qc.control_group_cd = outerjoin(cv.code_value)

order by qc.control_group_cd

head qc.control_group_cd
  nAvailableServRes = false
  
detail
  if (nAvailableServRes = false and control_group_found = true)
    dTempServRes = qc.service_resource_cd 
    if (dTempServRes != 0.0)
      if (IsResourceViewable(dTempServRes) = true)
        nAvailableServRes = true
      endif
    endif
  endif

foot qc.control_group_cd
  if (control_group_found = false or nAvailableServRes = true)
    lCount = lCount + 1
    if (lCount > size(reply->qual, 5))
      stat = alterlist(reply->qual, lCount + 9)
    endif
    reply->qual[lCount].control_group_cd = cv.code_value 
    reply->qual[lCount].control_group_name = cv.definition 
    reply->qual[lCount].active_ind = cv.active_ind 
  endif

with nocounter

if (GetResourceSecurityStatus(0) = "F")
  set reply->status_data->subeventstatus[1].TargetObjectValue = "error checking service resource security."
else
  if (lCount > 0)
    set stat = alterlist(reply->qual, lCount)
    set reply->status_data.status = "S"
  else
    set reply->status_data.status = "Z"
  endif
endif 

end 

go

;Generated by GNU enscript 1.6.4.
