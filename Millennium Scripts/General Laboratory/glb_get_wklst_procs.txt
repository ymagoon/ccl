 ;;Solution_Test/script/PathNet -- Gen Lab/glb_get_wklst_procs/glb_get_wklst_procs.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1996 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       glb_get_wklst_procs.prg
        Object name:            glb_get_wklst_procs
        Request #:              250048

        Product:                General Lab
        Product Team:           PathNet
        HNA Version:            500
        CCL Version:            4.0

        Program purpose:        

        Tables read:            code_value

        Tables updated:         None
        Executing from:         

        Special Notes:          

******************************************************************************/

;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;    *000 05/14/96 Dennis Boyer         Initial Release                     *
;~DE~************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

             drop program GLB_GET_WKLST_PROCS:dba go    
             create program GLB_GET_WKLST_PROCS:dba
     
      /***********************************************************************
      * Request Record                                                       *
      ***********************************************************************/
      /*
      record request
      (
         1  worklist_id                  = f8
      )
      */

      /***********************************************************************
      * Reply Record                                                         *
      ***********************************************************************/
      record reply
      ( 
         1  qual[*]
            2  accession                 = c20
            2  order_mnemonic            = vc
            2  order_status_cd           = f8
            2  report_priority_cd        = f8
            2  name_full_formatted       = vc
%i cclsource:status_block.inc
      )
      
      /***********************************************************************
      * Script                                                               *
      ***********************************************************************/
      #Script
         declare q_cnt                   = i4
         set reply->status_data.status = "F"

         select into "nl:"
            w.order_id,
            o.order_mnemonic,
            o.order_status_cd,
            oa.accession,
            d1.seq,
            ol_exists = decode(ol.seq, "Y", "N"),
            ol.report_priority_cd,
            d2.seq,
            p_exists = decode(p.seq, "Y", "N"),
            p.name_full_formatted

         from
            worklist_order_r w,
            orders o,
            accession_order_r oa,
            dummyt d1,
            order_laboratory ol,
            dummyt d2,
            person p

         plan w  where w.worklist_id = request->worklist_id
         join o  where o.order_id = w.order_id
         join oa where oa.order_id = w.order_id
         join d1 where d1.seq = 1
         join ol where ol.order_id = o.order_id
         join d2 where d2.seq = 1
         join p  where p.person_id = o.person_id

         head report
            q_cnt = 0

         detail
            q_cnt = q_cnt + 1
            if (mod(q_cnt,10) = 1)
               stat = alterlist(reply->qual, q_cnt + 10)
            endif
            reply->qual[q_cnt].order_mnemonic = o.order_mnemonic
            reply->qual[q_cnt].order_status_cd = o.order_status_cd 
            reply->qual[q_cnt].accession = oa.accession
            if (ol_exists = "Y")
               reply->qual[q_cnt].report_priority_cd = ol.report_priority_cd
            endif
            if (p_exists = "Y")
               reply->qual[q_cnt].name_full_formatted = p.name_full_formatted
            endif

         foot report
            stat = alterlist(reply->qual, q_cnt)

         with nocounter,
            outerjoin = d1,
            dontcare = ol,
            outerjoin = d2,
            dontcare = p

         if (curqual = 0)
            set reply->status_data.status = "Z"
         else
            set reply->status_data.status = "S"
         endif

         end
         go

;Generated by GNU enscript 1.6.4.
