 ;;Solution_Test/script/PathNet -- Gen Lab/glb_add_dif_format/glb_add_dif_format.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************
        Source file name:       GLB_ADD_DIF_FORMAT.PRG
        Object name:            GLB_ADD_DIF_FORMAT
        Request #:              250079

        Product:                General Lab           
        Product Team:           GLB
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Insert a new differential count format
                                into the database

        Tables read:            n/a

        Tables updated:         differential_ref
                                differential_ref_assay

        Executing from:         General Lab build/maintain differential

        Special Notes:          n/a
*******************************************************************************/
;~DB~***************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG                 *
;    ***************************************************************************
;    *                                                                         *
;    *Mod Date     Engineer             Comment                                *
;    *--- -------- -------------------- -------------------------------------- *
;     000 05/08/96 Kevin Brunner        Initial Release                        *
;     001 12/12/97 Chad Moats           Added non_total_trigger_ind to request *
;~DE~***************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ***********************


      drop program GLB_ADD_DIF_FORMAT:dba go    
      create program GLB_ADD_DIF_FORMAT:dba
     
/********************************************************************/
/*  Request Record                                                  */
/********************************************************************/
/*
      record request                                      
      (
         1 dif_ref_id               = f8
         1 catalog_cd               = f8
         1 opt                      = c20
         1 opt_key                  = c20
         1 default_count            = i4
         1 normalize_ind            = i2
         1 subtract_key             = c1
         1 auto_correct_ind         = i2
         1 auto_correct_value       = f8
         1 auto_correct_assay_cd    = f8
         1 auto_correct_comment_ind = i2
         1 auto_correct_comment     = vc
         1 auto_correct_verf_ind    = i2
         1 instr_label              = c30
         1 count_label              = c30
         1 morph_label              = c30
         1 components[x]
           2 task_assay_cd          = f8
           2 display_name           = c10
           2 proc_type_flag         = i2
           2 count_key              = c1
           2 zero_ind               = i2
           2 non_total_ind          = i2
           2 non_total_trigger_ind  = i2                                ;001
           2 sequence               = i2
      ) 

*/
record reqinfo
( 1 commit_ind  = i2
  1 updt_id     = f8
  1 position_cd = f8
  1 updt_app    = i4
  1 updt_task   = i4
  1 updt_req    = i4
  1 updt_applctx= i4
)

/********************************************************************/
/*  Reply Record                                                    */
/********************************************************************/

      record reply
      ( 
         1 status_data
           2 status                = c1
           2 subeventstatus[1]
             3 OperationName       = c15
             3 OperationStatus     = c1
             3 TargetObjectName    = c15
             3 TargetObjectValue   = vc
      )

     #Script
      declare dif_ref_id = f8 with protect, noconstant(0.0)
      set failed = "F"
      set dif_ref_id = 0
      set comp_cnt = size(request->components,5)
      set reply->status_data->status = "F"

/*******************************************************************/
/* CHECK differential_ref TABLE to ensure that d.catalog_cd and 
/* d.opt_key combo does not already exsist on differential_ref TABLE                                        
/*******************************************************************/
      SELECT INTO "nl:"
      
      FROM differential_ref d
      
      WHERE d.catalog_cd = request->catalog_cd AND d.opt_key = cnvtupper(cnvtalphanum(request->opt_key))
      
      WITH NOCOUNTER 
      
      IF (curqual > 0)
        GO TO row_exsists_dif_ref
      ENDIF
        
/*******************************************************************/
/* Get new dif_ref_id.                                             */
/*******************************************************************/
      SELECT 
         into "nl:"
            y = seq(reference_seq,nextval) "##################;rp0"
      FROM 
         dual
      DETAIL
         dif_ref_id = y
      WITH
         format, counter
      if (curqual = 0)
         go to seq_failed
      endif
      set request->dif_ref_id = dif_ref_id

/*******************************************************************/
/* Insert row into differential_ref table                          */
/*******************************************************************/
      INSERT
         into differential_ref d
      SET
         d.dif_ref_id               = dif_ref_id,
         d.catalog_cd               = request->catalog_cd,
         d.opt                      = request->opt,
         d.opt_key                  = cnvtupper(cnvtalphanum(request->opt_key)),
         d.default_count            = request->default_count,
         d.normalize_ind            = request->normalize_ind,
         d.subtract_key             = request->subtract_key,
         d.auto_correct_ind         = request->auto_correct_ind,
         d.auto_correct_value       = request->auto_correct_value,
         d.auto_correct_assay_cd    = request->auto_correct_assay_cd,
         d.auto_crct_cmt_ind        = request->auto_correct_comment_ind,
         d.auto_crct_cmt            = request->auto_correct_comment,
         d.auto_correct_verf_ind    = request->auto_correct_verf_ind,
         d.instr_label              = request->instr_label,
         d.count_label              = request->count_label,
         d.morph_label              = request->morph_label,
         d.updt_dt_tm               = cnvtdatetime(curdate,curtime),
         d.updt_id                  = reqinfo->updt_id,
         d.updt_task                = reqinfo->updt_task,
         d.updt_cnt                 = 0,
         d.updt_applctx             = reqinfo->updt_applctx
      WITH
         nocounter
     
      if (curqual = 0)
         go to d_insert_failed
      endif     

/*******************************************************************/
/* Insert rows into differential_ref_assay component table.        */
/*******************************************************************/
      INSERT
         into differential_ref_assay dc,
              (dummyt d1 with seq = value(comp_cnt))
      SET         
         dc.dif_ref_id             = dif_ref_id,
         dc.task_assay_cd          = request->components[d1.seq]->task_assay_cd,
         dc.display_name           = request->components[d1.seq]->display_name,
         dc.proc_type_flag         = request->components[d1.seq]->proc_type_flag,
         dc.count_key              = cnvtupper(request->components[d1.seq]->count_key),
         dc.zero_ind               = request->components[d1.seq]->zero_ind,
         dc.non_total_ind          = request->components[d1.seq]->non_total_ind,
         dc.non_total_trigger_ind  = request->components[d1.seq]->non_total_trigger_ind,        ;001
         dc.position_seq           = request->components[d1.seq]->sequence,
         dc.updt_dt_tm             = cnvtdatetime(curdate,curtime),
         dc.updt_id                = reqinfo->updt_id,
         dc.updt_task              = reqinfo->updt_task,
         dc.updt_cnt               = 0,
         dc.updt_applctx           = reqinfo->updt_applctx
      PLAN (d1
      JOIN dc
          where dc.task_assay_cd = request->components[d1.seq]->task_assay_cd)
      WITH
         nocounter,
         outerjoin = d1

      if (curqual = 0)
         go to dc_insert_failed
      endif
      go to exit_script

/********************************************************************/ 
/* Executed if differential_ref table insert is unsuccessful.       */
/********************************************************************/
     #row_exsists_dif_ref
      set reply->status_data->subeventstatus[1]->operationname = 
         "insert"
      set reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      set reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue = 
         "Option has already been entered into table."
      set failed = "T"
      go to exit_script
      
/********************************************************************/ 
/* Executed if sequence cannot be obtained.                         */
/********************************************************************/
     #seq_failed
      set reply->status_data->subeventstatus[1]->operationname = 
         "seq"
      set reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      set reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue = 
         "equation_id"
      set failed = "T"
      go to exit_script

/********************************************************************/ 
/* Executed if differential_ref table insert is unsuccessful.       */
/********************************************************************/
     #d_insert_failed
      set reply->status_data->subeventstatus[1]->operationname = 
         "insert"
      set reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      set reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue = 
         "equation table"
      set failed = "T"
      go to exit_script

/********************************************************************/ 
/* Executed if differential_ref_assay table insert is unsuccessful. */
/********************************************************************/
     #dc_insert_failed
      set reply->status_data->subeventstatus[1]->operationname = 
         "insert"
      set reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      set reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue = 
         "differential_ref_assay table"
      set failed = "T"
      go to exit_script

/********************************************************************/ 
/* Script exit.                                                     */
/********************************************************************/
     #exit_script
      if (failed = "F")
         set reply->status_data->status = "S"
         commit
      else
         rollback
      endif
END
GO     

;Generated by GNU enscript 1.6.4.
