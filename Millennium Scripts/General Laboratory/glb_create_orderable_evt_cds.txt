 ;;Solution_Test/script/PathNet -- Gen Lab/glb_create_orderable_evt_cds/glb_create_orderable_evt_cds.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1999 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       glb_create_orderable_evt_cds.prg
        Object name:            glb_create_orderable_evt_cds
        Request #:              n/a

        Product:                PathNet - General Lab
        Product Team:           PathNet - General Lab
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Read the orderable procedures for 
                                General Lab and create the resolution
                                table to event codes.

        Tables read:            code_value
                                order_catalog
                                order_catalog_synonym
        Tables updated:         
        Executing from:         

        Special Notes:          In general, this program only needs to be
                                run at client sites who require ESI to
                                post inbound results to clinical events.
                                The catalog_cd does not post to the
                                clinical_events table unless ESI is 
                                configured post it. 

******************************************************************************/

 
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;    *000 08/03/98 Hal Patchen          Initial Release                     *
;~DE~************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

DROP PROGRAM GLB_CREATE_ORDERABLE_EVT_CDS GO
CREATE PROGRAM GLB_CREATE_ORDERABLE_EVT_CDS
paint
call clear(1,1)
call box(1,1,3,80)
call text(2,3, "CREATE ORDERABLE EVENT CODES")
call text(5,3,  
  "This program will create event codes for General Lab order catalog")
call text(6,3,  
  "items (i.e. catalog_cd).  In general, you do not need to create")
call text(7,3,  
  "orderable event codes unless your organization has a need to post")
call text(8,3,  
  "results into the Open Clinical Foundation (OCF) data repository")
call text(9,3,  
  "through an External Systems Interface (ESI).")
call text(24,3, "Create orderable event codes?")
call accept(24,35, "P;;CU", "N" where curaccept in ("Y", "N"))
if (curaccept = "N")
   go to Exit_Script
endif
set message nowindow

record internal
( 
  1 int_rec[*]
    2 catalog_cd = f8
    2 mnemonic = vc ;100
)

%i cclsource:dm_post_event_code.inc

SET CODE_VALUE = 0.0
SET CODE_SET = 0
SET CDF_MEANING = FILLSTRING(12, " ")
SET PRIMARY_CD = 0.0
SET GLB_CAT_CD = 0.0
SET LAB_source_cd = 0.0

SET CODE_SET = 6000
SET CDF_MEANING = "GENERAL LAB"
EXECUTE CPM_GET_CD_FOR_CDF
SET GLB_CAT_CD = CODE_VALUE

SET CODE_SET = 73
SET CDF_MEANING = "LAB"
EXECUTE CPM_GET_CD_FOR_CDF
SET LAB_source_cd = CODE_VALUE

SET CODE_SET = 6011
SET CDF_MEANING = "PRIMARY"
EXECUTE CPM_GET_CD_FOR_CDF
SET PRIMARY_CD = CODE_VALUE

set cnt = 0
set x = 0

SELECT INTO "NL:"
       OC.SEQ
      ,OC.CATALOG_CD
      ,OC.CATALOG_TYPE_CD
      ,OC.ACTIVITY_TYPE_CD	
      ,OCS.MNEMONIC_TYPE_CD
      ,OCS.MNEMONIC
FROM ORDER_CATALOG OC, ORDER_CATALOG_SYNONYM OCS
PLAN OC
  WHERE OC.CATALOG_TYPE_CD = GLB_CAT_CD and
        OC.ACTIVE_IND = 1
JOIN OCS
  WHERE OC.CATALOG_CD = OCS.CATALOG_CD AND
        OCS.MNEMONIC_TYPE_CD = PRIMARY_CD AND
        OCS.ACTIVE_IND = 1

ORDER BY OCS.MNEMONIC

HEAD REPORT
  cnt = 0

detail
  cnt = cnt + 1
  stat = alterlist(internal->int_rec, cnt)
  internal->int_rec[cnt]->catalog_cd = oc.catalog_cd
  internal->int_rec[cnt]->mnemonic = ocs.mnemonic
with nocounter

call echo (build("count :", cnt))

/****************************************/
/** ADDING AN ENTRY FOR EACH ORDERABLE **/
/****************************************/

for (x = 1 to cnt)
  set dm_post_event_code->event_set_name = substring(1,40,internal->int_rec[x]->mnemonic)
  set dm_post_event_code->event_cd_disp = substring(1,40,internal->int_rec[x]->mnemonic)
  set dm_post_event_code->event_cd_descr = substring(1,60,internal->int_rec[x]->mnemonic)
  set dm_post_event_code->event_cd_definition = internal->int_rec[x]->mnemonic
  set dm_post_event_code->status = "ACTIVE" 
  set dm_post_event_code->format = "UNKNOWN"
  set dm_post_event_code->storage = "UNKNOWN"
  set dm_post_event_code->event_class = "UNKNOWN"
  set dm_post_event_code->event_confid_level = "ROUTCLINICAL"
  set dm_post_event_code->event_subclass = "UNKNOWN"
  set dm_post_event_code->event_code_status = "AUTH"
  set dm_post_event_code->event_cd = 0.0
  set dm_post_event_code->parent_cd = internal->int_rec[x]->catalog_cd
  set dm_post_event_code->flex1_cd = 0.0
  set dm_post_event_code->flex2_cd = 0.0
  set dm_post_event_code->flex3_cd = 0.0
  set dm_post_event_code->flex4_cd = 0.0
  set dm_post_event_code->flex5_cd = 0.0

  execute dm_post_event_code

  call echo(build("added :", dm_post_event_code->event_cd_disp,"->",
       dm_post_event_code->parent_cd,"->", dm_post_event_code->event_cd))
endfor                      

/********************************************/
/** ADDING ENTRY FOR LAB DEFAULT CODE      **/
/** PARENT_CD = LAB  (CONTRIBUTOR_SOURCE)  **/
/********************************************/

set dm_post_event_code->event_set_name = FILLSTRING(40, " ")
set dm_post_event_code->event_cd_disp = "LAB"
set dm_post_event_code->event_cd_descr = "LAB"
set dm_post_event_code->event_cd_definition = "LAB"
set dm_post_event_code->status = "ACTIVE" 
set dm_post_event_code->format = "UNKNOWN"
set dm_post_event_code->storage = "UNKNOWN"
set dm_post_event_code->event_class = "UNKNOWN"
set dm_post_event_code->event_confid_level = "ROUTCLINICAL"
set dm_post_event_code->event_subclass = "UNKNOWN"
set dm_post_event_code->event_code_status = "AUTH"
set dm_post_event_code->event_cd = 0.0
set dm_post_event_code->parent_cd = LAB_source_cd
set dm_post_event_code->flex1_cd = 0.0
set dm_post_event_code->flex2_cd = 0.0
set dm_post_event_code->flex3_cd = 0.0
set dm_post_event_code->flex4_cd = 0.0
set dm_post_event_code->flex5_cd = 0.0

execute dm_post_event_code

call echo(build("added :", dm_post_event_code->event_cd_disp,"->",
          dm_post_event_code->parent_cd,"->", dm_post_event_code->event_cd))

#Exit_Script

END
GO

;Generated by GNU enscript 1.6.4.
