 ;;Solution_Test/script/PathNet -- Gen Lab/glb_get_review_item/glb_get_review_item.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-2003 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       glb_get_review_item.prg
        Object name:            glb_get_review_item
        Request #:              250160

        Product:                General Lab
        Product Team:           PathNet
        HNA Version:            500
        CCL Version:            4.0

        Program purpose:        This program checks to see if a particular order is 
                                on the pcs_review_item table or the test_reviewer 
                                table.  It only checks the test_reviewer table if the
                                order has an activity type of HLA.  When the user 
                                attempts to resend results to ESO and they are not
                                found on the clinical_event table, this script checks
                                to see if they are in the review queue.
                                
        Tables read:            pcs_review_item

        Tables updated:         None
        Executing from:         OrderResultViewer.exe

        Special Notes:          
******************************************************************************/

;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;    *000 03/06/03 Rob Peterson         Initial Release                     *
;~DE~************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

  drop program GLB_GET_REVIEW_ITEM:dba go
  create program GLB_GET_REVIEW_ITEM:dba

  /***********************************************************************
  * Request Record                                                       *
  ***********************************************************************/
  /*
  record request
  (
     1  order_id                  = f8
     2  activity_type_cd          = c12
  )
  */

  /***********************************************************************
  * Reply Record                                                         *
  ***********************************************************************/
  record reply
  (
     1  qual[*]
        2  review_status_cd = f8
        2  review_status_meaning = c12
        2  review_status_display = c40
%i cclsource:status_block.inc
  )

  /****************************************************************************
  *       Declare Variables                                                   *
  *****************************************************************************/
  declare count                = i4 with protect, noconstant(0)
  declare hla_activity_type_cd = f8 with protect, noconstant(0.0)
  declare found_flag           = i2 with protect, noconstant(0)

  set reply->status_data.status = "F"

  set stat = uar_get_meaning_by_codeset(106, "HLA", 1, hla_activity_type_cd)

  if(request->activity_type_cd = hla_activity_type_cd)
    /***************************************************************************
    * Select result_status_cd from the test_reviewer table for the order_id    *
    * passed into the script.  This is only done when the activity type is HLA *
    ***************************************************************************/
    select into "nl:"

    from
      test_reviewer t

    where t.order_id = request->order_id

    head report
      count = 0

    detail
      count = count + 1
      if (mod(count,10) = 1)
        stat = alterlist(reply->qual,count + 9)
      endif
      reply->qual[count].review_status_cd = t.review_status_cd
      reply->qual[count].review_status_meaning = uar_get_code_meaning(t.review_status_cd)
      reply->qual[count].review_status_display = uar_get_code_display(t.review_status_cd)
    with nocounter

    ;Set found_flag to 1 when items are found on the test_reviewer table
    if (count > 0)
      set found_flag = 1
    endif

  endif

  if (found_flag = 0)
    /************************************************************************
    * If the order is not an HLA order or is not found on the test_reviewer *
    * table, the select result_status_cd from the pcs_review_item table for *
    * the order_id passed into the script                                   *
    ************************************************************************/
    select into "nl:"

    from
      pcs_review_item p

    where p.order_id = request->order_id

    head report
      count = 0

    detail
      count = count + 1
      if (mod(count,10) = 1)
        stat = alterlist(reply->qual,count + 9)
      endif
      reply->qual[count].review_status_cd = p.review_status_cd
      reply->qual[count].review_status_meaning = uar_get_code_meaning(p.review_status_cd)
      reply->qual[count].review_status_display = uar_get_code_display(p.review_status_cd)
    with nocounter
  endif

  if (count > 0)
    set stat = alterlist(reply->qual, count)
    set reply->status_data.status = "S"
  else
    set reply->status_data.status = "Z"
  endif

end go

;Generated by GNU enscript 1.6.4.
