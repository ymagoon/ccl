 ;;Solution_Test/script/PathNet -- Gen Lab/glb_chg_dif_format/glb_chg_dif_format.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************
        Source file name:       GLB_CHG_DIF_FORMAT.PRG
        Object name:            GLB_CHG_DIF_FORMAT
        Request #:              250080

        Product:                General Lab           
        Product Team:           GLB
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Update a differential count format
                                in the database

        Tables read:            n/a

        Tables updated:         differential_ref
                                differential_ref_assay

        Executing from:         General Lab build/maintain differential

        Special Notes:          n/a
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     500 05/08/96 Newton Hughes        Initial Release                     *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

      drop program GLB_CHG_DIF_FORMAT:dba go    
      create program GLB_CHG_DIF_FORMAT:dba
     
/********************************************************************/
/*  Request Record                                                  */
/********************************************************************/
/*
      record request                                      
      (
         1 dif_ref_id               = f8
         1 catalog_cd               = f8
         1 opt                      = c20
         1 opt_key                  = c20
         1 default_count            = i4
         1 normalize_ind            = i2
         1 subtract_key             = c1
         1 auto_correct_ind         = i2
         1 auto_correct_value       = f8
         1 auto_correct_assay_cd    = f8
         1 auto_correct_equation_id = f8
         1 auto_correct_verf_ind    = i2
         1 count_label              = c30
         1 morph_label              = c30
         1 instr_label              = c30
         1 components[x]
           2 task_assay_cd          = f8
           2 display_name           = c10
           2 proc_type_flag         = i2
           2 count_key              = c1
           2 zero_ind               = i2
           2 non_total_ind          = i2
      ) 
*/

/********************************************************************/
/*  Reply Record                                                    */
/********************************************************************/

      record reply
      ( 
%i cclsource:status_block.inc
      )
     
     #Script
      set failed = "F"
      set comp_cnt = size(request->components,5)
      set reply->status_data->status = "F"

/*******************************************************************/
/* Update row into differential_ref table                          */
/*******************************************************************/
      UPDATE
         into differential_ref d
      SET
         d.catalog_cd               = request->catalog_cd,
         d.opt                      = request->opt,
         d.opt_key                  = cnvtupper(cnvtalphanum(request->opt_key)),
         d.default_count            = request->default_count,
         d.normalize_ind            = request->normalize_ind,
         d.subtract_key             = request->subtract_key,
         d.auto_correct_ind         = request->auto_correct_ind,
         d.auto_correct_value       = request->auto_correct_value,
         d.auto_correct_assay_cd    = request->auto_correct_assay_cd,
         d.auto_correct_equation_id = request->auto_correct_equation_id,
         d.auto_correct_verf_ind    = request->auto_correct_verf_ind,
         d.count_label              = request->count_label,
         d.morph_label              = request->morph_label,
         d.instr_label              = request->instr_label,
         d.updt_dt_tm               = cnvtdatetime(curdate,curtime),
         d.updt_id                  = reqinfo->updt_id,
         d.updt_task                = reqinfo->updt_task,
         d.updt_cnt                 = d.updt_cnt + 1,
         d.updt_applctx             = reqinfo->updt_applctx
      WHERE
        d.dif_ref_id = request->dif_ref_id
      WITH
         nocounter
     
      if (curqual = 0)
         go to d_update_failed
      endif     

/*******************************************************************/
/* Update rows into differential_ref_assay component table.        */
/*******************************************************************/
      UPDATE
         into differential_ref_assay dc,
              (dummyt d1 with seq = value(comp_cnt))
      SET         
         dc.task_assay_cd  = request->components[d1.seq]->task_assay_cd,
         dc.display_name   = request->components[d1.seq]->display_name,
         dc.proc_type_flag = request->components[d1.seq]->proc_type_flag,
         dc.count_key      = cnvtupper(request->components[d1.seq]->count_key),
         dc.zero_ind       = request->components[d1.seq]->zero_ind,
         dc.non_total_ind  = request->components[d1.seq]->non_total_ind,
         dc.updt_dt_tm     = cnvtdatetime(curdate,curtime),
         dc.updt_id        = reqinfo->updt_id,
         dc.updt_task      = reqinfo->updt_task,
         dc.updt_cnt       = dc.updt_cnt + 1,
         dc.updt_applctx   = reqinfo->updt_applctx
      PLAN (d1
      JOIN dc
         WHERE
            (dc.dif_ref_id = request->dif_ref_id) and
            (dc.task_assay_cd = request->components[d1.seq]->task_assay_cd))
      WITH
         nocounter,
         outerjoin = d1

      if (curqual = 0)
         go to dc_update_failed
      endif
      go to exit_script

/********************************************************************/ 
/* Executed if differential_ref table update is unsuccessful.       */
/********************************************************************/
     #d_update_failed
      set reply->status_data->subeventstatus[1]->operationname = 
         "update"
      set reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      set reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue = 
         "equation table"
      set failed = "T"
      go to exit_script

/********************************************************************/ 
/* Executed if differential_ref_assay table update is unsuccessful. */
/********************************************************************/
     #dc_update_failed
      set reply->status_data->subeventstatus[1]->operationname = 
         "update"
      set reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      set reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue = 
         "differential_ref_assay table"
      set failed = "T"
      go to exit_script

/********************************************************************/ 
/* Script exit.                                                     */
/********************************************************************/
     #exit_script
      if (failed = "F")
         set reply->status_data->status = "S"
         commit
      else
         rollback
      endif
END
GO     

;Generated by GNU enscript 1.6.4.
