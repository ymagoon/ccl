 ;;Solution_Test/script/PathNet -- Gen Lab/glb_get_prefs/glb_get_prefs.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************
        Source file name:       GLB_GET_PREFS.PRG
        Object name:            GLB_GET_PREFS
        Request #:              250198

        Product:                General Lab
        Product Team:           GLB
        HNA Version:            500
        CCL Version:            4.0

        Program purpose:        Retrieve global prefs info from DM_PREFS

        Tables read:            dm_prefs

        Tables updated:         n/a

        Executing from:         General Lab Preference Tool

        Special Notes:          n/a
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     ### 05/02/00 Gary Runde           Initial Release                     *
;     001 09/12/00 Chris Heil           Retrieve consistency comments prefs *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

      drop program GLB_GET_PREFS:dba go
      create program GLB_GET_PREFS:dba

/********************************************************************/
/*  Request Record                                                  */
/********************************************************************/
/*
      record request
      (
        1 domains[*]
          2  pref_domain         = vc
      )
*/

/********************************************************************/
/*  Reply Record                                                    */
/********************************************************************/

      record reply
      (
         1 qual[*]
           2 pref_domain               = vc
           2 pref_section              = vc
           2 pref_name                 = vc
           2 pref_nbr                  = i4
           2 pref_id                   = f8
           2 pref_cd                   = f8
         1 template_qual[*]
           2 template_name             = vc
           2 template_id               = f8
%I cclsource:status_block.inc
      )

     #Script
      declare lCnt					= i4 with protected, noconstant(0)
      set reply->status_data->status = "F"
      set sErrorMsg = FILLSTRING(255," ")    ;string containing error message
      set error_check = ERROR(sErrorMsg,1)   ;reset error queue to empty
      set num_prefs = size(request->domains, 5)
      set glb_cd = 0.0
      set cdf_meaning = fillstring(12, " ")
      

      set pCnt = 0

      if (num_prefs = 0)
          go to exit_script
      endif

/********************************************************************/
/* Get the necessary prefs off of the DM_PREFS table                */
/********************************************************************/

      select into "nl:"
         p.pref_section,
         p.pref_nbr,
         p.pref_domain,
         p.pref_id,
         p.pref_name
      from
         dm_prefs p
         
      where expand(lCnt, 1, value(num_prefs), p.pref_domain, request->domains[lCnt].pref_domain)

      head report
          pCnt = 0

      detail
          pCnt = pCnt + 1
          if (mod(pCnt, 5) = 1)
             stat = alterlist(reply->qual, pCnt + 5)
          endif
          reply->qual[pCnt].pref_domain = p.pref_domain
          reply->qual[pCnt].pref_section = p.pref_section
          reply->qual[pCnt].pref_name = p.pref_name
          reply->qual[pCnt].pref_nbr = p.pref_nbr
          reply->qual[pCnt].pref_id = p.pref_id
          reply->qual[pCnt].pref_cd = p.pref_cd

      foot report
          stat = alterlist(reply->qual, pCnt)

      with nocounter

      set error_check = ERROR(sErrorMsg,0)   ;get error message
      if (error_check != 0)
        set reply->status_data->status = "F"
        set reply->status_data->subeventstatus[1]->TargetObjectValue = sErrorMsg
        go to exit_script
      endif


/********************************************************************/
/* Retrieve the consistency comment templates.                      */
/********************************************************************/
      set code_set = 106
      set code_value = 0.0
      set cdf_meaning = "GLB"
      execute cpm_get_cd_for_cdf
      set glb_cd = code_value

      select into "nl:"
         wp.short_desc,
         wp.template_id
      from
         wp_template wp
      plan wp
      where wp.activity_type_cd = glb_cd
        and wp.active_ind = 1

      head report
          tCnt = 0

      detail
          tCnt = tCnt + 1
          if (mod(tCnt, 5) = 1)
             stat = alterlist(reply->template_qual, tCnt + 5)
          endif
          reply->template_qual[tCnt].template_name = wp.short_desc
          reply->template_qual[tCnt].template_id = wp.template_id

      foot report
          stat = alterlist(reply->template_qual, tCnt)

      with nocounter

      set error_check = ERROR(sErrorMsg,0)   ;get error message
      if (error_check != 0)
        set reply->status_data->status = "F"
        set reply->status_data->subeventstatus[1]->TargetObjectValue = sErrorMsg
        go to exit_script
      endif

      if (pCnt > 0)
         set stat = alterlist(reply->qual, pCnt)
         set reply->status_data->status = "S"
      else
         set reply->status_data->status = "Z"
      endif

;call echorecord(reply)

/********************************************************************/
/* Script exit.                                                     */
/********************************************************************/
#exit_script

END GO

;Generated by GNU enscript 1.6.4.
