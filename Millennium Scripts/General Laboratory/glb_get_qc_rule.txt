 ;;Solution_Test/script/PathNet -- Gen Lab/glb_get_qc_rule/glb_get_qc_rule.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************
        Source file name:       GLB_GET_QC_RULE.PRG
        Object name:            GLB_GET_QC_RULE
        Request #:              257503

        Product:                General Lab           
        Product Team:           GLB
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Retrieve QC Rules associated with
                                the given short description

        Tables read:            qc_rule_type
                                qc_rule

        Tables updated:         n/a

        Executing from:         General Lab Build/Maintain QC Reference tool

        Special Notes:          n/a
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     ### 06/25/96 Chad Moats           Initial Release                     *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

      drop program GLB_GET_QC_RULE:dba go    
      create program GLB_GET_QC_RULE:dba
     
/********************************************************************/
/*  Request Record                                                  */
/********************************************************************/
/*
      record request                                      
      (                                       
         1 short_description = c20
         1 all_rules_ind = i2
      )
*/

/********************************************************************/
/*  Reply Record                                                    */
/********************************************************************/

      record reply
      (
         1 rule_sets[*]
           2 rule_id                 = f8
           2 short_description       = c20
           2 description             = vc
           2 rules[*]
             3 sequence              = i2
             3 rule_form_flag        = i2
             3 error_flag            = i2
             3 rule_definition       = vc
             3 across_control_ind    = i2
             3 across_run_ind        = i2
             3 trig_ind              = i2
             3 max_req_results       = i4
             3 req_trouble_step_ind  = i2
             3 req_comment_ind       = i2
         1 status_data
           2 status                  = c1
           2 subeventstatus[1]
             3 OperationName         = c15
             3 OperationStatus       = c1
             3 TargetObjectName      = c15
             3 TargetObjectValue     = vc
      )

     #Script
      set rule_cnt = 0
      set set_cnt = 0
      set detail_cnt = 0
      set short_desc = cnvtupper(request->short_description)
      set found_rule = "F"
      set reply->status_data->status = "F"
      set sErrorMsg = FILLSTRING(255," ")    ;string containing error message
      set error_check = ERROR(sErrorMsg,1)   ;reset error queue to empty

      SELECT into "nl:"
         qct.rule_id,
         qc.sequence,
	 d1.seq
      FROM
         qc_rule_type qct,
         qc_rule qc,
	 (dummyt d1 with seq = 1)
      PLAN qct
         where ((short_desc = cnvtupper(qct.short_description)
                 and request->all_rules_ind = 0) or 
                (request->all_rules_ind = 1))
                and qct.active_ind > 0
      JOIN d1
         where d1.seq = 1
      JOIN qc
         where qct.rule_id = qc.rule_id
      ORDER BY qct.rule_id, qc.sequence
      HEAD qct.rule_id
         found_rule = "Y"
         set_cnt = set_cnt + 1          
         if (mod(set_cnt,5) =1)
            stat =  alterlist(reply->rule_sets,set_cnt + 5)
         endif
         reply->rule_sets[set_cnt].rule_id = qct.rule_id
         reply->rule_sets[set_cnt].short_description = qct.short_description
call echo(reply->rule_sets[set_cnt].short_description, 1)
         reply->rule_sets[set_cnt].description = qct.description
         rule_cnt = 0
      HEAD qc.sequence
         rule_cnt = rule_cnt + 1          
         if (mod(rule_cnt,5) = 1)
            stat =  alterlist(reply->rule_sets[set_cnt].rules,rule_cnt + 5)
         endif
         reply->rule_sets[set_cnt].rules[rule_cnt]->sequence = qc.sequence
         reply->rule_sets[set_cnt].rules[rule_cnt]->rule_form_flag = qc.rule_form_flag
         reply->rule_sets[set_cnt].rules[rule_cnt]->error_flag = qc.error_flag
         reply->rule_sets[set_cnt].rules[rule_cnt]->rule_definition = qc.rule_definition
;call echo( reply->rule_sets[set_cnt].rules[rule_cnt]->rule_definition,1)
         reply->rule_sets[set_cnt].rules[rule_cnt]->across_control_ind = qc.across_control_ind
         reply->rule_sets[set_cnt].rules[rule_cnt]->across_run_ind = qc.across_run_ind
         reply->rule_sets[set_cnt].rules[rule_cnt]->trig_ind = qc.trig_ind
         reply->rule_sets[set_cnt].rules[rule_cnt]->max_req_results = qc.max_req_results
         reply->rule_sets[set_cnt].rules[rule_cnt]->req_trouble_step_ind = qc.req_trouble_step_ind
         reply->rule_sets[set_cnt].rules[rule_cnt]->req_comment_ind = qc.req_comment_ind
      DETAIL
         detail_cnt = detail_cnt + 1
      FOOT qct.rule_id
         stat = alterlist(reply->rule_sets[set_cnt].rules, rule_cnt)
      WITH
         nocounter,
	 outerjoin = d1

      set error_check = ERROR(sErrorMsg,0)   ;get error message
      if (error_check != 0)
        set reply->status_data->status = "F"
        set reply->status_data->subeventstatus[1]->TargetObjectValue = sErrorMsg
        go to exit_script
      endif

      if (found_rule = "F")
         go to no_rules
      else
        set reply->status_data->status = "S"
        set reply->status_data->subeventstatus[1]->TargetObjectValue = "Successful"
      endif

      set stat = alterlist(reply->rule_sets, set_cnt)

      go to exit_script

/********************************************************************/ 
/* Executed if qc rules are not found.                              */
/********************************************************************/
     #no_rules
      set reply->status_data->subeventstatus[1]->operationname = 
         "get"
      set reply->status_data->subeventstatus[1]->operationstatus = 
         "Z"
      set reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue = 
         "No records found"
      set reply->status_data->status = "Z"

      go to exit_script

/********************************************************************/ 
/* Script exit.                                                     */
/********************************************************************/
     #exit_script

END GO
     

;Generated by GNU enscript 1.6.4.
