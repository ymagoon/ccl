 ;;Solution_Test/script/PathNet -- Gen Lab/glb_del_wklst_ele/glb_del_wklst_ele.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************
        Source file name:       GLB_DEL_WORKLIST_ELE.PRG
        Object name:            GLB_DEL_WORKLIST_ELE
        Request #:              250086

        Product:                General Lab           
        Product Team:           GLB
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Delete a worklist from the database   

        Tables read:            n/a

        Tables updated:         worklist         
                                worklist_element         
                                worklist_order_r
                                worklist_accession_r

        Executing from:         General Lab worklist request           

        Special Notes:          n/a
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     500 05/20/96 Newton Hughes        Initial Release                     *
;    *600 05/19/05 Catherine Boeding    Delete from worklist_element        *   
;    *601 08/09/10 Oliver Barbara       Remove worklist_id from             *
;                                       perform_result and qc_result        *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

      DROP PROGRAM glb_del_wklst_ele:dba GO
      CREATE PROGRAM glb_del_wklst_ele:dba

/*
      RECORD request
      (
        1 worklist_id        		= f8
      )	
*/
      RECORD reply
      (
%i cclsource:status_block.inc
      )

#script
      SET failed = "F"
      SET reply->status_data->status = "F"

/*******************************************************************/
/* delete rows from worklist elements table                        */
/*******************************************************************/  

      SELECT INTO "NL:"
      FROM
        worklist_element we
      WHERE 
        we.worklist_id = request->worklist_id
      WITH 
        nocounter

      IF (curqual > 0)

        DELETE FROM
          worklist_element we
        WHERE
          we.worklist_id = request->worklist_id
        WITH
          NOCOUNTER

        IF (curqual = 0)
          GO TO we_delete_failed
        ENDIF
      ENDIF

/*******************************************************************/
/* delete rows from worklist_pos_order_r                           */
/*******************************************************************/ 

      select into "nl:"
      
      from worklist_pos_order_r wp
      
      where wp.worklist_id = request->worklist_id
      
      with forupdate(wp)
      
      
      delete from worklist_pos_order_r wp
      
      where wp.worklist_id = request->worklist_id
      
      with nocounter

/*******************************************************************/
/* delete rows from worklist_order_r table                         */
/*******************************************************************/  

      DELETE FROM
        worklist_order_r wo
      WHERE
        wo.worklist_id = request->worklist_id
      WITH
        NOCOUNTER

      SET order_qual = curqual

      DELETE FROM
        worklist_accession_r wa
      WHERE
        wa.worklist_id = request->worklist_id
      WITH
        NOCOUNTER

      IF (order_qual + curqual = 0)
        GO TO wo_delete_failed
      ENDIF

/*******************************************************************/
/* delete rows from worklist_event                                 */
/*******************************************************************/ 

      select into "nl:"
      
      from worklist_event we
      
      where we.worklist_id = request->worklist_id
      
      with forupdate(we)
      
      
      delete from worklist_event we
      
      where we.worklist_id = request->worklist_id
      
      with nocounter

/*******************************************************************/
/* Remove references to automatic worklist in perform_result table */
/*******************************************************************/ 
      update into 
        perform_result pr
      set 
        pr.worklist_id = 0.0
      where 
        pr.worklist_id = request->worklist_id
      with nocounter


/*******************************************************************/
/* Remove references to automatic worklist in qc_result table      */
/*******************************************************************/
       update into 
         qc_result qr
       set 
         qr.worklist_id = 0.0
       where 
         qr.worklist_id = request->worklist_id
       with nocounter


/*******************************************************************/
/* delete row from worklist table                                  */
/*******************************************************************/  

      DELETE FROM
        worklist w      
      WHERE
        w.worklist_id = request->worklist_id
      WITH
        NOCOUNTER

      IF (curqual = 0)
        GO TO w_delete_failed
      ELSE
        GO TO exit_script
      ENDIF
      
#wo_delete_failed
      SET reply->status_data->subeventstatus[1]->operationname = 
         "delete"
      SET reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      SET reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      SET reply->status_data->subeventstatus[1]->targetObjectValue = 
         "worklist_order_r"
      SET failed = "T"
      GO TO exit_script
     
#w_delete_failed
      SET reply->status_data->subeventstatus[1]->operationname = 
         "delete"
      SET reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      SET reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      SET reply->status_data->subeventstatus[1]->targetObjectValue = 
         "worklist table"
      SET failed = "T"
      GO TO exit_script

#we_delete_failed
      set reply->status_data->subeventstatus[1]->operationname = 
         "delete"
      set reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      set reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue = 
         "worklist_element table"
      SET failed = "T"
      GO TO exit_script

#exit_script
      IF (failed = "F")
         SET reply->status_data->status = "S"
         set reqinfo->commit_ind = 1
      ELSE
         set reqinfo->commit_ind = 0
      ENDIF

      END GO

;Generated by GNU enscript 1.6.4.
