 ;;Solution_Test/script/PathNet -- Common Services/ACCESSION_STRING/ACCESSION_STRING.PRG Turn on black mode

;;--------------------------------------------------------------------------------------------------------------------;;
;; File:  accession_string.prg                                                                                        ;;
;; Description: Creates the unformatted and unique accession strings.                                                 ;;
;; Request Nbr: n/a                                                                                                   ;;
;;                                                                                                                    ;;
;;--------------------------------------------------------------------------------------------------------------------;;
;; Notes                                                                                                              ;;
;; ----- -------------------------------------------------------------------------------------------------------------;;
;; 000   The calling program must include ACCESSION_COMMON.INC                                                        ;;
;;                                                                                                                    ;;
;;                                                                                                                    ;;
;;--------------------------------------------------------------------------------------------------------------------;;
;; Mod Date                  Feature    Description                                                                   ;;
;; --- -------- -----------  ---------- ------------------------------------------------------------------------------;;
;; 000 07/01/99 CERGYL       6255       File creation.                                                                ;;
;;--------------------------------------------------------------------------------------------------------------------;;
        
        drop program accession_string:dba go
        create program accession_string:dba
      
        set accession_nbr = fillstring (20, " ")
        set accession_nbr_chk = fillstring (50, " ")
        
        set acc_pool_string = cnvtstring (accession_str->accession_pool_id, 32, 6, r)
 
        set bpos = 0
        set epos = 0
        for (cnt1 = 1 to 32)
          if (cnvtint (substring (cnt1, 1, acc_pool_string)) > 0)
            set bpos = cnt1
            set cnt1 = 32
            set cnt2 = 32
            while (cnt2 > bpos)
              if (cnvtint (substring (cnt2, 1, acc_pool_string)) > 0 or substring (cnt2, 1, acc_pool_string) = ".")
                if (substring (cnt2, 1, acc_pool_string) = ".")
                  set epos = cnt2 - 1
                else
                  set epos = cnt2
                endif
                set cnt2 = bpos - 1
              else
                set cnt2 = cnt2 - 1
              endif
            endwhile
          endif
        endfor
 
        set strlength = (epos - bpos) + 1
        if (strlength <= 0)
          set strlength = 1
        endif
        
        if (accession_str->site_prefix_disp = " " or accession_str->site_prefix_disp = "")
          set accession_str->site_prefix_disp = "00000"
        endif
        
        if (size (trim (accession_str->alpha_prefix)) > 0 and accession_str->alpha_prefix > " ")
        
          if (size (trim (accession_str->alpha_prefix)) = 1)
            set accession_str->alpha_prefix = concat (" ", accession_str->alpha_prefix)
          endif    
         
          set accession_nbr = concat(trim(accession_nbr),
                                     accession_str->site_prefix_disp,
                                     accession_str->alpha_prefix,
                                     cnvtstring (accession_str->accession_year, 4, 0, r),
                                     cnvtstring (accession_str->accession_seq_nbr, 7, 0, r))
        
          set accession_nbr_chk = concat (trim (accession_nbr_chk),
                                          cnvtstring (accession_str->accession_year, 4, 0, r),
                                          substring (bpos, strlength, acc_pool_string),
                                          cnvtstring (accession_str->accession_seq_nbr, 7, 0, r))
        
        else
 
          set accession_nbr = concat (trim (accession_nbr),
                                      accession_str->site_prefix_disp,
                                      cnvtstring (accession_str->accession_year, 4, 0, r),
                                      cnvtstring (accession_str->accession_day, 3, 0, r),
                                      cnvtstring (accession_str->accession_seq_nbr, 6, 0, r))
        
          set accession_nbr_chk = concat (trim (accession_nbr_chk),
                                          cnvtstring (accession_str->accession_year, 4, 0, r),
                                          cnvtstring (accession_str->accession_day, 3, 0, r),
                                          substring (bpos, strlength, acc_pool_string),
                                          cnvtstring (accession_str->accession_seq_nbr, 6, 0, r))
        endif

end go  

;Generated by GNU enscript 1.6.4.
