 ;;Solution_Test/script/PathNet -- Common Services/ACCESSION_MODIFY/ACCESSION_MODIFY.PRG Turn on black mode

;;--------------------------------------------------------------------------------------------------------------------;;
;; File:  accession_modify.prg                                                                                        ;;
;; Description: Accession modifification routine.                                                                     ;;
;; Request Nbr: 265064                                                                                                ;;
;;                                                                                                                    ;;
;;--------------------------------------------------------------------------------------------------------------------;;
;; Notes                                                                                                              ;;
;; ----- -------------------------------------------------------------------------------------------------------------;;
;;                                                                                                                    ;;
;;                                                                                                                    ;;
;;--------------------------------------------------------------------------------------------------------------------;;
;; Mod Date                  Feature    Description                                                                   ;;
;; --- -------- -----------  ---------- ------------------------------------------------------------------------------;;
;; 000 07/01/99 CERGYL       6255       File creation.                                                                ;;
;;--------------------------------------------------------------------------------------------------------------------;;

        drop program accession_modify:dba go
        create program accession_modify:dba
        
        record reply
        (
         1 accession              = c20
         1 accession_id           = f8
         1 accession_status       = i2
         1 accession_meaning      = vc
%I CCLSOURCE:STATUS_BLOCK.INC
        )
        
%I CCLSOURCE:ACCESSION_COMMON.INC

        set reqinfo->commit_ind = 0
        set reply->status_data->status = "F"
        set assignment_status = ACC_ERROR
        
        ;*
        ;* Validate that the "new" accession is not on the accession table.
        ;*

        set accession_chk->accession = request->acc_to->accession
        set accession_chk->accession_nbr_check = request->acc_to->accession_nbr_check
        set accession_chk->action_ind = 1

        execute accession_check

        if (accession_status != ACC_SUCCESS)
          set reply->accession_status = accession_status
          set reply->accession_meaning = build ("To Accession:  ", accession_meaning)
          set reply->accession_id = accession_dup_id
          set reply->accession = request->accession
          go to EXIT_SCRIPT
        endif

        ;*
        ;* Validate that the "old" accession is on the accession table.
        ;*

        set accession_chk->accession = request->acc_from->accession
        set accession_chk->accession_nbr_check = request->acc_from->accession_nbr_check 
        set accession_chk->action_ind = 1

        execute accession_check

        if (accession_status != ACC_DUPLICATE)
          set reply->accession_status = accession_status
          set reply->accession_meaning = build ("From Accession:  ", accession_meaning)
          set reply->accession_id = accession_dup_id
          set reply->accession = request->accession
          go to EXIT_SCRIPT
        endif

        ;*
        ;* Update the "old" accession to the "new" accession.
        ;*

        select into "nl:" a.accession_id
        from accession a
        where a.accession_id = accession_dup_id
        with nocounter, forupdate (a)

        if (curqual > 0)
          update into accession a
            set 
                a.accession = request->acc_to->accession,
                a.accession_nbr_check = trim (request->acc_to->accession_nbr_check),
                a.site_prefix_cd = request->acc_to->site_prefix_cd,
                a.accession_year = request->acc_to->accession_year,
                a.accession_day = request->acc_to->accession_day,
                a.accession_format_cd = request->acc_to->accession_format_cd,
                a.alpha_prefix = request->acc_to->alpha_prefix,
                a.accession_sequence_nbr = request->acc_to->accession_sequence_nbr,
                a.accession_pool_id = request->acc_to->accession_pool_id,
                a.preactive_ind = request->acc_to->preactive_ind,
                a.updt_dt_tm = cnvtdatetime (curdate, curtime3),
                a.updt_id = reqinfo->updt_id,
                a.updt_task = reqinfo->updt_task,
                a.updt_applctx = reqinfo->updt_applctx,
                a.updt_cnt = a.updt_cnt + 1
            where a.accession_id = accession_dup_id
              and a.updt_cnt = accession_updt_cnt
          with nocounter

          if (curqual > 0)
            set reply->accession_status = ACC_SUCCESS
            set reply->accession_meaning = "Accession modified on the accession table"
            set reply->status_data->status = "S"
            set reqinfo->commit_ind = 1
          endif
        else
          set reply->accession_meaning = "Update failed on the Accession table"
        endif

        #EXIT_SCRIPT
        
        end go

;Generated by GNU enscript 1.6.4.
