 ;;Solution_Test/script/PathNet -- Gen Lab/glb_get_cp_encntr/GLB_GET_CP_ENCNTR.PRG Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       glb_get_cp_encntr.prg
        Object name:            glb_get_cp_encntr
        Request #:              none

        Product:                PathNet                               
        Product Team:           General Lab
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Called by charting to get encounters that have
                                new results

        Tables read:            result_event, result, orders
        Tables updated:         none
        Executing from:         none

        Special Notes:          

******************************************************************************/

 
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     001 10/29/98 Chris Heil           Initial Release                     *
;     002 12/23/98 Chris Heil           Size return array from previous     *
;                                       total passed in.                    *
;     003 01/26/00 Melissa Lipford      Change > to >= last_dist_run_dt_tm  *
;~DE~************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************
drop   program glb_get_cp_encntr:dba go
create program glb_get_cp_encntr:dba

/***********************************************************************
 *   Record to be used by charting                                     *
 ***********************************************************************/
/*
  last_dist_run_dt_tm = dq8 --> activity since date should be returned

  record cp_encntr          --> return list of encounters with activity
  (                             since last_dist_run_dt_tm
    1 encntr_list[*]
      2 encntr_id = f8
  )
*/

/***********************************************************************
 *   Initialize Variables                                              *
 ***********************************************************************/
  set code_set = 0
  set code_value = 0.0
  set verified_cd = 0.0
  set corrected_cd = 0.0
  set autoverified_cd = 0.0
  set cdf_meaning = fillstring(12," ")
  set orig_encntr_cnt = size(cp_encntr->encntr_list, 5)

/***********************************************************************
 *   Retreive verified and corrected code values                       *
 ***********************************************************************/
  set code_set = 1901
  set cdf_meaning = "VERIFIED"
  execute cpm_get_cd_for_cdf
  set verified_cd = code_value

  set code_set = 1901
  set cdf_meaning = "CORRECTED"
  execute cpm_get_cd_for_cdf
  set corrected_cd = code_value

  set code_set = 1901
  set cdf_meaning = "AUTOVERIFIED"
  execute cpm_get_cd_for_cdf
  set autoverified_cd = code_value

/***********************************************************************
 *   Retreive encounter ids for all results either verified or         *
 *   corrected since last_dist_run_dt_tm.                              *
 ***********************************************************************/
  select distinct into "nl:"
    o.encntr_id
  from result_event re,
       result r,
       orders o
  plan re
;003    where re.event_dt_tm > cnvtdatetime(last_dist_run_dt_tm)
    where re.event_dt_tm >= cnvtdatetime(last_dist_run_dt_tm)         ;003
      and re.event_type_cd in (verified_cd, corrected_cd, autoverified_cd)
  join r
    where r.result_id = re.result_id
  join o
    where o.order_id = r.order_id
      and o.encntr_id > 0

  order by
    o.encntr_id

  head report
    encntr_cnt = 0

  head o.encntr_id
    encntr_cnt = encntr_cnt + 1
    if (mod((encntr_cnt), 50) = 1)
      stat = alterlist(cp_encntr->encntr_list, 
                       orig_encntr_cnt + encntr_cnt + 50)
    endif

    cp_encntr->encntr_list[orig_encntr_cnt + encntr_cnt]->encntr_id = o.encntr_id

  detail
    row + 0

  foot o.encntr_id
    row + 0

  foot report
    stat = alterlist(cp_encntr->encntr_list, orig_encntr_cnt + encntr_cnt)

  with nocounter
end go

;Generated by GNU enscript 1.6.4.
