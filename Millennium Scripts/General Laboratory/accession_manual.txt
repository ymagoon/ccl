 ;;Solution_Test/script/PathNet -- Common Services/ACCESSION_MANUAL/ACCESSION_MANUAL.PRG Turn on black mode

;;--------------------------------------------------------------------------------------------------------------------;;
;; File:  accession_manual.prg                                                                                        ;;
;; Description: Manual accession assignment.                                                                          ;;
;; Request Nbr: 265063                                                                                                ;;
;;                                                                                                                    ;;
;;--------------------------------------------------------------------------------------------------------------------;;
;; Notes                                                                                                              ;;
;; ----- -------------------------------------------------------------------------------------------------------------;;
;;                                                                                                                    ;;
;;                                                                                                                    ;;
;;--------------------------------------------------------------------------------------------------------------------;;
;; Mod Date                  Feature    Description                                                                   ;;
;; --- -------- -----------  ---------- ------------------------------------------------------------------------------;;
;; 000 07/01/99 CERGYL       6255       File creation.                                                                ;;
;;--------------------------------------------------------------------------------------------------------------------;;

        drop program accession_manual:dba go
        create program accession_manual:dba

        record reply
        (
         1 accession              = c20
         1 accession_id           = f8
         1 accession_status       = i4
         1 accession_meaning      = vc
%I CCLSOURCE:STATUS_BLOCK.INC
        )

%I CCLSOURCE:ACCESSION_COMMON.INC
        
        set reply->status_data->status = "Z"
        set reqinfo->commit_ind = 0
        set reply->accession_status = ACC_ERROR

        ;*
        ;* Validate that the accession does not exist on the accession table.
        ;*
        
        set accession_chk->accession = request->accession
        set accession_chk->accession_nbr_check = request->accession_nbr_check 
        set accession_chk->check_disp_ind = request->check_disp_ind
        set accession_chk->site_prefix_cd = request->site_prefix_cd
        set accession_chk->accession_year = request->accession_year
        set accession_chk->accession_day = request->accession_day
        set accession_chk->accession_pool_id = request->accession_pool_id
        set accession_chk->accession_seq_nbr = request->accession_sequence_nbr
        set accession_chk->accession_class_cd = 0
        set accession_chk->accession_format_cd = request->accession_format_cd
        set accession_chk->alpha_prefix = request->alpha_prefix
        set accession_chk->preactive_ind = request->preactive_ind
        set accession_chk->assignment_ind = 0

        ;*
        ;* The action_ind will be set to validate only (1) when the validate_flag is 1, otherwise the action_ind
        ;* will be set to validate/insert (0).
        ;*

        set accession_chk->action_ind = request->validate_flag

        execute accession_check

        if (accession_status != ACC_DUPLICATE)
          go to EXIT_SCRIPT
        endif

        if (request->validate_flag = 1)

          set accession_status = ACC_AOR_FALSE
          if (accession_dup_id > 0)
            select into "nl:" aor.accession_id, aor.order_id, o.person_id
            from accession_order_r aor,
                 orders o
            plan aor
              where aor.accession_id = accession_dup_id
            join o
              where aor.order_id = o.order_id
            detail
              if (accession_status != ACC_PERSON_FALSE)
                if (request->person_id > 0)
                  if (o.person_id = request->person_id)
                    accession_status = ACC_PERSON_TRUE
                  else
                    accession_status = ACC_PERSON_FALSE
                  endif
                else
                  accession_status = ACC_AOR_TRUE
                endif
              endif
            with nocounter
          endif
  
          case (accession_status)
            of ACC_AOR_FALSE:
              set reply->accession_meaning = "Accession is not on the accession_order_r table"
            of ACC_AOR_TRUE:
              set reply->accession_meaning = "Accession is on the accession_order_r table"
            of ACC_PERSON_FALSE:
              set reply->accession_meaning = "Accession is associated with a different person"
            of ACC_PERSON_TRUE:
              set reply->accession_meaning = "Accession is associated with the same person"
          endcase

          go to EXIT_SCRIPT
        endif
        
        if (accession_assignment_ind = 1)

          update into accession a
             set a.assignment_ind = 0,
                 a.updt_dt_tm = cnvtdatetime (curdate, curtime3),
                 a.updt_id = reqinfo->updt_id,
                 a.updt_task = reqinfo->updt_task,
                 a.updt_applctx = reqinfo->updt_applctx,
                 a.updt_cnt = a.updt_cnt + 1
          where a.accession_id = accession_id
            and a.updt_cnt = accession_updt_cnt
            and a.assignment_ind = accession_assignment_ind
     
          if (curqual = 0)
            set accession_status = ACC_ERROR
            set accession_meaning = "Update failed on the Accession table"
          else
            set accession_status = ACC_SUCCESS
          endif

        endif
         
        #EXIT_SCRIPT

        set reply->accession_status = accession_status
        set reply->accession_meaning = accession_meaning
        set reply->accession = request->accession
        set reply->accession_id = accession_id
        
        if (accession_status = ACC_ERROR)
          set reply->status_data->status = "F"
          set reqinfo->commit_ind = 0
        else
          set reply->status_data->status = "S"
          set reqinfo->commit_ind = 1
        endif
        
        end go

;Generated by GNU enscript 1.6.4.
