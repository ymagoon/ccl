 ;;Solution_Test/script/ePathLink/PathNet General Lab - PathLink Scripts/glb_get_nurse_unit.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1996 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       GLB_GET_NURSE_UNIT.PRG
        Object name:            GLB_GET_NURSE_UNIT
        Request #:              250141

        Product:                General Lab
        Product Team:           PathNet
        HNA Version:            500
        CCL Version:            4.0

        Program purpose:        This script returns a list of nurse units for a 
                                given facility.

        Tables read:            location_group

        Tables updated:         None
        Executing from:         

        Special Notes:          None

******************************************************************************/

;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;    *000 01/15/98  Chad Moats          Initial Release                     *
;~DE~************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

             drop program GLB_GET_NURSE_UNIT:dba go    
             create program GLB_GET_NURSE_UNIT:dba
     
      /***********************************************************************
      * Request Record                                                       *
      ***********************************************************************/
      /*
      record request
      (
          1  loc_facility_cd                = f8
      )
      */

      /***********************************************************************
      * Reply Record                                                         *
      ***********************************************************************/

      record building
      ( 
         1  buildings[*]
            2  loc_building_cd                 = f8
      )

      /***********************************************************************
      * Reply Record                                                         *
      ***********************************************************************/

      record reply
      ( 
         1  qual[*]
            2  location_cd               = f8
            2  location_disp             = vc
%i cclsource:status_block.inc
      )

      /***********************************************************************
      * Script                                                               *
      ***********************************************************************/
      #Script
         declare l_cnt                    = i4
         declare b_cnt                    = i4
         set l_cnt                        = 0
         set b_cnt                        = 0
	 set cur_dt_tm			  = cnvtdatetime(curdate,curtime)
         set reply->status_data.status    = "F"
         set sErrorMsg = FILLSTRING(255," ")    ;string containing error message
         set error_check = ERROR(sErrorMsg,1)   ;reset error queue to empty


      /***********************************************************************
      * Get Nurse Unit Type Cd                                               *
      ***********************************************************************/

 ;        select into "nl:"
 ;          cv.code_value
 ;        from code_value cv
 ;        where cv.code_set = 220 and
 ;              cv.cdf_meaning = "NURSEUNIT" and
 ;              cv.active_ind = 1 
 ;        detail
 ;          location_type_cd = cv.code_value
 ;        with nocounter


      /***********************************************************************
      * Get Building for a given facility                                    *
      ***********************************************************************/

         select into "nl:"
           lg.child_loc_cd
         from 
           location_group lg
         plan lg
           where lg.parent_loc_cd = request->loc_facility_cd and
		 cnvtdatetime(cur_dt_tm) > lg.beg_effective_dt_tm and	         
		 cnvtdatetime(cur_dt_tm) < lg.end_effective_dt_tm and	         
                 lg.active_ind = 1
         head lg.child_loc_cd
           b_cnt = b_cnt + 1
           if (mod(b_cnt,5) = 1)
              stat =  alterlist(building->buildings,b_cnt + 5)
           endif        
           building->buildings[b_cnt].loc_building_cd = lg.child_loc_cd
         detail
           b_cnt = b_cnt + 0
         with nocounter

         set error_check = ERROR(sErrorMsg,0)   ;get error message
         if (error_check != 0)
           set reply->status_data->status = "F"
           set reply->status_data->subeventstatus[1]->TargetObjectValue = sErrorMsg
           go to exit_script
         endif

         if (curqual = 0)
           go to no_nurse_unit_found
         else
           set stat = alterlist(building->buildings, b_cnt)
         endif


      /***********************************************************************
      * Get Nurse units for each building                                    *
      ***********************************************************************/

         select into "nl:"
           lg.child_loc_cd
         from 
           location_group lg,
           (dummyt d1 with seq = value(b_cnt))
         plan d1
         join lg
           where lg.parent_loc_cd = building->buildings[d1.seq].loc_building_cd and
		 cnvtdatetime(cur_dt_tm) > lg.beg_effective_dt_tm and  
		 cnvtdatetime(cur_dt_tm) < lg.end_effective_dt_tm and	         
                 lg.active_ind = 1
         head lg.child_loc_cd
           l_cnt = l_cnt + 1
           if (mod(l_cnt,5) = 1)
              stat =  alterlist(reply->qual,l_cnt + 5)
           endif        
           reply->qual[l_cnt].location_cd = lg.child_loc_cd
         detail
           l_cnt = l_cnt + 0
         with nocounter

         set error_check = ERROR(sErrorMsg,0)   ;get error message
         if (error_check != 0)
           set reply->status_data->status = "F"
           set reply->status_data->subeventstatus[1]->TargetObjectValue = sErrorMsg
           go to exit_script
         endif

         if (curqual = 0)
           go to no_nurse_unit_found
         else
           set stat = alterlist(reply->qual, l_cnt)
         endif

         set reply->status_data->status = "S"
         go to exit_script

      /***********************************************************************
      * If no nurse units were found then                                    *
      ***********************************************************************/

         #no_nurse_unit_found
           set reply->status_data->subeventstatus[1]->operationname = 
              "get"
           set reply->status_data->subeventstatus[1]->operationstatus = 
              "Z"
           set reply->status_data->subeventstatus[1]->targetobjectname = 
             "table"
           set reply->status_data->subeventstatus[1]->targetObjectValue = 
             "location_group table"
           set reply->status_data->status = "Z"

           go to exit_script


        #exit_script
           end
           go      

;Generated by GNU enscript 1.6.4.
