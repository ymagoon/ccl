 ;;Solution_Test/script/PathNet -- Gen Lab/glb_upd_ts_qcid/glb_upd_ts_qcid.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************
        Source file name:       GLB_UPD_TS_QCID.PRG
        Object name:            GLB_UPD_TS_QCID
        Request #:              257528

        Product:                General Lab - Quality Control           
        Product Team:           GLB
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Update test site info given control lot 
                                into the database

        Tables read:            n/a

        Tables updated:         resource_accession_r
                                accession

        Executing from:         General Lab Build/Maintain QC Maintenance 

        Special Notes:          n/a
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     ### 07/11/96 Chad Moats           Initial Release                     *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

      drop program GLB_UPD_TS_QCID:dba go    
      create program GLB_UPD_TS_QCID:dba
     
/********************************************************************/
/*  Request Record                                                  */
/********************************************************************/
/*
      record request                                      
      (                                       
         1 control_id              = f8
         1 testsites[5]
           2 service_resource_cd     = f8
           2 accession_id            = f8
           2 accession               = vc
           2 preactive_ind           = i2
           2 instr_x_ref             = c20
	   2 symbology		     = c2
      ) 
*/
;record reqinfo
;( 1 commit_ind  = i2
;  1 updt_id     = f8
;  1 position_cd = f8
;  1 updt_app    = i4
;  1 updt_task   = i4
;  1 updt_req    = i4
;  1 updt_applctx= i4
;)

/********************************************************************/
/*  Reply Record                                                    */
/********************************************************************/

      record reply
      ( 
         1 status_data
           2 status                = c1
           2 subeventstatus[1]
             3 OperationName       = c15
             3 OperationStatus     = c1
             3 TargetObjectName    = c15
             3 TargetObjectValue   = vc
      )

     #Script
      set failed = "F"
      set reply->status_data->status = "F"
      set cnt = 0
      declare stat                    = i4
      declare t_cnt                   = i4
      declare ts_to_add               = i4
      set ts_to_add = size(request->testsites, 5)


/*******************************************************************/
/* If the accession_id is greater than zero, check to see if it    */
/* exists for that service resource and control.                   */
/*******************************************************************/

      for (cnt = 1 to ts_to_add)
      if (request->testsites[cnt].accession_id > 0)

        SELECT into "nl:"
           ra.accession_id
        FROM
           resource_accession_r ra
        PLAN ra
           where request->control_id = ra.control_id and
                 request->testsites[cnt].service_resource_cd = ra.service_resource_cd and
                 request->testsites[cnt].preactive_ind = ra.preactive_ind
        WITH
           nocounter

        if (curqual = 0)
           /*******************************************************************/
           /* Insert row into resource_accession_r table.                     */
           /*******************************************************************/
           INSERT
                into resource_accession_r ra1
           SET
                 ra1.service_resource_cd = request->testsites[cnt].service_resource_cd,
                 ra1.preactive_ind       = request->testsites[cnt].preactive_ind,
                 ra1.accession_id        = request->testsites[cnt].accession_id,
                 ra1.control_id          = request->control_id,
                 ra1.instr_xref          = request->testsites[cnt].instr_x_ref,
		 ra1.symbology		 = request->testsites[cnt].symbology,
                 ra1.updt_dt_tm          = cnvtdatetime(curdate,curtime),
                 ra1.updt_cnt            = 0,
                 ra1.updt_task           = reqinfo->updt_task,
                 ra1.updt_id             = reqinfo->updt_id,
                 ra1.updt_applctx        = reqinfo->updt_applctx
           PLAN ra1
           WHERE ra1.control_id = request->control_id and
                 ra1.preactive_ind = request->testsites[cnt].preactive_ind and
                 ra1.accession_id = request->testsites[cnt].accession_id 
           WITH
              nocounter
        
           if (curqual = 0)
                 go to ra_insert_failed
           endif     

        else 
           /*******************************************************************/
           /* Update row into resource_accession_r table.                     */
           /*******************************************************************/

           UPDATE
                 into resource_accession_r ra2
           SET
                 ra2.accession_id = request->testsites[cnt].accession_id,
                 ra2.instr_xref   = request->testsites[cnt].instr_x_ref,
		 ra2.symbology    = request->testsites[cnt].symbology,
                 ra2.updt_dt_tm   = cnvtdatetime(curdate,curtime),
                 ra2.updt_cnt     = 0,
                 ra2.updt_task    = reqinfo->updt_task,
                 ra2.updt_id      = reqinfo->updt_id,
                 ra2.updt_applctx = reqinfo->updt_applctx
            PLAN ra2 where
                 ra2.service_resource_cd = request->testsites[cnt].service_resource_cd and
                 ra2.preactive_ind       = request->testsites[cnt].preactive_ind and
                 ra2.control_id          = request->control_id 
            WITH
                 nocounter
     
            if (curqual = 0)
                 go to ra_update_failed
            endif     
        endif
      endif

      endfor

      go to exit_script

/********************************************************************/ 
/* Executed if resource_accession_r table update is unsuccessful.   */
/********************************************************************/
     #ra_update_failed
      set reply->status_data->subeventstatus[1]->operationname = 
         "update"
      set reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      set reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue = 
         "resource_accession_r table"
      set failed = "T"
      go to exit_script

/********************************************************************/ 
/* Executed if resource_accession_r table insert is unsuccessful.   */
/********************************************************************/
     #ra_insert_failed
      set reply->status_data->subeventstatus[1]->operationname = 
         "insert"
      set reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      set reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue = 
         "resource_accession_r table"
      set failed = "T"
      go to exit_script

/********************************************************************/ 
/* Script exit.                                                     */
/********************************************************************/
     #exit_script
      if (failed = "F")
         set reply->status_data->status = "S"
         commit
      else
         rollback
      endif

END
GO     
      

;Generated by GNU enscript 1.6.4.
