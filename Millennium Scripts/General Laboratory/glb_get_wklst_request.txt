 ;;Solution_Test/script/PathNet -- Gen Lab/glb_get_wklst_request/glb_get_wklst_request.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-2000 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       GLB_GET_WKLST_REQUEST.PRG
        Object name:            GLB_GET_WKLST_REQUEST
        Request #:              250047

        Product:                General Lab           
        Product Team:           GLB
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Return the worklist request template for the
                                input worklist_ref_cd.

        Tables read:            worklist_ref
                                worklist_ref_procedure
                                worklist_ref_priority

        Tables updated:         n/a

        Executing from:         General Lab worklist application
                                General Lab database building tool

        Special Notes:          

******************************************************************************/

 
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     ### 12/20/95 Dennis Boyer         Initial Release                     *
;    *001 04/28/00 Hal Patchen          Worklist rewrite                    *
;    *002 06/07/04 Erik Ankrom          Added Procedure List to Reply       *
;    *003 09/01/10 Steve Hendrick       Add automatic_worklist_ind to Reply *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

      drop program GLB_GET_WKLST_REQUEST:dba go    
      create program GLB_GET_WKLST_REQUEST:dba
     
/********************************************************************/
/*  Request Record                                                  */
/********************************************************************/
/*
      record request
      (
         1 worklist_ref_id = f8
      )
*/
/********************************************************************/
/*  Reply Record                                                    */
/********************************************************************/

      record reply
      (
         1 worklist_ref_id         = f8
         1 service_resource_cd     = f8
         1 service_resource_disp   = vc
         1 display                 = c20
         1 display_key             = c20
         1 parent_ind              = i2
         1 cumulative_ind          = i2
         1 in_lab_ind              = i2
         1 spacing_ind             = i2
         1 include_flag            = i2
         1 result_space_ind        = i2
         1 previous_result_ind     = i2
         1 look_ahead_hours        = i4
         1 printer                 = c20
         1 blank_lines             = i4
         1 copies                  = i4
         1 report_format_ind       = i2
         1 procedure_inc_flag      = i2
         1 priority_ind            = i2
         1 cross_resource_ind      = i2
         1 nbr_procs               = i4
         1 procedures[5]
           2 procedure_cd          = f8
           2 mnemonic              = vc
           2 grpdet                = i2     ; (tell if this is a group or a detail - 0: group; 1: detail
         1 nbr_pris                = i4
         1 priorities[5]
           2 priority_cd           = f8
         1 bre_procs[*]
           2 procedure_cd          = f8
           2 mnemonic              = vc
           2 grpdet                = i2
         1 filter_qc_assays_ind    = i2
%i cclsource:status_block.inc
         1 automatic_worklist_ind  = i2
      )

     #Script
      declare primary_synonym_cd = f8 with protect, noconstant(0.0)

      set proc_cnt = 0
      set bre_cnt = 0
      set pri_cnt = 0
      set reply->status_data->status = "F"

      SELECT into "nl:"
         c.code_value
      FROM
         code_value c
      WHERE
         c.code_set = 6011 and
         c.cdf_meaning = "PRIMARY"
      DETAIL
         primary_synonym_cd = c.code_value
      WITH
         nocounter

      SELECT into "nl:"
         w.worklist_ref_id,
         wp.worklist_ref_id,
         wp.catalog_cd,
         wp.seq,
         o.mnemonic,
         d.mnemonic,
         wpr.worklist_ref_id,
         wpr.priority_cd,
         join_path = decode(wp.seq,"wp",wpr.seq,"wpr","NONE")
      FROM 
         worklist_ref w,
         dummyt d4,
         worklist_ref_procedure wp,
         dummyt d1,
         order_catalog_synonym o,
         dummyt d2,
         discrete_task_assay d,
         dummyt d3,
         worklist_ref_priority wpr
      PLAN w
         where w.worklist_ref_id = request->worklist_ref_id
           and w.active_ind = 1
      JOIN d4
      JOIN (wp
         where w.worklist_ref_id = wp.worklist_ref_id
         JOIN (d1
         JOIN o
            where wp.catalog_cd = o.catalog_cd and
                  primary_synonym_cd = o.mnemonic_type_cd)
         JOIN (d2
         JOIN d 
            where wp.task_assay_cd = d.task_assay_cd))
      ORJOIN (d3 
         where d3.seq = 1
      JOIN wpr
         where w.worklist_ref_id = wpr.worklist_ref_id)

      HEAD REPORT
         reply->worklist_ref_id     = w.worklist_ref_id
         reply->display             = w.display
         reply->display_key         = w.display_key
         reply->service_resource_cd = w.service_resource_cd
         reply->parent_ind          = w.parent_ind
         reply->cumulative_ind      = w.cumulative_ind
         reply->in_lab_ind          = w.in_lab_ind
         reply->spacing_ind         = w.spacing_ind
         reply->include_flag        = w.include_flag
         reply->result_space_ind    = w.result_space_ind
         reply->previous_result_ind = w.previous_result_ind
         reply->look_ahead_hours    = w.look_ahead_hours
         reply->printer             = w.printer
         reply->blank_lines         = w.blank_lines
         reply->copies              = w.copies
         reply->report_format_ind   = w.report_format_ind
         reply->procedure_inc_flag  = w.procedure_inc_flag
         reply->priority_ind        = w.priority_ind
         reply->cross_resource_ind  = w.cross_resource_ind
         reply->filter_qc_assays_ind = w.filter_qc_assays_ind
         reply->automatic_worklist_ind = w.automatic_worklist_ind 

      DETAIL
         if (join_path = "wp")
            proc_cnt = proc_cnt + 1
            if (mod(proc_cnt,5) = 1 and proc_cnt != 1)
               stat = alter(reply->procedures, proc_cnt + 5)
            endif
            reply->nbr_procs = proc_cnt
            if ((reply->procedure_inc_flag = 0) or (reply->procedure_inc_flag = 1))
               reply->procedures[proc_cnt]->procedure_cd = wp.catalog_cd
               reply->procedures[proc_cnt]->grpdet = 0	; (mark this procedure as a group)
               if (reply->procedures[proc_cnt]->procedure_cd > 0)
                  reply->procedures[proc_cnt]->mnemonic = o.mnemonic
               endif
            elseif (reply->procedure_inc_flag = 2)
               reply->procedures[proc_cnt]->procedure_cd = wp.task_assay_cd
               reply->procedures[proc_cnt]->grpdet = 1       ; (mark this procedure as a detail)
               if (reply->procedures[proc_cnt]->procedure_cd > 0)
                  reply->procedures[proc_cnt]->mnemonic = d.mnemonic
               endif
            endif
         elseif (join_path = "wpr")
            pri_cnt = pri_cnt + 1
            if (mod(pri_cnt,5) = 1 and pri_cnt != 1)
               stat = alter(reply->priorities, pri_cnt + 5)
            endif
            reply->nbr_pris = pri_cnt
            reply->priorities[pri_cnt]->priority_cd = wpr.priority_cd
         endif
    WITH
         nocounter,
         outerjoin=d3,
         outerjoin=d1,
         outerjoin=d2,
         dontcare = o

    IF (curqual = 0)
       set reply->status_data->status = "Z"
    ELSE
       SET reply->status_data->status = "S"
    ENDIF
         
    if (reply->procedure_inc_flag = 0 and proc_cnt = 0)
       select into "nl:"
       from orc_resource_list rl
       where rl.service_resource_cd = reply->service_resource_cd
         and rl.active_ind = 1
       detail
         bre_cnt = bre_cnt + 1
         if (bre_cnt > size(reply->bre_procs, 5))
            stat = alterlist(reply->bre_procs, bre_cnt + 10)
         endif
         reply->bre_procs[bre_cnt]->procedure_cd = rl.catalog_cd
         reply->bre_procs[bre_cnt]->mnemonic = uar_get_code_display(rl.catalog_cd)
         reply->bre_procs[bre_cnt]->grpdet = 0
     endif

     set stat = alter(reply->procedures, proc_cnt)
     set stat = alter(reply->priorities, pri_cnt)
     set stat = alterlist(reply->bre_procs, bre_cnt)

#exit_script
      end go

;Generated by GNU enscript 1.6.4.
