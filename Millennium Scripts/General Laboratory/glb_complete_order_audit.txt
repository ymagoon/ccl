 ;;Solution_Test/script/PathNet -- Gen Lab/glb_complete_order_audit/glb_complete_order_audit.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1997 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;    *000 12/16/97 Hal Patchen          Initial Release                     *
;    *001 06/24/99 Derek Hime		    Internationalization of script      *
;         12/12/99 Kasie Sly		    Merge of 001 Internationalization   *
;~DE~************************************************************************
             drop program GLB_COMPLETE_ORDER_AUDIT:dba go
             create program GLB_COMPLETE_ORDER_AUDIT:dba

             paint

/********************************     i18n     ************************begin 001 mod block******/
; INCLUDE I18N FUNCTION DECLARATIONS
%i cclsource:i18n_uar.inc
; INITIALIZE VARIABLE THAT WILL KEEP THE HANDLE TO I18N DATA
set i18nHandle = 0
; CALL UAR_I18NLOCALIZATIONINIT
set h = uar_i18nlocalizationinit(i18nHandle,curprog,"",curcclrev)
/***********************************     i18n     ***********************************001**/
set JAN = uar_i18ngetmessage(i18nHandle,"jan","JAN")
set FEB = uar_i18ngetmessage(i18nHandle,"feb","FEB")
set MAR = uar_i18ngetmessage(i18nHandle,"mar","MAR")
set APR = uar_i18ngetmessage(i18nHandle,"apr","APR")
set MAY = uar_i18ngetmessage(i18nHandle,"may","MAY")
set JUN = uar_i18ngetmessage(i18nHandle,"jun","JUN")
set JUL = uar_i18ngetmessage(i18nHandle,"jul","JUL")
set AUG = uar_i18ngetmessage(i18nHandle,"aug","AUG")
set SEP = uar_i18ngetmessage(i18nHandle,"sep","SEP")
set OCT = uar_i18ngetmessage(i18nHandle,"oct","OCT")
set NOV = uar_i18ngetmessage(i18nHandle,"nov","NOV")
set DEC = uar_i18ngetmessage(i18nHandle,"dec","DEC")


set GLTitle = uar_i18ngetmessage(i18nHandle,"title","COMPLETE ORDER AUDIT")
set GLOrdFrom = uar_i18ngetmessage(i18nHandle,"ordfrom","Orders From:")
set GLTo = uar_i18ngetmessage(i18nHandle,"tto","To:")
set GLCorrect = uar_i18ngetmessage(i18nHandle,"correct","Correct (Y/N)?")

/******************************************************************end 001 mod block**********/
      /***********************************************************************
      * Initialize                                                           *
      ***********************************************************************/
         set ORDER_STATUS_CODESET        = 6004
         set ORDER_STATUS_COMPLETED_CDF  = "COMPLETED"
         declare order_status_completed_cd = f8
         set dt_day_beg = fillstring(2," ")
         set dt_month_beg = fillstring(3," ")
         set dt_year_beg = fillstring(4," ")
         set dt_day_end = fillstring(2," ")
         set dt_month_end = fillstring(3," ")
         set dt_year_end = fillstring(4," ")
         set tm_hour_beg = fillstring(2," ")
         set tm_min_beg = fillstring(2," ")
         set tm_hour_end = fillstring(2," ")
         set tm_min_end = fillstring(2," ")
         set dt_tm_beg = fillstring(23, " ")
         set dt_tm_end = fillstring(23, " ")

      /***********************************************************************
      * Load required code values                                            *
      ***********************************************************************/
         select into "nl:"
            cv.code_value

         from
            code_value cv

         where (cv.code_set = ORDER_STATUS_CODESET
             and cv.cdf_meaning = ORDER_STATUS_COMPLETED_CDF)
           and cv.active_ind = 1
           and cv.begin_effective_dt_tm <= cnvtdatetime(curdate, curtime3)
           and cv.end_effective_dt_tm >= cnvtdatetime(curdate, curtime3)

         detail
            if (cv.code_set = ORDER_STATUS_CODESET)
               case (cv.cdf_meaning)
                 of ORDER_STATUS_COMPLETED_CDF:
                    order_status_completed_cd = cv.code_value
               endcase
            endif
         with nocounter

      /***********************************************************************
      * Display screen                                                       *
      ***********************************************************************/
         call clear(1,1)
         call video(n)
         call box(1,1,3,80)
;001     call text(2,3, "COMPLETE ORDER AUDIT")
  	   call text(2,3, GLTitle)			;001
;001     call text(5,3, "Orders From:")
	   call text(5,3, GLOrdFrom)				;001
;001     call text(6,12, "To:")
	   call text(6,12, GLTo)						;001

      /***********************************************************************
      * Accept order date range.                                             *
      ***********************************************************************/
         set dt_day_beg = format(day(curdate), "##;P0;I")
;001         set dt_month_beg = format(cnvtdatetime(curdate, curtime3), "MMM;;D")
         set dt_month_beg = format(cnvtdatetime(curdate, curtime3), "@MONTHABBREV;;D")			;001
         set dt_year_beg = format(year(curdate), "####;P0;I")
         set tm_hour_beg = "00"
         set tm_min_beg = "00"
         set dt_day_end = format(day(curdate), "##;P0;I")
;001         set dt_month_end = format(cnvtdatetime(curdate, curtime3), "MMM;;D")
         set dt_month_end = format(cnvtdatetime(curdate, curtime3), "@MONTHABBREV;;D")			;001
         set dt_year_end = format(year(curdate), "####;P0;I")
         set tm_hour_end = format(hour(curtime), "##;P0;I")
         set tm_min_end = format(minute(curtime), "##;P0;I")

         call video(l)
         call text(5,16, dt_day_beg)
         call text(5,18, "-")
         call text(5,19, dt_month_beg)
         call text(5,22, "-")
         call text(5,23, dt_year_beg)
         call text(5,29, tm_hour_beg)
         call text(5,31, ":")
         call text(5,32, tm_min_beg)
         call text(6,16, dt_day_end)
         call text(6,18, "-")
         call text(6,19, dt_month_end)
         call text(6,22, "-")
         call text(6,23, dt_year_end)
         call text(6,29, tm_hour_end)
         call text(6,31, ":")
         call text(6,32, tm_min_end)
         go to CORRECT_YN

      #ACC_DATE_RANGE
         call accept(5,16, "NN", value(dt_day_beg) 
           where cnvtint(curaccept) between 1 and 31)
         set dt_day_beg = format(curaccept, "##;P0;I")
         call accept(5,19, "XXX;;CU", value(dt_month_beg) 
;001       where curaccept in ("JAN", "FEB", "MAR", "APR", "MAY", "JUN",
;001                           "JUL", "AUG", "SEP", "OCT", "NOV", "DEC"))
           where curaccept in (JAN, FEB, MAR, APR, MAY, JUN,							;001
                               JUL, AUG, SEP, OCT, NOV, DEC))							;001

         set dt_month_beg = curaccept
         call accept(5,23, "NNNN", value(dt_year_beg)) 
         set dt_year_beg = format(curaccept, "####;P0;I")
         call accept(5,29, "NN", value(tm_hour_beg)
           where cnvtint(curaccept) between 0 and 24) 
         set tm_hour_beg = format(curaccept, "##;P0;I")
         call accept(5,32, "NN", value(tm_min_beg) 
           where cnvtint(curaccept) between 0 and 59) 
         set tm_min_beg = format(curaccept, "##;P0;I")

         call accept(6,16, "NN", value(dt_day_end) 
           where cnvtint(curaccept) between 1 and 31)
         set dt_day_end = format(curaccept, "##;P0;I")
         call accept(6,19, "XXX;;CU", value(dt_month_end) 
;001       where curaccept in ("JAN", "FEB", "MAR", "APR", "MAY", "JUN",
;001                           "JUL", "AUG", "SEP", "OCT", "NOV", "DEC"))
           where curaccept in (JAN, FEB, MAR, APR, MAY, JUN,							;001
                                JUL, AUG, SEP, OCT, NOV, DEC))							;001

         set dt_month_end = curaccept
         call accept(6,23, "NNNN", value(dt_year_end)) 
         set dt_year_end = format(curaccept, "####;P0;I")
         call accept(6,29, "NN", value(tm_hour_end)
           where cnvtint(curaccept) between 0 and 24) 
         set tm_hour_end = format(curaccept, "##;P0;I")
         call accept(6,32, "NN", value(tm_min_end) 
           where cnvtint(curaccept) between 0 and 59) 
         set tm_min_end = format(curaccept, "##;P0;I")

      #CORRECT_YN
;001     call text(24,2, "Correct (Y/N)?")
	   call text(24,2, GLCorrect)				;001
         set Y = uar_i18ngetmessage(i18nHandle,"Y","Y")								;001
         set N = uar_i18ngetmessage(i18nHandle,"N","N")								;001

;001     call accept(24,17, "X;;CU", "Y" where curaccept in ("Y", "N"))
         call accept(24,17, "X;;CU", "Y" where curaccept in (Y, N))						;001
;001     if (curaccept = "N")
	   if (curaccept = N)													;001
            go to ACC_DATE_RANGE
         endif
         set dt_tm_beg = concat(dt_day_beg, "-", dt_month_beg, "-", 
           dt_year_beg, " ", tm_hour_beg, ":", tm_min_beg, ":00.00")
         set dt_tm_end = concat(dt_day_end, "-", dt_month_end, "-", 
           dt_year_end, " ", tm_hour_end, ":", tm_min_end, ":59.99")

      /***********************************************************************
      * Generate complete order audit                                        *
      ***********************************************************************/
         select
            accession = uar_fmt_accession(aor.accession,size(aor.accession,1)),
            o.order_mnemonic "#########################;;c",
;001        date_time = format(o.updt_dt_tm, "DD-MMM-YYYY HH:MM;;DT"),
		date_time = concat(cnvtupper(format(o.updt_dt_tm, "@MEDIUMDATE4YR;;q"))," ",format(o.updt_dt_tm, "@TIMENOSECONDS;;q")),		;001
            p.name_full_formatted "###################################;;c"

         from
            orders o,
            person p,
            accession_order_r aor

         plan o where o.order_status_cd = order_status_completed_cd
                  and o.updt_dt_tm >= cnvtdatetime(dt_tm_beg)
                  and o.updt_dt_tm <= cnvtdatetime(dt_tm_end)
         join p where p.person_id = o.person_id
         join aor where aor.order_id = o.order_id

         order by 
            accession,
            o.order_mnemonic

         with counter

         end
         go

;Generated by GNU enscript 1.6.4.
