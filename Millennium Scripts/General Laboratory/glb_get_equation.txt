 ;;Solution_Test/script/PathNet -- Gen Lab/glb_get_equation/glb_get_equation.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************
        Source file name:       GLB_GET_EQUATION.PRG
        Object name:            GLB_GET_EQUATION
        Request #:              250063

        Product:                General Lab
        Product Team:           GLB
        HNA Version:            500
        CCL Version:            4.0

        Program purpose:        Retrieve equation(s) associated with
                                the given procedure, test site, or both.

        Tables read:            equation
                                equation_component

        Tables updated:         n/a

        Executing from:         General Lab build/maintain equation tool

        Special Notes:          n/a
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     ### 02/28/96 Dennis Boyer        Initial Release                      *
;     001 03/28/98 Chad Moats          Added LookBack Minutes to reply      *
;     002 12/01/98 Chris Heil          Add support for unknown ages         *
;     003 09/22/2003 Fone Ji           Add look_time_direction_flag and     *
;                                      units_cd to reply                    *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

      drop program GLB_GET_EQUATION:dba go
      create program GLB_GET_EQUATION:dba

/********************************************************************/
/*  Request Record                                                  */
/********************************************************************/
/*
      record request
      (
         1 equation_id = f8
      )
*/

/********************************************************************/
/*  Reply Record                                                    */
/********************************************************************/

      record reply
      (
         1 equation_id             = f8
         1 task_assay_cd           = f8
         1 task_assay_disp         = vc
         1 service_resource_cd     = f8
         1 service_resource_disp   = vc
         1 unknown_age_ind         = i2                                        ;002
         1 age_from_units_cd       = f8
         1 age_from_minutes        = i4
         1 age_to_units_cd         = f8
         1 age_to_minutes          = i4
         1 sex_cd                  = f8
         1 sex_disp                = c20
         1 species_cd              = f8
         1 species_disp            = c20
         1 equation_description    = vc
         1 active_dt_tm            = dq8
         1 inactive_dt_tm          = dq8
         1 default_ind             = i2
         1 active_ind              = i2
         1 components[2]
           2 sequence              = i2
           2 result_status_cd      = f8
           2 included_assay_cd     = f8
           2 included_assay_disp   = vc
           2 name                  = vc
           2 default_value         = f8
           2 cross_drawn_dt_tm_ind = i2
           
           2 time_window_minutes   = i4
           2 time_window_back_minutes   = i4                                                            /*001*/
           2 result_req_flag       = i2
           2 variable_prompt       = vc
           2 constant_value        = f8
           2 component_flag        = i2
           2 look_time_direction_flag = i2
           2 units_cd              = f8
         1 status_data
           2 status                  = c1
           2 subeventstatus[1]
             3 OperationName         = c15
             3 OperationStatus       = c1
             3 TargetObjectName      = c15
             3 TargetObjectValue     = vc
         1 non_const_comp_cnt = i4
      )

     #Script
      set comp_cnt = 0
      set found_equation = "F"
      set failed = "F"
      set reply->status_data->status = "F"
      set sErrorMsg = FILLSTRING(255," ")    ;string containing error message
      set error_check = ERROR(sErrorMsg,1)   ;reset error queue to empty

      declare COMP_TYPE_CONSTANT = i2 with protect, constant(3)
         

      SELECT into "nl:"
/*
         e.*,
         ec.*
*/
         e.task_assay_cd,
         e.service_resource_cd,
         e.unknown_age_ind,                                ;002
         e.age_from_units_cd,
         e.age_from_minutes,
         e.age_to_units_cd,
         e.age_to_minutes,
         e.sex_cd,
         e.species_cd,
         e.equation_description,
         e.active_dt_tm,
         e.inactive_dt_tm,
         e.default_ind,
         ec.sequence,
         ec.result_status_cd,
         ec.included_assay_cd,
         ec.name,
         ec.default_value,
         ec.cross_drawn_dt_tm_ind,
         ec.look_time_direction_flag,
         ec.time_window_minutes,
         ec.time_window_back_minutes,                                                                   /*001*/
         ec.result_req_flag,
         ec.variable_prompt,
         ec.constant_value,
         ec.component_flag,
         ec.units_cd
      FROM
         equation e,
         equation_component ec
      PLAN e
         where request->equation_id = e.equation_id
      JOIN ec
         where e.equation_id = ec.equation_id
      HEAD REPORT
         found_equation = "Y"
         comp_cnt = 0
         reply->equation_id = request->equation_id
         reply->task_assay_cd = e.task_assay_cd
         reply->service_resource_cd = e.service_resource_cd
         reply->unknown_age_ind = e.unknown_age_ind                ;002
         reply->age_from_units_cd = e.age_from_units_cd
         reply->age_from_minutes = e.age_from_minutes
         reply->age_to_units_cd = e.age_to_units_cd
         reply->age_to_minutes = e.age_to_minutes
         reply->sex_cd = e.sex_cd
         reply->species_cd = e.species_cd
         reply->equation_description = e.equation_description
         reply->active_dt_tm = e.active_dt_tm
         reply->inactive_dt_tm = e.inactive_dt_tm
         reply->default_ind = e.default_ind
         reply->active_ind = e.active_ind
      DETAIL
         comp_cnt = comp_cnt + 1
         if (mod(comp_cnt,2) = 1 and comp_cnt != 1)
            stat =  alter(reply->components,comp_cnt + 2)
         endif
         reply->components[comp_cnt]->sequence = ec.sequence
         reply->components[comp_cnt]->result_status_cd = ec.result_status_cd
         reply->components[comp_cnt]->included_assay_cd = ec.included_assay_cd
         reply->components[comp_cnt]->name = ec.name
         reply->components[comp_cnt]->default_value = ec.default_value
         reply->components[comp_cnt]->cross_drawn_dt_tm_ind = ec.cross_drawn_dt_tm_ind
         reply->components[comp_cnt]->look_time_direction_flag = ec.look_time_direction_flag
         reply->components[comp_cnt]->time_window_minutes = ec.time_window_minutes
         reply->components[comp_cnt]->time_window_back_minutes = ec.time_window_back_minutes            /*001*/
         reply->components[comp_cnt]->result_req_flag = ec.result_req_flag
         reply->components[comp_cnt]->variable_prompt = ec.variable_prompt
         reply->components[comp_cnt]->constant_value = ec.constant_value
         reply->components[comp_cnt]->component_flag = ec.component_flag
         reply->components[comp_cnt]->units_cd= ec.units_cd
         
         if (ec.component_flag != COMP_TYPE_CONSTANT)
           reply->non_const_comp_cnt = reply->non_const_comp_cnt + 1
         endif
      WITH
         nocounter

      set error_check = ERROR(sErrorMsg,0)   ;get error message
      if (error_check != 0)
        set reply->status_data->status = "F"
        set reply->status_data->subeventstatus[1]->TargetObjectValue = sErrorMsg
        set failed = "T"
        go to exit_script
      endif

      if (comp_cnt = 0)
         go to no_components
      endif

      set stat = alter(reply->components, comp_cnt)
      go to exit_script

/********************************************************************/
/* Executed if equation components are not found.                   */
/********************************************************************/
     #no_components
      set reply->status_data->subeventstatus[1]->operationname =
         "get"
      set reply->status_data->subeventstatus[1]->operationstatus =
         "Z"
      set reply->status_data->subeventstatus[1]->targetobjectname =
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue =
         "equation_components table"
      set failed = "Z"
      go to exit_script

/********************************************************************/
/* Script exit.                                                     */
/********************************************************************/
     #exit_script
      if (failed = "T")
         set reply->status_data->status = "F"
      elseif (failed = "Z")
         set reply->status_data->status = "Z"
      else
         set reply->status_data->status = "S"
      endif

END
GO

;Generated by GNU enscript 1.6.4.
