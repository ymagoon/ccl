 ;;Solution_Test/script/PathNet -- Gen Lab/glb_print_text/GLB_PRINT_TEXT.INC Turn on black mode

;  Macro that formats a long string so it can be printed on a report page.
;  Put this in the HEAD REPORT section of a SELECT.
;  Set print_text equal to the text to be displayed before calling the print_text_routine macro.

;     *
;     * Initialize report variables
;     *
            LEFT_MARGIN		     = 10
            RIGHT_MARGIN             = 110,
            SPACES                   = 32,
            CR                       = 13,
            LINE                     = fillstring(125,"-"),
            print_text               = fillstring(10000, " "),
            text                     = fillstring(100, " "),
            max_text_len             = 0,
            ptr                      = 0,
            start_col                = 0,
            start_pos                = 0,
            last_space_pos           = 0,
            text_len                 = 0,

;     *
;     * Define Macro routines
;     *
            macro (print_text_routine)
               start_col = LEFT_MARGIN,
               start_pos = 0,
               last_space_pos = 0,
               text_len = 0,
               text = "",
               ptr = 1,
               max_text_len = size(trim(print_text),3),
               while (ptr <= max_text_len)
                  text_char = substring(ptr, 1, print_text),
                  if (ichar(text_char) < SPACES)
                     if (ichar(text_char) = CR)
                        if (start_pos > 0)
                           text = substring(start_pos, text_len, print_text),
                           col start_col, text
                        else
                           col start_col, " "
                        endif,
                        row+1,
                        start_col = LEFT_MARGIN,
                        start_pos = 0,
                        last_space_pos = 0,
                        text_len = 0,
                        text = ""
                     endif,
                     if (ichar(text_char) != CR)
                        if (text_len > 0)
                           text = substring(start_pos, text_len, print_text)
                           col start_col, text,
                           start_col = size(text,2) + LEFT_MARGIN
                        endif,
                        start_col = start_col + 1,
                        if (start_col >= RIGHT_MARGIN)
                           row+1,
                           start_col = LEFT_MARGIN
                        endif,
                        start_pos = ptr + 1,
                        start_pos = ptr + 1,
                        last_space_pos = 0,
                        text_len = 0,
                        text = ""
                     endif
                  endif,
                  if (ichar(text_char) >= SPACES)
                     if (start_pos = 0)
                        start_pos = ptr
                     endif,
                     if (ichar(text_char) = SPACES)
                        last_space_pos = ptr
                     endif
                     text_len = text_len + 1,
                     if ((start_col + text_len) >= RIGHT_MARGIN)
                        if (last_space_pos > 0)
                            text_len = (last_space_pos - start_pos) + 1,
                            ptr = last_space_pos
                        endif,
                        text = substring(start_pos, text_len, print_text),
                        col start_col, text,
                        row+1,
                        start_col = LEFT_MARGIN,
                        start_pos = 0,
                        last_space_pos = 0,
                        text_len = 0,
                        text = ""
                     endif                    
                  endif,
                  ptr = ptr + 1
               endwhile,
               if (text_len > 0)
                  text = substring(start_pos, text_len, print_text),
                  col start_col, text,
                  row+1,
                  start_col = LEFT_MARGIN,
                  start_pos = 0,
                  last_space_pos = 0,
                  text_len = 0,
                  text = ""
               endif,
               print_text = fillstring(10000, " ")
            endmacro

;Generated by GNU enscript 1.6.4.
