 ;;Solution_Test/script/PathNet -- Gen Lab/glb_add_qc_trouble_step/glb_add_qc_trouble_step.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1995 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/
/*****************************************************************************
        Source file name:       GLB_ADD_QC_TROUBLE_STEP.PRG
        Object name:            GLB_ADD_QC_TROUBLE_STEP
        Request #:              257501

        Product:                General Lab           
        Product Team:           GLB
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Insert a new QC Troubleshooting Step
                                into the database

        Tables read:            n/a

        Tables updated:         qc_trouble_step

        Executing from:         General Lab Build/Maintain QC Reference

        Special Notes:          n/a
******************************************************************************/
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     500 06/21/96 Chad Moats           Initial Release                     *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

      drop program GLB_ADD_QC_TROUBLE_STEP:dba go    
      create program GLB_ADD_QC_TROUBLE_STEP:dba
     
/********************************************************************/
/*  Request Record                                                  */
/********************************************************************/
/*
      record request                                      
      (
         1 service_resource_cd      = f8
         1 task_assay_cd            = f8
         1 troublesteps[*]
           2 error_flag             = i2
           2 step_nbr               = i4
           2 step_description       = vc
      ) 
*/

record reqinfo
( 1 commit_ind  = i2
  1 updt_id     = f8
  1 position_cd = f8
  1 updt_app    = i4
  1 updt_task   = i4
  1 updt_req    = i4
  1 updt_applctx= i4
)

/********************************************************************/
/*  Reply Record                                                    */
/********************************************************************/

      record reply
      ( 
         1 status_data
           2 status                = c1
           2 subeventstatus[1]
             3 OperationName       = c15
             3 OperationStatus     = c1
             3 TargetObjectName    = c15
             3 TargetObjectValue   = vc
      )


     #Script
      set failed = "F"
      set steps_cnt = size(request->troublesteps,5)
      set reply->status_data->status = "F"
      set idx = 0
      set next_code = 0.0


/*******************************************************************/
/* Get new trouble_id.                                             */
/*******************************************************************/
     for (idx = 1 to steps_cnt)
      SELECT into "nl:"
         y = seq(reference_seq,nextval)
             "##################;rp0"
      FROM
         dual
      DETAIL
         next_code = cnvtreal(y)
      WITH
         format, nocounter

/*******************************************************************/
/* Insert row into qc_trouble_step                                 */
/*******************************************************************/
      INSERT
         into qc_trouble_step q
      SET
        q.trouble_id          = next_code,
        q.service_resource_cd = request->service_resource_cd,
        q.task_assay_cd       = request->task_assay_cd,
        q.error_flag          = request->troublesteps[idx]->error_flag,
        q.step_nbr            = request->troublesteps[idx]->step_nbr,
        q.step                = request->troublesteps[idx]->step_description,
        q.active_ind          = 1,
        q.active_status_dt_tm = cnvtdatetime(curdate,curtime3),
        q.active_status_prsnl_id = reqinfo->updt_id,
        q.beg_effective_dt_tm = cnvtdatetime(curdate,curtime3),
        q.end_effective_dt_tm = cnvtdatetime("31 DEC 2100 00:00"),
        q.updt_dt_tm          = cnvtdatetime(curdate,curtime3),
        q.updt_id             = reqinfo->updt_id,
        q.updt_task           = reqinfo->updt_task,
        q.updt_cnt            = 0,
        q.updt_applctx        = reqinfo->updt_applctx
      PLAN q
          where q.error_flag = request->troublesteps[idx]->error_flag
          and q.step_nbr = request->troublesteps[idx]->step_nbr
      WITH
         nocounter

      if (curqual = 0)
         go to q_insert_failed
      endif

   endfor
   go to exit_script

/********************************************************************/ 
/* Executed if qc_trouble_step table sequnce is unsuccessful.       */
/********************************************************************/
     #seq_failed
      set reply->status_data->subeventstatus[1]->operationname = 
         "seq"
      set reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      set reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue = 
         "qc_trouble_step table"
      set failed = "T"
      go to exit_script

/********************************************************************/ 
/* Executed if qc_trouble_step table insert is unsuccessful.        */
/********************************************************************/
     #q_insert_failed
      set reply->status_data->subeventstatus[1]->operationname = 
         "insert"
      set reply->status_data->subeventstatus[1]->operationstatus = 
         "F"
      set reply->status_data->subeventstatus[1]->targetobjectname = 
         "table"
      set reply->status_data->subeventstatus[1]->targetObjectValue = 
         "qc_trouble_step table"
      set failed = "T"
      go to exit_script

/********************************************************************/ 
/* Script exit.                                                     */
/********************************************************************/
     #exit_script
      if (failed = "F")
         set reply->status_data->status = "S"
         commit
      else
         set reply->status_data->status = "F"
         rollback
      endif
END
GO     

;Generated by GNU enscript 1.6.4.
