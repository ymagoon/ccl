 ;;Solution_Test/script/PathNet -- Gen Lab/glb_upd_dta_end_dates/glb_upd_dta_end_dates.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-2000 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Author:                 Kasie Sly
        Date Written:           3/01/00
        Source file name:       glb_upd_dta_end_dates.prg
        Object name:            glb_upd_dta_end_dates
        Request #:              None

        Product:                PathNet General Lab
        Product Team:           PathNet General Lab
        HNA Version:            500
        CCL Version:            4.0

        Program purpose:        Update end_effective_dt_tm of DTAs from
                                12/31/2000 to 12/31/2100.

        Tables read:            discrete_task_assay

        Tables updated:         discrete_task_assay,
                                code_value

        Special Notes:

******************************************************************************/

/**************************************************************************
 **************************************************************************
 *            GENERATED MODIFICATION CONTROL LOG                          *
 **************************************************************************
 *                                                                        *
 * Mod Date     Engineer             Comment                              *
 * --- -------- -------------------- ------------------------------------ *
 * 001          Kasie D Sly           Initial Release                     *
 **************************************************************************
 ******************  END OF ALL MODCONTROL BLOCKS  ************************/

drop   program glb_upd_dta_end_dates:dba go
create program glb_upd_dta_end_dates:dba

  record dta_rec
  (
    1 qual[*]
      2 dta_cd		    = f8
      2 dta_disp            = vc
      2 end_effective_dt_tm = dq8
  )

  execute cclseclogin
  if (validate ( xxcclseclogin->loggedin, 99 ) != 1)
    go to exit_script
  endif
  
  set upd_ind = 0
  set new_dt_tm = cnvtdatetime ("31-DEC-2100 00:00:00.00")

  call clear (1,1)
  call video (n)
  call box   (1,1,3,80)
  call text  (2, 3, "GLB Update DTA End Effective Date Utility")
  call video (n)
  call text  (15, 3, "Retrieving Data...")

#RETRIEVE_DATA
  set dta_cnt = 0
  
  select into "nl:"
    dta = trim(UAR_GET_CODE_DISPLAY(dta.task_assay_cd),3),
    dta.end_effective_dt_tm "DD-MMM-YYYY HH:MM:SS;;D",
    dta.task_assay_cd
  from discrete_task_assay dta
  where datetimecmp(dta.end_effective_dt_tm ,CNVTDATETIME("31-DEC-2000")) = 0
    and dta.active_ind = 1

  detail
    dta_cnt = dta_cnt + 1
    if (mod(dta_cnt, 50) = 1)
      stat = alterlist(dta_rec->qual, dta_cnt + 50)
    endif

    dta_rec->qual[dta_cnt]->dta_cd = dta.task_assay_cd
    dta_rec->qual[dta_cnt]->dta_disp = dta
    dta_rec->qual[dta_cnt]->end_effective_dt_tm = cnvtdatetime(dta.end_effective_dt_tm)

  foot report
    stat = alterlist(dta_rec->qual, dta_cnt)
  with nocounter

#INFO_PROMPT
  call clear (1,1)
  call video (n)
  call box   (1,1,3,80)
  call text  (2, 3, "GLB DTA End Effective Date Update Utility")
  call video (n)
  call text  (5, 3,  "(A)udit Mode, (U)pdate Mode, or (E)xit?")
  call text  (10, 3, "Select the Audit Mode to provide a list of all discrete task assays          ")
  call text  (11, 3, "with an end effective date of 12/31/2000.  This report will be displayed     ")
  call text  (12, 3, "to the screen.  Please print the report as a record of changes to be made    ")
  call text  (13, 3, "during the Update Mode.                                                      ")
  call text  (15, 3, "Select the Update Mode to update the end effective date for all discrete     ")
  call text  (16, 3, "task assays from 12/31/2000 to 12/31/2100.  A report of items changed will   ")
  call text  (17, 3, "be displayed on the screen after the update is complete.  Please print this  ")
  call text  (18, 3, "report as a record of the changes made.                                      ")
  call accept(5, 43, "P;;CU")

  case (curaccept)
  of "A":  go to display_audit
  of "a":  go to display_audit
  of "U":  go to update_info
  of "u":  go to update_info
  of "E":  go to exit_script
  of "e":  go to exit_script
  else go to info_prompt
  endcase

#UPDATE_INFO
  update into discrete_task_assay dta set
    dta.end_effective_dt_tm = cnvtdatetime("31-DEC-2100 00:00:00.00"),
    dta.updt_dt_tm = cnvtdatetime(curdate, curtime3),
    dta.updt_task = 0,
    dta.updt_id = 0
  where datetimecmp(dta.end_effective_dt_tm ,CNVTDATETIME("31-DEC-2000")) = 0
    and dta.active_ind = 1
  with nocounter

  update into code_value cv set
    cv.end_effective_dt_tm = cnvtdatetime("31-DEC-2100 00:00:00.00"),
    cv.updt_dt_tm = cnvtdatetime(curdate, curtime3),
    cv.updt_task = 0,
    cv.updt_id = 0
  where cv.code_set = 14003
    and cv.end_effective_dt_tm = cnvtdatetime("31-DEC-2000 00:00:00.00")
    and cv.active_ind = 1
  with nocounter

  commit
set upd_ind = 1

#DISPLAY_AUDIT
  select
    d1.seq
  from (dummyt d1 with seq = value(dta_cnt))
  plan d1
  order by
    dta_rec->qual[d1.seq]->dta_disp
  head report
    call center("GLB DTA End Effective Date Update Utility", 0, 125)
    row +2
      dtaCol = 1
      mnemonicCol = 30
      dateCol = 70
    row + 1
      col dtaCol "Task_Assay_CD"
      col mnemonicCol "Mnemonic"
      col dateCol "End_Effective_DT_TM"
    row +1
      col dtaCol "--------------------------------------------------------------------------------------------------"
    row + 2
  detail
      col 0 dta_rec->qual[d1.seq]->dta_cd
      col 30 dta_rec->qual[d1.seq]->dta_disp
      col 0 dta_rec->qual[d1.seq]->dta_cd
      my_date_time = format(cnvtdatetime(dta_rec->qual[d1.seq]->end_effective_dt_tm), "DD-MMM-YYYY HH:MM:SS;;D")
      col 70
	if (upd_ind=0)
        my_date_time
	else
        new_dt_tm "DD-MMM-YYYY HH:MM:SS;;D"
      endif		
	row + 1
  with nocounter

if (upd_ind=0)
  go to info_prompt
else
  exit_script
endif

#EXIT_SCRIPT
call clear (1,1)
call video (n)
end go

;Generated by GNU enscript 1.6.4.
