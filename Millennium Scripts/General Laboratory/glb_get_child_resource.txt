 ;;Solution_Test/script/PathNet -- Gen Lab/glb_get_child_resource/glb_get_child_resource.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-1999 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/
 
/*****************************************************************************
        Source file name:       GLB_GET_CHILD_RESOURCE.PRG
        Object name:            GLB_GET_CHILD_RESOURCE
        Request #:              250191
 
        Product:                General Lab
        Product Team:           GLB
        HNA Version:            500
        CCL Version:            4.0
 
        Program purpose:        Return all active child service resources for
                                requested service resource.
 
        Tables read:            resource_group
                                service_resource
 
        Tables updated:         n/a
 
        Executing from:         General Lab pending inquiry application
 
        Special Notes:          n/a
 
******************************************************************************/
 
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     000 09/03/99 Kurt Gugler          Initial Release                     *
;     002 11/19/02 Chad Moats           Added service resource security     *
;~DE~************************************************************************
;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************
 
drop program GLB_GET_CHILD_RESOURCE:dba go
create program GLB_GET_CHILD_RESOURCE:dba
 
%i cclsource:pcs_resource_security.inc
 
/***********************************************************************
* Request Record                                                       *
***********************************************************************/
 
/*
RECORD request
( 1 service_resource_cd = f8
)
*/
 
/***********************************************************************
* Reply Record                                                         *
***********************************************************************/
 
RECORD reply
( 1 qual[*]
    2 service_resource_cd        = f8
    2 service_resource_disp      = vc
    2 service_resource_mean      = vc
    2 service_resource_type_cd   = f8
    2 cs_login_loc_cd            = f8
 
%i cclsource:status_block.inc
)
 
 
SET reply->status_data->status = "F"
SET sr_cnt = 0
SET success_ind = 0
SET sErrorMsg = FILLSTRING(255," ")
SET error_check = ERROR(sErrorMsg,1)
 
declare dServResCd              = f8 with protect, noconstant(0.0)
 
CALL InitResourceSecurity(1)
 
SELECT INTO "nl:"
  r.child_service_resource_cd,
  s.service_resource_cd
FROM
  resource_group r,
  service_resource s
PLAN r
WHERE
  (request->service_resource_cd = r.parent_service_resource_cd
  OR request->service_resource_cd = 0.0)
  AND r.root_service_resource_cd = 0.0
  AND r.active_ind = 1
JOIN s
WHERE
  s.service_resource_cd = r.child_service_resource_cd
  AND s.activity_type_cd IN
  (SELECT
     cvat.code_value
   FROM
     code_value cvat
   WHERE cvat.cdf_meaning IN ("GLB", "BB", "HLA"))
  AND s.active_ind = 1
ORDER BY
  r.parent_service_resource_cd
DETAIL
  dServResCd = r.child_service_resource_cd
  if (IsResourceViewable(dServResCd) = TRUE)
    sr_cnt = sr_cnt + 1
    stat = ALTERLIST(reply->qual,sr_cnt)
    reply->qual[sr_cnt]->service_resource_cd = r.child_service_resource_cd
    reply->qual[sr_cnt]->service_resource_type_cd = s.service_resource_type_cd
    reply->qual[sr_cnt]->cs_login_loc_cd = s.cs_login_loc_cd
  endif
WITH nocounter
 
SET error_check = ERROR(sErrorMsg,0)
IF (error_check != 0)
  SET reply->status_data->status = "F"
  SET reply->status_data->subeventstatus[1]->operationname = "select"
  SET reply->status_data->subeventstatus[1]->operationstatus = "F"
  SET reply->status_data->subeventstatus[1]->targetobjectname = "resource_group"
  SET reply->status_data->subeventstatus[1]->TargetObjectValue = sErrorMsg
  SET success_ind = 0
  GO TO EXIT_SCRIPT
ENDIF
 
SET final_cnt = SIZE(reply->qual,5)
SET success_ind = 1
 
#EXIT_SCRIPT
 
IF (success_ind = 1)
  IF (final_cnt = 0)
    IF (GetResourceSecurityStatus(0) = "F")
      SET reply->status_data->status = GetResourceSecurityStatus(0)
      call PopulateResSecStatusBlock(0)
    ELSE
      SET reply->status_data->status = "Z"
    ENDIF
  ELSE
    SET reply->status_data->status = "S"
  ENDIF
ENDIF
 
END
GO
 

;Generated by GNU enscript 1.6.4.
