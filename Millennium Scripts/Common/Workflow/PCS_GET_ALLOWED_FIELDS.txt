 ;;Solution_Test/script/PathNet -- Gen Lab/pcs_get_allowed_fields/pcs_get_allowed_fields.prg Turn on black mode

/*~BB~************************************************************************
      *                                                                      *
      *  Copyright Notice:  (c) 1983 Laboratory Information Systems &        *
      *                              Technology, Inc.                        *
      *       Revision      (c) 1984-2005 Cerner Corporation                 *
      *                                                                      *
      *  Cerner (R) Proprietary Rights Notice:  All rights reserved.         *
      *  This material contains the valuable properties and trade secrets of *
      *  Cerner Corporation of Kansas City, Missouri, United States of       *
      *  America (Cerner), embodying substantial creative efforts and        *
      *  confidential information, ideas and expressions, no part of which   *
      *  may be reproduced or transmitted in any form or by any means, or    *
      *  retained in any storage or retrieval system without the express     *
      *  written permission of Cerner.                                       *
      *                                                                      *
      *  Cerner is a registered mark of Cerner Corporation.                  *
      *                                                                      *
  ~BE~***********************************************************************/

/*****************************************************************************

        Source file name:       pcs_get_allowed_fields.prg
        Object name:            pcs_get_allowed_fields
        Request #:              1052552

        Product:                PathNet Common                             
        Product Team:           PathNet Common
        HNA Version:            500
        CCL Version:            4.0
        
        Program purpose:        Retrieve PCS_DEMOGRAPHIC_FIELD.

        Tables read:            code_value_group, pcs_demographic_field, code_value

        Tables updated:         None
        Executing from:         PCSDemographics.ocx

        Special Notes:          ?

******************************************************************************/

 
;~DB~************************************************************************
;    *                      GENERATED MODIFICATION CONTROL LOG              *
;    ************************************************************************
;    *                                                                      *
;    *Mod Date     Engineer             Comment                             *
;    *--- -------- -------------------- ----------------------------------- *
;     ### 03/01/06 HP2519               Initial Release                     *
;~DE~************************************************************************


;~END~ ******************  END OF ALL MODCONTROL BLOCKS  ********************

  drop program pcs_get_allowed_fields:dba go
  create program pcs_get_allowed_fields:dba

  /***********************************************************************
  *   Request Record                                                     *
  ***********************************************************************/
  /*
  record request
  (
  	1 app_key = vc
  )
  */

  /***********************************************************************
  *   Reply Record                                                       *
  ***********************************************************************/
  record reply
  ( 
    1  field_qual[*]
      2  field_cd = f8
      2  field_disp = c40
      2  field_desc = vc
      2  field_mean = c12
      2  field_active_ind = i2
      2  collation_name = vc
      2  current_field_cd = f8
      2  default_label_text= vc
      2  historical_ind = i2
      2  preview_text = vc      
%i cclsource:status_block.inc
  )
  

  /***********************************************************************
  *   Script                                                             *
  ***********************************************************************/
  set modify predeclare
  
  declare lReplyCnt = I4 with protect,noconstant(0)
  declare stat = I4 with protect,noconstant(0)
  declare sCclError = VC with protect,noconstant("")
  declare lAppCodeSet = i4 with protect, constant(4000880)
  declare dApplication = F8 with protect, noconstant(0.0)
  
  
  	set stat = error(sCclError,1) ;Reset the last CCL error
  	set dApplication = uar_get_code_by("MEANING", lAppCodeSet, nullterm(request->app_key))
  	
    select into "nl:" p.field_cd
    ;Need to join to code_value to get active_ind
    from code_value_group cvg, pcs_demographic_field p, code_value cv
    plan cvg where cvg.code_set = 4000821 and cvg.parent_code_value = dApplication
    join p where p.field_cd = cvg.child_code_value
    join cv where cv.code_value = p.field_cd
    head report
    	lReplyCnt = 0
    detail
    	lReplyCnt = lReplyCnt + 1
    	if (mod(lReplyCnt,10)=1)
    		stat=alterlist(reply->field_qual,lReplyCnt + 9)
    	endif
    	reply->field_qual[lReplyCnt]->field_cd = p.field_cd
   		reply->field_qual[lReplyCnt]->field_active_ind = cv.active_ind
    	reply->field_qual[lReplyCnt]->collation_name = p.collation_name
    	reply->field_qual[lReplyCnt]->current_field_cd = p.current_field_cd
    	reply->field_qual[lReplyCnt]->default_label_text = p.default_label_text
    	reply->field_qual[lReplyCnt]->historical_ind = p.historical_ind
    	reply->field_qual[lReplyCnt]->preview_text = p.preview_text
    foot report
    	stat = alterlist(reply->field_qual,lReplyCnt)
  	with nocounter
	if (error(sCclError,1)!=0)
	  set reply->status_data->status = "F"
	  set reply->subeventstatus[1]->OperationName="SELECT"
	  set reply->subeventstatus[1]->OperationStatus="F"
	  set reply->subeventstatus[1]->TargetObjectName="PCS_DEMOGRAPHIC_FIELD"
	  set reply->subeventstatus[1]->TargetObjectValue = sCclError
	elseif (lReplyCnt > 0)
	      set reply->status_data->status = "S"
    else
      set reply->status_data->status = "Z"
    endif

  set modify nopredeclare

  end go 

;Generated by GNU enscript 1.6.4.
