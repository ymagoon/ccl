/*
 *  ---------------------------------------------------------------------------------------------
 *  Script Name:  oru_optum_lab_out
 *  Description:  Optum Script for Lab results outbound
 *  Type:         Modify Object Script
 *  ---------------------------------------------------------------------------------------------
 *  Author:         Tony McArtor
 *  Library:        OEOCF23ORUORU
 *  Creation Date:  09/18/13 
 *  ---------------------------------------------------------------------------------------------
 *  Mod#  Date    Author       Description & Requestor Information
 *
 * 1.  09/18/13  T. McArtor   Copy of oru_bayc_outv7 script with modifications and added Outreach Block Lab 
 * 2.  01/24/14  H. Kaczmarczyk Added a block for all Lab results except AP
 * 3.  01/30/14  H Kaczmarczyk Adding Coding to remove DONOTSEND w LOINC values, renumber OBX segments
 *                                                        and ignore messages when all OBX segments have been removed.
 *  ---------------------------------------------------------------------------------------------
*/

if (size(oen_reply->RES_ORU_GROUP->OBX_GROUP, 5)= 0)
  set OenStatus->Ignore = 1
endif

/*********************Add the PCPE to PD1:4 H Kaczmarczyk 5/13/13*******************************************/

declare pt_enctr_id = f8
declare phys_NPInbr = vc
declare phys_name_first = vc
declare phys_name_last = vc

set pt_enctr_id = oen_reply->cerner->encntr_prsnl_info->encntr [1]->encntr_id
set phys_reltn = uar_get_code_by ("MEANING",333,"PCPE")
set phys_number = uar_get_code_by ("DISPLAY",263,"NPI Number")

select into "nl:"
  p.alias
  , pr.name_last
  , pr.name_first
from 
  encntr_prsnl_reltn e
  , prsnl_alias p
  , prsnl pr
plan e
  where e.encntr_id = pt_enctr_id  
    and e.encntr_prsnl_r_cd = phys_reltn  
	and e.manual_create_ind = 0 
join p
  where e.prsnl_person_id = p.person_id   
    and p.alias_pool_cd = phys_number
join pr
  where p.person_id = pr.person_id
detail
  phys_NPInbr = p.alias
  phys_name_first = pr.name_first
  phys_name_last = pr.name_last  
with nocounter

/*********************10/27/16  Add Mod 10: PD1 Clear all old life time PCP identifiers Hope K.************************/

set pd1_size = size(oen_reply->PERSON_GROUP [1]->PAT_GROUP [1]->PD1, 5)

if (pd1_size > 0)
  set stat = alterlist(oen_reply->PERSON_GROUP [1]->PAT_GROUP [1]->PD1, 0)
endif
 
if (phys_NPInbr not in("", NULL, " "))
  set oen_reply->PERSON_GROUP [1]->PAT_GROUP [1]->PD1 [1]->pat_primary_provider [1]->id_nbr =  phys_NPInbr
  set oen_reply->PERSON_GROUP [1]->PAT_GROUP [1]->PD1 [1]->pat_primary_provider [1]->last_name =  phys_name_last
  set oen_reply->PERSON_GROUP [1]->PAT_GROUP [1]->PD1 [1]->pat_primary_provider [1]->first_name = phys_name_first
  set oen_reply->PERSON_GROUP [1]->PAT_GROUP [1]->PD1 [1]->pat_primary_provider [1]->assign_auth->name_id  = "NPI Number"
  set oen_reply->PERSON_GROUP [1]->PAT_GROUP [1]->PD1 [1]->pat_primary_provider [1]->name_type = "Personnel"
  set oen_reply->PERSON_GROUP [1]->PAT_GROUP [1]->PD1 [1]->pat_primary_provider [1]->id_type = "National Provider Identifier"
endif

/**********************Block all Lab except AP  H. Kaczmarczyk    01/24/14****************************************/

/* Remove results with an alias of "DONOTSEND" */
declare index_to_remove = i4
set obx_size = size(oen_reply->RES_ORU_GROUP->OBX_GROUP, 5)
set x = 1

; find the OBX segment to remove
while(x <= obx_size)
  set oen_reply->RES_ORU_GROUP ->OBX_GROUP [x]->OBX->set_id = cnvtstring(x)
  set index_to_remove = -1
  
  if (oen_reply->RES_ORU_GROUP->OBX_GROUP [x]->OBX->observation_id->alt_identifier = "DONOTSEND")
    set index_to_remove = x
  endif
  
; remove the unwanted OBX segment
  execute oencpm_msglog(build("remove index is: ", index_to_remove))
  
  if (index_to_remove > -1)
    set stat = alterlist(oen_reply->RES_ORU_GROUP-> OBX_GROUP, obx_size -1, x -1)
    set obx_size = obx_size -1
  else
    set x = x +1
  endif
endwhile

declare listsize = i4
declare accessionid = vc
declare ordalias = vc

set x = 1
set listsize = size(oen_reply->RES_ORU_GROUP [1]->OBR [1]->filler_field1,5)

execute oencpm_msglog(build("LISTSIZE = ",listsize))

for (x = 1 to listsize)
  if (oen_reply->RES_ORU_GROUP [1]->OBR [1]->filler_field1 [x]->field_type = "HNA_ACCNID")
    set accessionid = oen_reply->res_oru_group [1]->obr [1]->filler_field1 [x]->value 
  endif
endfor

execute oencpm_msglog(build("Accession id = ",accessionid))

select
  ao.order_id
 o.alias
from 
  accession_order_r ao
  , order_alias o
plan ao
  where ao.accession_id = cnvtint(accessionid)
join o 
  where o.order_id = ao.order_id and o.alias not in("",null," ")
detail
  ordalias = o.alias
with nocounter
   
execute oencpm_msglog(build("AP Order Alias = ",ordalias))

set oen_reply->RES_ORU_GROUP [1]->OBR [1]->placer_ord_nbr [1]->entity_id = ordalias
#end_of_script